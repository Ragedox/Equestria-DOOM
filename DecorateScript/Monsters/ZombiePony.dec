ACTOR EquestriaZombieMan : DoomEquestriaMonster Replaces ZombieMan
{
	Monster
	
	Health 20
	
	Radius 20
	Height 56
	
	Speed 8
	
	PainChance 200
	
	+FLOORCLIP
	+CASTSPRITESHADOW
    
	SeeSound "grunt/sight"
    AttackSound "grunt/attack"
    PainSound "grunt/pain"
    DeathSound "grunt/death"
    ActiveSound "grunt/active"
    
	Obituary "$OB_ZOMBIE" // "%o was killed by a zombieman."
    DropItem "EquestriaClip"
    
	States
    {
	Spawn:
        POSS A 10 A_Look
        Loop

	See:
        POSS AABBCCDD 4 A_Chase
        Loop
   
	Missile:
        POSS E 10 A_FaceTarget
        POSS F 8 A_PosAttack
        POSS E 8
        Goto See
    
	Pain:
        POSS G 3
        POSS G 3 A_Pain
        Goto See
   
	Death:
		POSS H 0 { bSPRITEFLIP = randompick(0,1); }
		
        POSS H 5
        POSS I 5 A_Scream
        POSS J 5 A_NoBlocking
        POSS K 5
        POSS L -1
        Stop
    
	XDeath:
		TNT1 A 0 { bSPRITEFLIP = randompick(0, 1); }
		
		POSS U 0
		{
			A_SpawnItemEx("XDeathGibber",0,0,16,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_SpawnItemEx("BloodExplosionSphere",0,0,16,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_XScream;
			A_NoBlocking;
		}
        POSS U -1
        Stop
		
	XDeath.GibsOff:
		POSS M 0
		{
			A_XScream;
			A_NoBlocking;
		}
		POSS MNOPQRST 5
		POSS U -1
		Stop
		
	MidAir:
		POSS J 1 A_SetRoll((roll + 20))
		Loop
		
	Death.Plasma:
		POSS H 0 { bSPRITEFLIP = randompick(0,1); }
		POSS H 5
        POSS I 5
        POSS J 5 A_NoBlocking
        POSS K 5
        POSS L -1
        Stop
    
	Raise:
        POSS K 5
        POSS JIH 5
        Goto FlipRaise
    }
}

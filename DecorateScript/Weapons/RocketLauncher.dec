actor EquestriaRocketLauncher : DoomEquestriaWeapon replaces RocketLauncher
{
//$Title Rocket Launcher
	Weapon.BobStyle alpha
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "RocketAmmo"
	
	+WEAPON.NOAUTOFIRE
	
	Inventory.PickupMessage "$GOTLAUNCHER"
	Inventory.PickupSound "pickup/rocklauncher"
	
	Tag "$TAG_ROCKETLAUNCHER"
	
	States
	{

	Spawn:
		LAUN A -1
		Stop
	
	Select:
		TNT1 A 0 A_TakeInventory("KickCheck", 999)
	
		MISG A 0 A_SetCrossHair(13)
		MISG A 1 A_Raise
		Loop
	
	Deselect:
		TNT1 A 0 A_TakeInventory("KickCheck", 999)
		
		MISG A 1 A_Lower
		Wait
	
	Ready:
		TNT1 A 0 A_JumpIfInventory("KickCheck", 1, "KickAttack")
		
		MISG A 1 A_WeaponReady
		Loop
	
	Fire:
		MISG A 4 A_GunFlash
		MISG B 4 
		MISG B 12 
		{
			A_WeaponOffset(0, 40, WOF_KEEPX | WOF_INTERPOLATE);
			A_FireMissile;
		}
		MISG B 0
		{
			A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH);
			A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
			A_ReFire;
		}
		Goto Ready
	
	Flash:
		MISF A 4 
		MISF B 4 Bright
		MISF C 4 Bright 
		MISF D 4 
		Goto LightDone
	
	}
}

actor DERocket : Rocket replaces Rocket
{
  +ForceXYBillBoard
  -Randomize
  States
  {
  Spawn:
    MISL A random(2,4) Bright
	TNT1 A 0 
	{
		A_SpawnItemEx("FirePieceSpawn",random(-3,-3),random(-3,3),random(-3,3));
		A_SpawnItemEx("FirePieceSpawn",random(-3,-3),random(-3,3),random(-3,3));
		A_SpawnItemEx("DERocketSmoke",random(-3,-3),random(-3,3),random(-3,3));
	}
    Loop
  Death:
	TNT1 A 0 A_SpawnItemEx("ScorchFloorCheck", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
	BAL2 C 4 Bright 
	{
		A_Explode;
		A_Quake(3, 4, 0, 100);
		A_Quake(2, 3, 0, 200);
		A_Quake(1, 2, 0, 300);
	}
    MISL B 4 Bright
	MISL C 4 Bright
    MISL DDDDDDDDDD 1 Bright A_FadeOut(0.1)
	Stop
  }
}

// Decorate
ACTOR KickWeapon : Weapon
{
	States
	{
	Select:
		TNT1 A 1 A_Raise
		Wait
	Deselect:
		TNT1 A 1 A_Lower
		Wait
	Ready:
        TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_JumpIf(momZ > 0, "AirKick")
		TNT1 A 0 A_JumpIf(momZ < 0, "AirKick")
        TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "BerserkerKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_AlertMonsters (500, 0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("KickAttack", 0, 0, 0, -7)
        KICK H 2 
		KICK IGFEDCB 2
		TNT1 A 0 
		KICK A 0 A_TakeInventory("Kicking", 1)
		Goto InvisLoop
	BerserkerKick:
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_AlertMonsters (500, 0)
		KICK BCD 1
        TNT1 A 0 A_FireCustomMissile("SuperKickAttack", 0, 0, 0, -7)
        KICK H 2
		KICK IGFEDCB 2
		TNT1 A 0 SetPlayerProperty(0, 0, 0)
		KICK A 0 A_TakeInventory("Kicking", 1)
		Goto InvisLoop
	AirKick:
	    TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "SuperAirKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_Recoil(-6)
		KICK JKLMN 1
        TNT1 A 0 A_FireCustomMissile("AirKickAttack", 0, 0, 0, -31)
        KICK O 2 A_AlertMonsters (500, 0)
		KICK PQRST 2
		KICK A 0 A_TakeInventory("Kicking", 1)
		Goto InvisLoop
	SuperAirKick:
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_Recoil(-6)
		KICK JKLMN 1
        TNT1 A 0 A_FireCustomMissile("SuperAirKickAttack", 0, 0, 0, -31)
        KICK O 2 A_AlertMonsters (500, 0)
		KICK PQRST 2
		KICK A 0 A_TakeInventory("Kicking",1)
		Goto InvisLoop
	InvisLoop:
	Fire:
		TNT1 A 1
		Loop
	}
}

ACTOR KickAttack : FastProjectile
{
	Radius 6
	Height 6
	DamageType Kick
	Projectile 
	+FORCEXYBILLBOARD
	+NOEXTREMEDEATH
	RenderStyle Add
	Alpha 0.6
    Damage 8
    Speed 30
	SeeSound "None"
	DeathSound "None"
	Decal "None"
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT
        TNT1 A 1
		Stop
	Death:
		PUFF A 3 A_PlaySound("player/cyborg/fist")
		PUFF BCD 3
		Stop
	}
}


ACTOR SuperKickAttack : KickAttack
{
	-NOEXTREMEDEATH
    Damage 30
	DamageType ExtremePunches
}

ACTOR AirKickAttack: KickAttack
{
	Radius 12
	Height 12
    Damage 5
 }

ACTOR SuperAirKickAttack: KickAttack
{
	Radius 12
	Height 12
	-NOEXTREMEDEATH
	DamageType ExtremePunches
    Damage 40
}

ACTOR Kicking : Inventory
{
	Inventory.MaxAmount 1
}
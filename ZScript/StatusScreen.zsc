class Equestria_StatusScreen : DoomStatusScreen
{
	override void Ticker(void)
	{
		// counter for general background animation
		bcnt++;  
	
		if (bcnt == 1)
			S_ChangeMusic("");
		
		if (bcnt == 24)
			StartMusic();
	
		checkForAccelerate();
		bg.updateAnimatedBack();
	
		switch (CurState)
		{
		case StatCount:
			updateStats();
			break;
		
		case ShowNextLoc:
			updateShowNextLoc();
			break;
		
		case NoState:
			updateNoState();
			break;

		case LeavingIntermission:
			// Hush, GCC.
			break;
		}
	}
	
	override void drawStats (void)
	{
		// line height
		int lh = 20;
		
		drawLF();
	
		let tcolor = Font.CR_RED;

		Font textFont = BigFont;
		int statsx = SP_STATSX;

		int timey = SP_TIMEY;
		if (wi_showtotaltime)
			timey = min(SP_TIMEY, 200 - 2 * lh);

		// Check if everything fits on the screen.
		String percentage = wi_percents? " 0000%" : " 0000/0000";
		int perc_width = textFont.StringWidth(percentage);
		int k_width = textFont.StringWidth("$TXT_IMKILLS");
		int i_width = textFont.StringWidth("$TXT_IMITEMS");
		int s_width = textFont.StringWidth("$TXT_IMSECRETS");
		int allwidth = max(k_width, i_width, s_width) + perc_width;
		if ((SP_STATSX*2 + allwidth) > 320)	// The content does not fit so adjust the position a bit.
			statsx = max(0, (320 - allwidth) / 2);

		DrawText (textFont, tcolor, statsx - 50, SP_STATSY+20, "$TXT_IMKILLS");
		DrawText (textFont, tcolor, statsx - 50, SP_STATSY+lh+20, "$TXT_IMITEMS");
		DrawText (textFont, tcolor, statsx - 50, SP_STATSY+2*lh+20, "$TXT_IMSECRETS");
		DrawText (textFont, tcolor, SP_TIMEX, timey + lh, "$TXT_IMTIME");
		if (wbs.partime) DrawText (textFont, tcolor, 160 + SP_TIMEX + 25, timey + lh, "$TXT_IMPAR");
			 
		drawPercent (textFont, 320 - statsx + 50, SP_STATSY+20, cnt_kills[0], wbs.maxkills, true, tcolor);
		drawPercent (textFont, 320 - statsx + 50, SP_STATSY+lh+20, cnt_items[0], wbs.maxitems, true, tcolor);
		drawPercent (textFont, 320 - statsx + 50, SP_STATSY+2*lh+20, cnt_secret[0], wbs.maxsecret, true, tcolor);
			 
		// This really sucks - not just by its message - and should have been removed long ago!
		// To avoid problems here, the "sucks" text only gets printed if the lump is present, this even applies to the text replacement.
			 
		if (cnt_time >= wbs.sucktime * 60 * 60 && wbs.sucktime > 0 && Sucks.IsValid())
		{ // "sucks"
			int x = 160 - SP_TIMEX;
			int y = timey;
			DrawText (textFont, tcolor, x  - textFont.StringWidth("$TXT_IMSUCKS"), y - textFont.GetHeight() - 2 +20,	"$TXT_IMSUCKS");
		}

		if (wi_showtotaltime)
			drawTimeFont (textFont, 160 - statsx, timey + lh, cnt_total_time, tcolor);

		if (wbs.partime)
			drawTimeFont (textFont, 320 - statsx + 25, timey + lh, cnt_par, tcolor);
	}
}
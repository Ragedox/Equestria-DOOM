/* Copyright Alexander 'm8f' Kromm (mmaulwurff@gmail.com) 2018-2019
 *
 * This file is part of Death-flip.
 *
 * Death-flip is free software: you can redistribute it and/or modify it under
 * the terms of the GNU General Public License as published by the Free Software
 * Foundation, either version 3 of the License, or (at your option) any later
 * version.
 *
 * Death-flip is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * Death-flip.  If not, see <https://www.gnu.org/licenses/>.
 */

class df_EventHandler : EventHandler
{
	override void OnRegister()
	{
		blacklist = new("df_StringSet").init();
	}

	override void WorldThingDied(WorldEvent e)
	{
		if (!e.thing || blacklist.contains(e.thing.GetClassName())) return;

		bool flipped = CV_DEATHFLIP
                 ? (isPrevFlipped = !isPrevFlipped)
                 : random(0, 1);

		if (flipped)
			e.thing.bSPRITEFLIP = 1;
	}

	override void WorldThingRevived(WorldEvent e)
	{
		if (!e.thing || blacklist.contains(e.thing.GetClassName())) return;
		
		e.thing.bSPRITEFLIP = 0;
	}

	private df_StringSet blacklist;
	private bool isPrevFlipped;
}

class df_StringSet
{

// public: /////////////////////////////////////////////////////////////////////

  df_StringSet init()
  {
    // must be sorted.
    values.push("EquestriaChaingunGuy"); // Doom
    values.push("EquestriaCyberdemon");  // Doom

    return self;
  }

  bool contains(string s)
  {
    // binary search
    int L = 0;
    int R = values.size() - 1;

    while (L <= R)
    {
      int m = (L + R) / 2;
      string current = values[m];
      if      (current <  s) { L = m + 1; }
      else if (current >  s) { R = m - 1; }
      else if (current == s) { return true; }
    }
    return false;
  }

// private: /////////////////////////////////////////////////////////////////////

  private Array<String> values;

} // class df_StringSet
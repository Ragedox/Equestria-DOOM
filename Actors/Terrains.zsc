//Core Splash definition for others to inherit from

Class CoreSplashChunk : PuffSparkles
{
	Default
	{
		BounceType "None";
		Renderstyle "Translucent";
		Alpha 0.7;
		
		Gravity 0.5;
		Scale 1;
		+ROLLSPRITE +ROLLCENTER
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay { bSPRITEFLIP = randompick(0, 1); A_SetScale(scale.x = frandom(0.5, 1.0)); A_SetRoll(random(0, 359));}
		
		NWAT ABCD random(2,6);
		NWAT ABCD random(2,6) A_JumpIf(waterlevel > 1, "Null");
		Goto Spawn+4;
		
	Death:
		TNT1 A 1;
		Stop;
	}
	
	override void PostBeginPlay()
	{
		bSPRITEFLIP = randompick(0, 1);
	}
}

Class BigSplashChunk : CoreSplashChunk
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay { bSPRITEFLIP = randompick(0, 1); A_SetScale(scale.x = frandom(0.5, 1.0)); A_SetRoll(random(0, 359));}
		BWAT A 1 A_JumpIf(waterlevel > 1, "Null");
		BWAT A 0 A_SetRoll(roll + 1);
		Goto Spawn+1;
	}
}

Class CoreSplashBase : Actor
{
	Default
	{
		Radius 2; Height 4;
		
		Renderstyle "Translucent";
		Alpha 0.75;
		
		+NOBLOCKMAP
		+NOINTERACTION
	}
	override void PostBeginPlay()
	{
		bSPRITEFLIP = randompick(0, 1);
	}
}

Class SplashSmoke : Actor
{
	Default
	{
		RenderStyle "Translucent";
		Alpha 0.7;
	
		Speed 1; VSpeed 1;
	
		+NOBLOCKMAP
		+NoInteraction
		+ForceXYBillBoard
	}
	States
	{
	Spawn:
		BSMK A 0 NoDelay { bSPRITEFLIP = randompick(0,1); }
		
		BSMK AAABBBCCDDEE 3 A_Fadeout(0.05);
		BSMK E 1 A_FadeOut(0.05);
		Wait;
	}
}


//Blood

Class DEBloodChunk : CoreSplashChunk
{
	Default { TRANSLATION "193:207=32:47"; Gravity 0.75; Decal "BloodSplat7"; } 
}

Class DEBloodBase : DEBloodChunk
{
	States
	{
	Spawn:
		SPSH EFGHIJK random(3,6) A_FadeOut(0.05);
		Stop;
	}
}

//Lava

Class DELavaBase : CoreSplashBase
{
	Default
	{
		Alpha 1.0;
		+Bright
		TRANSLATION "112:122=160:165", "123:127=43:47";
	}
	States
	{
	Spawn:
		NLAV ABCDEF random(3,5);
		Stop;
	}
}

//Nukage

Class DENukageBase : CoreSplashBase
{
	Default
	{
		Translation "192:207=112:127";
		Alpha 1.0;
		+Bright
	}
	States
	{
	Spawn:
		NLAV ABCDEF random(3,5);
		Stop;
	}
}

//Slime - Brown

Class DESlimeBrownChunk : CoreSplashChunk
{
	Default	{	TRANSLATION "192:207=136:151";	}
}

Class DESlimeBrownBase : DESlimeBrownChunk
{
	States
	{
	Spawn:
		NSL1 EFGHIJK random(3,6) A_FadeOut(0.05);
		Stop;
	}
}


//Sludge - GreenyBrown

Class DESludgeChunk : CoreSplashChunk
{
	Default
	{
		Renderstyle "Translucent";
		Alpha 0.8;
		TRANSLATION "192:207=136:151";
	}
}

Class DESludgeBase : CoreSplashBase
{
	Default
	{
		Renderstyle "Translucent";
		Alpha 0.9;
	}
	States
	{
	Spawn:
		NSL2 EFGHIJK random(2,3) A_FadeOut(0.05);
		Stop;
	}
}

//Water Normal

Class DEWaterSplashChunk : CoreSplashChunk
{}

Class DEWaterSplashBase : CoreSplashBase
{
	States
	{
	Spawn:
		SPLS FEDCBA	1;
		SPLS ABCDEF	random(2,4);
		Stop;
	}
}
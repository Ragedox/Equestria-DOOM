Class GenericGib : DebrisCore
{
	Default
	{
		Decal "BloodSplat";
		BounceSound "gore/impact";
		//DeathSound "gore/impact";

		Height 4; Radius 2;
		
		Mass 20;
		Speed 8;
		Gravity 0.6;
	  
		+USEBOUNCESTATE +BOUNCEONWALLS +BOUNCEONFLOORS +BOUNCEONACTORS
		BounceType "Doom";
		BounceFactor 0.25;
		
		-COUNTKILL
		
		DebrisCore.RollSpeed 15;
	}
	States
	{
	Spawn:
		GORE A 0 NoDelay A_Jump(256, 1,2,3);
		Wait;
		#### A 0;
		Goto Spawn2;
		#### B 0;
		Goto Spawn2;
		#### C 0;
		Goto Spawn2;
		
	Spawn2:
		#### # 0 A_JumpIf(GetCVar("GoreToggle")== 0, "Bye");
		#### # 0 { A_SetRoll(random(0,359)); bSPRITEFLIP = randompick(0, 1); }
		Goto Roll;
	
	Bounce:
		#### # 0
		{
			A_SprayDecal("BloodSplat", 20);
			A_SetRoll(roll + random(-100, 100));
		}
	Roll:
		#### # 8 A_SpawnItemEx("BloodTrail",random(-4, 4),random(-4, 4),random(-4, 4));
		Loop;
	
	Death:
	Crash:
		#### # 350;
		#### # 0 A_JumpIf(GetCVar("GibsStayForever") == 0, "FadeOut");
		#### # 0; //A_JumpIf(GetCVar("GibsStayForever") == 2, "CheckSight");
		Loop;
		
	FadeOut:
		#### # 1 A_FadeOut(0.1);
		Wait;
	
	CheckSight:
		#### # 35 A_CheckSight("FadeOut");
		Loop;
	
	Bye: TNT1 A 0;
		Stop;
	}
}

Class EquestriaBlood : Blood replaces Blood
{
	Default
	{
		Decal "BloodSplat";
		Height 4; Radius 2;
		
		Gravity 0.75;
		Speed 8;
		Mass 2;
		
		+ForceXYBillBoard
		+NOTELEPORT +NOBLOCKMAP
		+CORPSE
		+MISSILE
		+MOVEWITHSECTOR
	}
	States
	{
	Spawn:
		BLUD CBA 30;
		Wait;
		
	Bounce.Wall:
	Death:
		BLUD A 2;
		Stop;
		
	Crash:
		TNT1 A 0 ACS_NamedExecute("CheckLiquid");
		TNT1 A 0 { bSPRITEFLIP = randompick(0, 1); A_Stop(); A_SetRenderStyle(1.0, STYLE_Normal); }
		
		BLUD DEF 2 A_FadeOut(frandom(0.001, 0.1));
		BLUD G 1 A_FadeOut(0.005);
		Wait;
	}
}

Class BloodTrail : EquestriaBlood
{
	Default
	{
		RenderStyle "Translucent";
		Alpha 0.75;
		
		Gravity 0.2;
		Scale 0.6;
	}
	States
	{
	Crash:
		TNT1 A 0 ACS_NamedExecute("CheckLiquid");
		TNT1 A 0 { bSPRITEFLIP = randompick(0, 1); A_Stop(); A_SetRenderStyle(1.0, STYLE_Normal); }
		
		BLUD DEF 1 A_FadeOut(frandom(0.01, 0.1));
		BLUD G 1 A_FadeOut(0.01);
		Wait;
	}
}

Class SmallBloodTrail : BloodTrail
{
	Default
	{
		Gravity 0.1;
		Scale 0.5;
	}
	States
	{
	Spawn:
		BLUD CBA 15;
		Wait;
	
	Crash:
		TNT1 A 0 ACS_NamedExecute("CheckLiquid");
		TNT1 A 0 { bSPRITEFLIP = randompick(0, 1); A_Stop(); A_SetRenderStyle(1.0, STYLE_Normal); }
		
		POB2 A 1 A_FadeOut(0.025);
		Wait;
	}
}

Class TinyGenericGib : GenericGib
{
	Default
	{
		BounceSound "";
		BounceType "None";
		BounceFactor 0;
		Speed 10;
		DebrisCore.RollSpeed 25;
		
		+SLIDESONWALLS
	}
	States
	{
	Spawn:
		GORE D 0 NoDelay A_Jump(256, 1,2);
		Wait;
		#### D 0;
		Goto Spawn2;
		#### E 0;
		Goto Spawn2;
	
	Spawn2:
		#### # 0 A_JumpIf(GetCVar("GoreToggle")== 0, "Bye");
		#### # 0 { A_SetRoll(random(0,359)); bSPRITEFLIP = randompick(0, 1); }
		Goto Roll;
		
	Roll:
		#### # 70 A_SpawnItem("SmallBloodTrail");
		Loop;
	}
}

Class NoBlood_TinyGenericGib : TinyGenericGib
{
	Default { Decal ""; -UseBounceState }
	States
	{
	Spawn:
		GORE D 0 NoDelay A_Jump(256, 1,2);
		Wait;
		#### D 0;
		Goto Spawn2;
		#### E 0;
		Goto Spawn2;
	
	Spawn2:
		#### # 0 A_JumpIf(GetCVar("GoreToggle")== 0, "Bye");
		#### # 0 { A_SetRoll(random(0,359)); bSPRITEFLIP = randompick(0, 1); }
		Goto Roll;
		
	Roll:
		#### # 1;
		Loop;
	}
}

Class BigGoreRandom : RandomSpawner
{
	Default
	{
		DropItem "BigGenericGib";
		DropItem "BigGibGore2";
	}
}

Class BigGenericGib : GenericGib
{
	Default
	{
		Height 10; Radius 5;
		
		Mass 50;
		Speed 15;
		Gravity 0.75;
	  
		BounceFactor 0.4;
		
		DebrisCore.RollSpeed 25;
	}
	States
	{
	Spawn:
		BGOR A 0;
		Goto Spawn2;
	
	Spawn2:
		#### # 0 A_JumpIf(GetCVar("GoreToggle")== 0, "Bye");
		#### # 0 { A_SetRoll(random(0,359)); bSPRITEFLIP = randompick(0, 1); }
		Goto Roll;
	
	Bounce:
		#### # 0
		{
			A_SprayDecal("BloodSplat", 20);
			A_SetRoll(roll + random(-100, 100));
		}
	Roll:
		#### # 2 A_SpawnItemEx("BloodTrail", random(-10, 10), random(-10, 10), random(-10, 10));
		Loop;
		
	Death:
		BGOR B 350 A_SetRoll(0);
		BGOR B 0 A_JumpIf(GetCVar("GibsStayForever") == 0, "FadeOut");
		BGOR B 0; //A_JumpIf(GetCVar("GibsStayForever") == 2, "CheckSight");
		Loop;
	}
}

Class BigGibGore2 : BigGenericGib
{
	States
	{
	Spawn:
		BGO2 A 0;
		Goto Spawn2;
	
	Death:
		BGO2 B 350 A_SetRoll(0);
		BGO2 B 0 A_JumpIf(GetCVar("GibsStayForever") == 0, "FadeOut");
		BGO2 B 0; //A_JumpIf(GetCVar("GibsStayForever") == 2, "CheckSight");
		Loop;
	}
}

Class XDeathGibber : Actor
{
	Default
	{
		Radius 20; Height 56;
		
		+NOCLIP
		+NOBLOCKMAP
		+NOTELEPORT
		+MOVEWITHSECTOR
		+NOGRAVITY
		+THRUACTORS
		+DONTSPLASH
	}
	States
	{
	Spawn:
		TNT1 A 0 A_JumpIf(GetCvar("GoreToggle") == 0, "Bye");
		
		TNT1 A 0
		{
			for (int i = 0; i < 10; i++)
			{
				A_SpawnProjectile("GenericGib", frandom(8, 56), frandom(-20, 20), random (0, 359), CMF_AIMDIRECTION, random (0, -180));	
				A_SpawnProjectile("TinyGenericGib", frandom(8, 56), frandom(-20, 20), random (0, 359), CMF_AIMDIRECTION, random (0, -180));
			}
		}
		Stop;
		
	Bye: TNT1 A 0;
	Stop;
	}
}

Class BigGibber : XDeathGibber
{
	States
	{
	Spawn:
		TNT1 A 0 A_JumpIf(GetCvar("GoreToggle")== 0, "Bye");
		
		TNT1 AAAAA 0 A_SpawnProjectile("BigGoreRandom", frandom(8, 32), frandom(-40, 40), random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		Stop;
	}
}

Class BloodExplosion : XDeathGibber
{
	States
	{
	Spawn:
		TNT1 A 0 A_JumpIf(GetCVar("GoreToggle")== 0, "Bye");
		
		TNT1 AAAAA 0 A_SpawnProjectile("BloodTrail", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, -180));
		TNT1 BBBBBB 0 A_SpawnProjectile("SmallBloodTrail", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, -180));
		TNT1 CCCC 0 A_SpawnProjectile("EquestriaBlood", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, -180));
		Stop;
	}
}


Class BloodExplosionSphere : BloodExplosion
{
	States
	{
	Spawn:
		TNT1 A 0 A_JumpIf(GetCVar("GoreToggle") == 0, "Bye");

		TNT1 AAAAA 0 A_SpawnProjectile("BloodTrail", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		TNT1 BBBBBB 0 A_SpawnProjectile("SmallBloodTrail", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		TNT1 CCCC 0 A_SpawnProjectile("EquestriaBlood", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		Stop;
	}
}
Class DoomEquestriaMonster : Actor abstract
{
	Default
	{
		Monster;
		
		PainThreshold 4;
		
		+CastSpriteShadow
		+AvoidMelee
		
		BloodType "Equestria_BloodEnemies", "Equestria_BloodSplatter", "EquestriaBloodAxe";
		
		DoomEquestriaMonster.TrainerBuff false;
	}
	
	bool bTrainerBuff;
	property TrainerBuff: bTrainerBuff;
	double OriginalHeight, OriginalRadius;
	double dOldSpeed;
	int iSweatTimer;
	bool bHasMissileEvenMore;
	
	override void PostBeginPlay()
	{
		OriginalHeight = height;
		OriginalRadius = radius * 2;
		
		dOldSpeed = speed;
		
		if (bMISSILEEVENMORE)
			bHasMissileEvenMore = 1;
		
		Super.PostBeginPlay();
	}
	
	/*override void Tick()
	{
		Super.Tick();
		if(health <= woundhealth && health > 0 && bCORPSE == false)
			sprite = getspriteindex("SPOS");
	}*/
	
	/*void A_SmartChase()
	{
		
	}*/
	
	override void Tick()
	{
		Super.Tick();
		
		if (!bTrainerBuff)
			return;
		
		if(health > 0 && CheckProximity("PossessedTrainer", 1024, 1, CPXF_CHECKSIGHT) && iSweatTimer >= 8)
		{
			A_SpawnProjectile("DEWaterSplashChunk", frandom(height*0.5, height), 0, frandom(0,360), CMF_AIMDIRECTION, frandom (0, -180));
			iSweatTimer = 0;
		}
		iSweatTimer++;
	}
	
	void EQ_Chase(statelabel melee = "Melee", statelabel missile = "Missile", int flags = 0)
	{
		if (CheckSight(target)== true)
			A_Chase(melee, missile, flags);
		else
			A_Wander();

		if(CheckProximity("PossessedTrainer", 1024, 1, CPXF_CHECKSIGHT | CPXF_SETMASTER) && bTrainerBuff == 1)
		{
			speed = dOldSpeed + 6;
			bMISSILEEVENMORE = 1;
		}
		else
		{
			speed = dOldSpeed;
			if(bHasMissileEvenMore == 0)
				bMISSILEEVENMORE = 0;
		}
	}
	
	States
	{
	XDeath:
	Crash.Berserk:
		#### # 0 //Simple Gibs
		{
			A_NoBlocking(); A_XScream(); A_SetRoll(0);
			
			if(GetCVar("GoreToggle") == 1)
			{
				int gorequantity;
				gorequantity = random(8, 16);
				
				for (int i = 0; i < gorequantity; i++)
					A_SpawnItemEx("GenericGib",
						FRandom((-OriginalRadius), OriginalRadius), FRandom((-OriginalRadius), OriginalRadius), frandom(OriginalHeight/3, OriginalHeight),
						frandom(2,4), frandom(2,4), frandom(4,8),
						frandom(0, 360), SXF_USEBLOODCOLOR | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_NOCHECKPOSITION);
				
				for (int i = 0; i < gorequantity; i++)
					A_SpawnItemEx("TinyGenericGib",
						FRandom((-OriginalRadius), OriginalRadius), FRandom((-OriginalRadius), OriginalRadius), frandom(OriginalHeight/3, OriginalHeight),
						frandom(2,4), frandom(2,4), frandom(4,8),
						frandom(0, 360), SXF_USEBLOODCOLOR | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_NOCHECKPOSITION);

				for (int i = 0; i < gorequantity; i++)
				{
					A_SpawnItemEx("Equestria_TinyBlood",
						frandom((-OriginalRadius), OriginalRadius),frandom((-OriginalRadius), OriginalRadius),frandom(OriginalHeight/3, OriginalHeight),
						frandom(0, 4),frandom(0, 4),frandom(0, 8),frandom(0,360),
						SXF_USEBLOODCOLOR | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_NOCHECKPOSITION);
					
					A_SpawnItemEx("Equestria_SmallBlood",
						frandom((-OriginalRadius), OriginalRadius),frandom((-OriginalRadius), OriginalRadius),frandom(OriginalHeight/3, OriginalHeight),
						frandom(0, 4),frandom(0, 4),frandom(0, 8),frandom(0,360),
						SXF_USEBLOODCOLOR | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_NOCHECKPOSITION);
					
					A_SpawnItemEx("Equestria_Blood",
						frandom((-OriginalRadius), OriginalRadius),frandom((-OriginalRadius), OriginalRadius),frandom(OriginalHeight/3, OriginalHeight),
						frandom(1, 2),frandom(1, 2),frandom(0, 8),frandom(0,360),
						SXF_USEBLOODCOLOR | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_NOCHECKPOSITION);
				}
				
				for (int i = 0; i < 4; i++)
				{
					A_SpawnItemEx("Equestria_SmallBlood",
						frandom((-OriginalRadius), OriginalRadius),frandom((-OriginalRadius), OriginalRadius),frandom(OriginalHeight/3, OriginalHeight),
						frandom(0, 1),frandom(0, 1),frandom(0, 2),frandom(0,360),
						SXF_USEBLOODCOLOR | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_NOCHECKPOSITION);
					
					A_SpawnItemEx("Equestria_Blood",
						frandom((-OriginalRadius), OriginalRadius),frandom((-OriginalRadius), OriginalRadius),frandom(OriginalHeight/3, OriginalHeight),
						frandom(0, 1),frandom(0, 1),frandom(0, 2),frandom(0,360),
						SXF_USEBLOODCOLOR | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_NOCHECKPOSITION);
				}
				
				return ResolveState("Classic.XDeath");
			}
			
			if(GetCVar("GoreToggle") == 2)
				return ResolveState("Real.XDeath");
				
			return ResolveState("Classic.XDeath");
		}
		Wait;
	}
}
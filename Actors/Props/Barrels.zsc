Class EquestriaExploBarrel : ExplosiveBarrel replaces ExplosiveBarrel
{
	Default
	{
		Radius 10; Height 32;
		
		DamageType "BarrelExp";
		DamageFactor "Nukage", 0.0; PainChance "Nukage", 0.0;
		
		+NOBLOOD
		+ALLOWPAIN
		-OLDRADIUSDMG
		+CANPASS

		Painchance 128;
	}
	States
	{
	Pain:
		BAR1 A 0 A_SpawnItem("SplashSmoke", 0, 40);
	Spawn:
		BAR1 A 8;
		Loop;
	
	Death.BarrelExp:
	XDeath.BarrelExp:
	Death:
		BAR1 AAAA 1 A_SpawnProjectile("Smoke", random(0, 32), 0, random (0, 359), CMF_AIMDIRECTION, random (0, -180));

		BAR1 AAAAA 1 A_SetScale(Scale.X + 0.05, 1.0, 1, true);
		BEXP AAAAA 1 Bright A_SetScale(Scale.X + 0.2, Scale.Y + 0.025);
		
		TNT1 AAAAAAAAAA 0
		{
			A_SpawnProjectile("Smoke", random(0, 32), random(-20, 20), random (0, 359), CMF_AIMDIRECTION, random (0, 359));
			A_SpawnProjectile("ExplosionSparkle", random(0,32), random(-20, 20), random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		}
		
		TNT1 BBBBBBBBBBBBBBB 0 A_SpawnProjectile("BarrelDebris", random(0, 32), random(-20, 20), random (0, 359), CMF_AIMDIRECTION, random (-10, -90));
		
		BEXP B 6 Bright
		{
			A_Scream();
			A_Explode();
			A_SetScale(1.5, 1.5);
			A_QuakeEx(3, 3, 3, 4, 0, 300, "", 0,0,0,0, 1);
			
			bFORCEXYBILLBOARD = 1;
		}
		
		TNT1 AAAA 0 A_SpawnProjectile("SplashSmoke", random(0, 32), random (-40, 40), random (0, 359), CMF_AIMDIRECTION, random(0, -90));
		
		BEXP C 1 A_FadeOut(0.1);
		Wait;
		
	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(150, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,20,0,1);
			A_Recoil(7.5);
		}
		Goto Death;
		
	XDeath:
		TNT1 AAAA 0 A_SpawnProjectile("Smoke", random(0, 32), 0, random (0, 359), CMF_AIMDIRECTION, random (0, -180));
		
		BAR1 AAA 1 A_SetScale(Scale.X + 0.4, Scale.Y + 0.025);
		BEXP A 4 Bright;
		
		TNT1 AAAAAAAAAA 0
		{
			A_SpawnProjectile("Smoke", random(0, 32), random(-20, 20), random (0, 359), CMF_AIMDIRECTION, random (0, 359));
			A_SpawnProjectile("ExplosionSparkle", random(0,32), random(-20, 20), random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		}
		
		TNT1 BBBBBBBBBBBBBBBBBBBB 0 A_SpawnProjectile("BarrelDebris", random(0, 32), random(-20, 20), random (0, 359), CMF_AIMDIRECTION, random (-10, -90));
		
		BEXP B 8 Bright
		{
			A_Scream();
			A_Explode();
			A_SetScale(1.5, 1.5);
			A_QuakeEx(3, 3, 3, 4, 0, 300, "", 0,0,0,0, 1);
			
			bFORCEXYBILLBOARD = 1;
		}
		
		TNT1 AAAA 0 A_SpawnProjectile("SplashSmoke", random(0, 32), random (-40, 40), random (0, 359), CMF_AIMDIRECTION, random(0, -90));
		
		BEXP C 1 A_FadeOut(0.1);
		Wait;
	}
}

Class BarrelDebris : DebrisCore
{
	Default
	{
		Radius 5; Height 10;
		Mass 50;
		
		Speed 12;
		
		Gravity 0.5;
		
		+ROLLSPRITE +ROLLCENTER
		DebrisCore.RollSpeed 35;
		
		+BOUNCEONWALLS +BOUNCEONFLOORS +USEBOUNCESTATE
		BounceType "Doom";
		BounceFactor 0.25;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay { A_SetRoll(random(0,359)); bSPRITEFLIP = randompick(0,1); }
		TNT1 A 0 A_Jump(256, 1, 2, 3, 4);
		Wait;
		EXPD A 0;
		Goto Roll;
		EXPD B 0;
		Goto Roll;
		EXPD C 0;
		Goto Roll;
		EXPD D 0;
		Goto Roll;
	
	Bounce:
		#### # 0 A_SetRoll(roll + random(-100, 100));
	Roll:
		#### # 1 A_SpawnItem("BarrelDebrisSmoke");
		Loop;
		
	Death:
	Crash:
		#### # 350;
		#### # 0 A_JumpIf(GetCVar("GibsStayForever") == 0, "FadeOut");
		
	FadeOut:
		#### # 1 A_FadeOut(0.1);
		Wait;
	}
}

Class BarrelDebrisSmoke : EquestriaRocketSmoke
{	Default{	Alpha 0.35;	Scale 0.2;	VSpeed 0;}	}

Class AcidBarrel : EquestriaExploBarrel
{	Default { +LOOKALLAROUND }
	States
	{
	Pain:
		BAR1 A 0 A_SpawnProjectile("AcidDrop",height, frandom(radius * -1, radius), frandom(0, 360), CMF_AIMDIRECTION, frandom (-90, -45));
	Spawn:
		BAR2 AB 6 A_LookEx(LOF_NOSOUNDCHECK | LOF_NOSEESOUND,0,512,0,360);
		Loop;
		
	See:
		BAR2 AB 6 A_Jump(10, "Pain");
		Goto Spawn;
	
	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(150, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,20,0,1);
			A_Recoil(7.5);
		}
		Goto XDeath;
		
	Death:
	Death.BarrelExp:
		BAR2 # 5;
		BAR2 C 5 A_Scream();
	XDeath:
	XDeath.BarrelExp:
		BAR2 DDDDD 1 Bright A_SpawnProjectile("AcidDrop",frandom(0, 32), frandom(radius * -1, radius), frandom(0, 360), CMF_AIMDIRECTION, frandom (-180, 90));
		BAR2 E 5 Bright
		{
			A_Explode(5, 64, XF_NOTMISSILE);
			A_SpawnItem("AcidPuddle",0,1);
			bFORCEXYBILLBOARD = 1;
			for (int i = 0; i < 25; i++)
			{
				A_SpawnProjectile("AcidDrop",frandom(0, 32), frandom(radius * -1, radius), frandom(0, 360), CMF_AIMDIRECTION, frandom (-180, 90));
			}
		}
		
		TNT1 AAAA 0 A_SpawnProjectile("SplashSmoke", random(0, 32), random (-40, 40), random (0, 359), CMF_AIMDIRECTION, random(0, -90));
		
		BAR2 F 1 Bright A_FadeOut(0.1);
		Wait;
	}
}

Class AcidDrop : EquestriaBlood
{
	Default
	{
		Decal "";
		Height 6; Radius 3;
		RenderStyle "Add";
		Gravity 1;
		Speed 8;
		Mass 35;
		Damage 10;
		DamageType "Nukage";

		+NOTELEPORT +NOBLOCKMAP
		+CORPSE
		+MISSILE
		+MOVEWITHSECTOR
		+ROLLSPRITE +ROLLCENTER
		+BRIGHT
		
		Translation "176:191=112:127";
	}
	States
	{
	Spawn:
		BLUD C 0 NoDelay A_SetRoll(Frandom(0, 360));
		BLUD C 1 A_SetRoll(roll + 8, SPF_INTERPOLATE);
		Wait;

	Death:
		BLUD CBA 70;
		Stop;
		
	Crash:
		TNT1 A 0 A_SpawnItemEX("AcidPuddle", 0,0,0,0,0,0,0, SXF_TRANSFERROLL);
		Stop;
	}
}

Class AcidPuddle : Actor
{
	Default
	{
		Radius 20; Height 5;
		Damage 5;
		DamageType "Nukage";
		Translation "176:191=112:127";
		+MOVEWITHSECTOR
		+CORPSE
		+NOTELEPORT +NOBLOCKMAP
		+FLATSPRITE
		+ROLLSPRITE +ROLLCENTER
		+BRIGHT
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256, 1,2,3,4);
		Wait;
		FLAT E 0;
		Goto InFloor;
		FLAT F 0;
		Goto InFloor;
		FLAT G 0;
		Goto InFloor;
		FLAT H 0;
		Goto InFloor;
		
	InFloor:
		FLAT ########## 35 Bright A_Explode(5, 20, XF_NOTMISSILE | XF_NOSPLASH);
		FLAT #################### 1 A_FadeOut(0.025);
		
	Dry:
		FLAT # 350;
		FLAT # 0 A_JumpIf(GetCVar("GibsStayForever")== 0, 1);
		Goto Dry;
		FLAT # 1 A_FadeOut(0.025);
		Wait;
	}
}

Class EmptyBarrel : EquestriaExploBarrel
{
	Default
	{
		+NOBLOOD
		-ALLOWPAIN
		-OLDRADIUSDMG
	}
	States
	{
	Spawn:
		BAR1 A -1;
		Stop;
		
	Death:
		BEXP ABCDE 4;
		Stop;
	}
}

Class EquestriaBurningBarrel : BurningBarrel replaces BurningBarrel
{
	Default
	{
		Radius 10;
	}
	States
	{
	Spawn:
		FCAN ABC 4 BRIGHT;
		Loop;
	}
}
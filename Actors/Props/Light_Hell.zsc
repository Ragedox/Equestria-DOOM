class EquestriaTorchFire : Actor
{
	Default
	{
		Radius 0; Height 0;
		+NoInteraction +Bright +NoBlockmap +NoClip +RelativeToFloor +MoveWithSector
		RenderStyle "Add";
	}
	
	override void PostBeginPlay()
	{
		bSPRITEFLIP = randompick(0,1);
	}
	
	States
	{
	Spawn:
		TRCH ABCD random(3,4);
		#### # 0 A_SpawnItemEx("Smoke",0,0,15,0,0,0,0,0,200);
		Loop;
	}
}
class EquestriaShortBlueTorch : Actor replaces ShortBlueTorch
{
	Default
	{
		Radius 2; Height 37;
		+Solid
		Translation "168:191=192:207", "208:223=192:207", "160:167=192:207", "32:47=192:207", "232:235=198:201";
	}
	
	override void BeginPlay()
	{
		A_SpawnItemEx("EquestriaTorchFire",0,0,25,0,0,0,0, SXF_TRANSFERTRANSLATION);
	}
	
	States
	{
	Spawn:
		TRC2 A -1;
		Stop;
	}
}

class EquestriaBlueTorch : Actor replaces BlueTorch
{
	Default
	{
		Radius 2; Height 60;
		+SOLID
		Translation "168:191=192:207", "208:223=192:207", "160:167=192:207", "32:47=192:207", "232:235=198:201";
	}
	
	override void BeginPlay()
	{
		A_SpawnItemEx("EquestriaTorchFire",0,0,50,0,0,0,0, SXF_TRANSFERTRANSLATION | SXF_TRANSFERPOINTERS);
	}
	
	States
	{
	Spawn:
		TRCH EFGH random(3,4);
		Loop;
	}
}

// Green Torch

class EquestriaShortGreenTorch : EquestriaShortBlueTorch replaces ShortGreenTorch
{ Default { Translation "160:235=112:119", "176:191=112:127", "208:222=112:122", "160:167=112:127"; } }

class EquestriaGreenTorch : EquestriaBlueTorch replaces GreenTorch
{ Default { Translation "160:235=112:119", "176:191=112:127", "208:222=112:122", "160:167=112:127"; } }

// Red Torch

class EquestriaShortRedTorch : EquestriaShortBlueTorch replaces ShortRedTorch
{ Default { Translation "0:255=0:255"; } }

class EquestriaRedTorch : EquestriaBlueTorch replaces RedTorch
{ Default { Translation "0:255=0:255"; } }

/////////

class EquestriaCandleStick : CandleStick replaces CandleStick
{
	Default { Radius 8; Height 15; }
}

// Candelabra

class EquestriaCandelabra : Candelabra replaces Candelabra
{
	Default
	{
		Radius 10;
	}
	States
	{
	Spawn:
		CBRA A -1 BRIGHT;
		Stop;
	}
}

/* Torches */

Class EquestriaWallTorch : Actor
{
	Default
	{
	//$Title Wall Torch
	//$Category Light Sources
	//$IsDecoration
	//$NotAngled
	
		+RANDOMIZE
		+NOBLOCKMAP
		+NOGRAVITY
		+FIXMAPTHINGPOS
		Radius 6.5; Height 10;
	}
	States
	{
	Spawn:
		WTCH ABCDABCD 4 Bright;
		#### # 0 A_SpawnItemEx("Smoke",0,0, height + 5,0,0,0,0,0,200);
		Loop;
	}
}
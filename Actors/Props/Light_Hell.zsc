class EquestriaTorchFire : Equestria_Particles
{
	Default
	{
		Radius 0; Height 0;
		-ForceXYBillBoard +NoInteraction +Bright +NoBlockmap +NoClip +NoGravity +RelativeToFloor +MoveWithSector
		RenderStyle "Add";
	}
	
	override void PostBeginPlay()
	{
		bSPRITEFLIP = randompick(0,1);
		Super.PostBeginPlay();
	}
	
	override void Tick()
	{
		if (!bTicked)
			Super.Tick();
			
		A_Warp(AAPTR_MASTER,0,0,50,0,WARPF_TOFLOOR);
	}
	States
	{
	Spawn:
		TRCH ABCD random(3,4);
		#### # 0 A_SpawnItemEx("Smoke",0,0,20,0,0,0,0,0,200);
		Loop;
	}
}

class EquestriaSmallTorchFire : EquestriaTorchFire
{
	override void Tick()
	{
		if (!bTicked)
			Super.Tick();
			
		A_Warp(AAPTR_MASTER,0,0,25,0,WARPF_TOFLOOR);
	}
}

class EquestriaShortBlueTorch : Actor replaces ShortBlueTorch
{
	
	Default
	{	//$Sprite SMBTA0
		Radius 2; Height 37;
		+Solid
		Translation "168:191=192:207", "208:223=192:207", "160:167=192:207", "32:47=192:207", "232:235=198:201";
	}
	
	override void BeginPlay()
	{
		A_SpawnItemEx("EquestriaSmallTorchFire",0,0,0,0,0,0,0, SXF_TRANSFERTRANSLATION | SXF_SETMASTER);
		Super.BeginPlay();
	}
	
	States
	{
	Spawn:
		TRC2 A -1;
		Stop;
	}
}

class EquestriaBlueTorch : Actor replaces BlueTorch
{
	Default
	{	//$Sprite TBLUA0
		Radius 2; Height 60;
		+SOLID
		Translation "168:191=192:207", "208:223=192:207", "160:167=192:207", "32:47=192:207", "232:235=198:201";
	}
	
	override void BeginPlay()
	{
		A_SpawnItemEx("EquestriaTorchFire",0,0,0,0,0,0,0, SXF_TRANSFERTRANSLATION | SXF_SETMASTER);
		Super.BeginPlay();
	}
	
	States
	{
	Spawn:
		TRCH EFGH random(3,4);
		Loop;
	}
}

// Green Torch

class EquestriaShortGreenTorch : EquestriaShortBlueTorch replaces ShortGreenTorch
{ Default { //$Sprite SMGTA0
			Translation "160:235=112:119", "176:191=112:127", "208:222=112:122", "160:167=112:127"; } }

class EquestriaGreenTorch : EquestriaBlueTorch replaces GreenTorch
{ Default { //$Sprite TGRNA0
			Translation "160:235=112:119", "176:191=112:127", "208:222=112:122", "160:167=112:127"; } }

// Red Torch

class EquestriaShortRedTorch : EquestriaShortBlueTorch replaces ShortRedTorch
{ Default { //$Sprite SMRTA0
			Translation "0:255=0:255"; } }

class EquestriaRedTorch : EquestriaBlueTorch replaces RedTorch
{ Default { //$Sprite TREDA0
			Translation "0:255=0:255"; } }

/////////

class EquestriaCandleStick : CandleStick replaces CandleStick
{
	Default { Radius 8; Height 15; }
}

// Candelabra

class EquestriaCandelabra : Candelabra replaces Candelabra
{
	Default
	{
		Radius 2;
	}
	States
	{
	Spawn:
		CBRA A -1 BRIGHT;
		Stop;
	}
}

/* Torches */

Class EquestriaWallTorch : Actor
{
	Default
	{
	//$Title Wall Torch
	//$Category Light Sources
	//$IsDecoration
	//$NotAngled
	
		+RANDOMIZE
		+NOBLOCKMAP
		+NOGRAVITY
		+FIXMAPTHINGPOS
		+SLIDESONWALLS
		Radius 6.5; Height 10;
	}
	
	override void BeginPlay()
	{
		A_AttachLight("Light", DynamicLight.RandomFlickerLight, "ffff55", 55, 60,
			DYNAMICLIGHT.LF_ATTENUATE | DYNAMICLIGHT.LF_SPOT, (0,0,16), 0.01, 125, 150, -90);
		Super.BeginPlay();
	}
	
	States
	{
	Spawn:
		WTCH ABCDABCD 4 Bright;
		#### # 0 A_SpawnItemEx("Smoke",0,0, height + 5,0,0,0,0,0,200);
		Loop;
	}
}
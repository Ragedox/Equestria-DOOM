Class DEParticles : Actor abstract
{
	Default
	{
		+NOGRAVITY
		+THRUACTORS
		+DONTSPLASH
		+Randomize
		+RollSprite +RollCenter
		+ForceXYBillboard
	}
}

Class Equestria_Particles : DEParticles abstract
{}

Class Equestria_Trails : Equestria_Particles abstract
{
	Default
	{
		Scale 0.75;
		RenderStyle "Add";
		Alpha 0.7;
		
		+NoInteraction
		+NoCLip
		
		DistanceCheck "CV_SFXDISTANCE";
	}
}

Class Smoke : DEParticles replaces RocketSmokeTrail
{
	Default
	{
		Height 2; Radius 1;
		
		RenderStyle "Translucent";
		
		Alpha 0.75;
		
		Speed 16;
		
		Scale 0.5;
		
		Gravity -0.25;
		
		-Missile
		-NoGravity
	}
	States
	{
	Spawn:
		RSMK A random(2,4) NoDelay
		{
			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(0, 359));
		}
		RSMK A 2;
		RSMK AAAAAAAAAABBBBBBBBCCCCCCDDDD 1
		{
			A_FadeOut(0.02);
			A_SetScale(Scale.X + 0.025, Scale.Y + 0.025);
		}
		RSMK E 1 A_FadeOut(0.05);
		Wait;
	}
	
	Override void Tick()
	{
		Super.Tick();
		if (!isFrozen())
		{
			A_ScaleVelocity(0.8);
		}
	}
}

Class ExplosionSmoke : Smoke
{
	Default
	{
		Height 2; Radius 1;
		
		RenderStyle "Translucent";
		
		Alpha 0.75;
		
		Speed 7;
		
		Scale 0.5;
		
		Gravity -0.05;
	}
	States
	{
	Spawn:
		RSMK A random(2,4) NoDelay
		{
			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(0, 359));
		}
		RSMK A 2;
		RSMK AAAAAAAAAABBBBBBBBCCCCCCDDDD 1
		{
			A_FadeOut(0.02);
			A_SetScale(Scale.X + 0.025, Scale.Y + 0.025);
		}
		RSMK E 1 A_FadeOut(0.05);
		Wait;
	}
}

Class EquestriaTeleportFog : TeleportFog replaces TeleportFog
{	Default	{	+ForceXYBillBoard	}	}

Class SoulParticle : DebrisCore
{
	Default
	{
		+NOINTERACTION
	}
	States
	{
	Spawn:
		SOUP A random(0, 4) NODELAY { bSPRITEFLIP = RandomPick(0,1); }
		SOUP BCD random(2, 3);
		SOUP EFGH random(5, 6) BRIGHT;
		#### # 1
		{
			A_FadeOut(0.1);
			A_SetScale(scale.x - 0.1);
		}
		Wait;
	}
}

Class SoulSpawn : Actor
{
	Default { +NOINTERACTION }
	States
	{
	Spawn:
		TNT1 AAAAAAAAAA 0 NoDelay A_SpawnItemEx("SoulParticle", random(-20, 20), random(-20, 20), random(20, 55),0,0, 0.75);
		Stop;
	}
}
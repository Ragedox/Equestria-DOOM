Class EquestriaBFG9K : EquestriaDoomWeapon replaces BFG9000
{
	Default
	{
		//$Title Big Fucking Gun 9000
		Height 40;
		
		Weapon.BobStyle "InverseSmooth";
		Weapon.BobSpeed 0.75;
		
		Weapon.SelectionOrder 9;
		Weapon.AmmoUse 50;
		Weapon.AmmoGive 50;
		Weapon.AmmoType "Cell";
		
		+WEAPON.NOAUTOFIRE
		
		Inventory.PickupMessage "$GOTBFG9000";
		
		Inventory.PickupSound "pickup/bfg";
	}
	States
	{
	Spawn:
		BFUG A -1 NoDelay { bSPRITEFLIP = RandomPick(0,1); }
		Stop;
	
	Ready:
		BFGG A 1 A_WeaponReady();
		Loop;
	
	Select:
		BFGG A 0 A_SetCrossHair(14);
		BFGG A 1 A_Raise();
		Wait;

	Deselect:
		TNT1 A 0 {
			if(health <= 0)
				return ResolveState("DeathCheck");
			return ResolveState(null);
		}
		
		BFGG A 1 A_Lower();
		Wait;

	Fire:
		BFGG B 10 A_StartSound("weapons/bfgf", CHAN_WEAPON);
		BFGG B 10 {	if(GetCVAR("CamRecoilToggle") == true) { A_Overlay(2, "Shake"); }}
		BFGG B 10
		{
			A_Overlay(-10, "Flash");
			A_WeaponOffset(0, 35, WOF_KEEPX | WOF_INTERPOLATE);
		}
		BFGG C 10 Bright
		{
			A_FireProjectile("EquestriaBFGBall", 0, 1, 0, -20);
			A_StartSound("weapons/bfgshot");
			A_WeaponOffset(0, 45, WOF_KEEPX | WOF_INTERPOLATE);
			
			if (GetCVar("CamRecoilToggle") == 1)
			{
				A_Overlay(2, "CamRecoil");
			}
			//A_SetBlend("green", 0.5, 16);
		}
		BFGG B 20
		{
			A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
			A_ReFire();
		}
		Goto Ready;
	
	AltFire:
		Stop;
		
	Flash:
		BFGF A 0
		{
			A_OverlayOffset(-10, 0, 15, WOF_KEEPX);
			
			if(GetCVar("MuzzleStyle"))
			{ A_OverlayFlags(-10, PSPF_RENDERSTYLE, 1); A_OverlayRenderStyle(-10, STYLE_Add); }
		}
		
		BFGF B 5 Bright A_Light(1);
		BFGF A 5 Bright A_Light(2);
		BFGF C 4 Bright A_Light(-6);
		BFGF C 1 Bright A_Light(-4);
		BFGF C 1 Bright A_Light(-3);
		BFGF C 1 Bright A_Light(-2);
		BFGF C 1 Bright A_Light(-1);
		BFGF A 4 Bright A_Light(0);
		BFGF B 2 Bright;
		Stop;
	
	Shake:
		TNT1 A 0 A_QuakeEx(5, 3, 5, 20, 0, 1, "", QF_SCALEUP | QF_RELATIVE, 1,1,1,0,0,1,1);
		
		TNT1 AAAA 1
		{
			A_WeaponOffset(frandom(-1.5, 1.5), frandom(28, 32), WOF_INTERPOLATE);
			//A_SetRoll(frandom(-0.5, 0.5));
		}
		TNT1 AAAAA 1
		{
			A_WeaponOffset(frandom(-3, 3), frandom(32, 36), WOF_INTERPOLATE);
			//A_SetRoll(frandom(-0.5, 0.5));
		}
		TNT1 AAAAAAA 1
		{
			A_WeaponOffset(frandom(-6, 6), frandom(32, 36), WOF_INTERPOLATE);
			//A_SetRoll(frandom(-0.5, 0.5));
		}
		Stop;

	CamRecoil:
		TNT1 A 0 A_ZoomFactor(0.85, ZOOM_INSTANT | ZOOM_NOSCALETURNING);

		TNT1 A 2 A_SetPitch(pitch-6, SPF_INTERPOLATE);
		TNT1 B 1 A_ZoomFactor(0.9, ZOOM_INSTANT | ZOOM_NOSCALETURNING);
		TNT1 B 1 A_ZoomFactor(1, ZOOM_NOSCALETURNING);
		TNT1 A 1 A_SetPitch(pitch+2, SPF_INTERPOLATE);
		TNT1 AAAA 1 A_SetPitch(pitch+0.50, SPF_INTERPOLATE);
		TNT1 AAAAAAAA 1 A_SetPitch(pitch+0.25, SPF_INTERPOLATE);
		Stop;
	}
}

Class EquestriaBFGBall : Actor replaces BFGBall
{
	Default
	{
		Projectile;
		
		Height 45; Radius 22.5;
		
		Speed 25;
		
		Damage 50;
		DamageType "BFGball";
		Decal "BFGLightning";
		
		RenderStyle "Add";
		
		+ForceXYBillBoard
		+RollSprite +RollCenter
		+Bright
		+ZDoomTrans
		+BloodSplatter
		
		DeathSound "weapons/bfgx";
		Obituary "$OB_MPBFG_BOOM";
	}
	States
	{
	Spawn:
		BFS1 A 0 NoDelay
		{
			A_SetRoll(random(0, 360));
			for (int i = 0; i < 10; i++)
			{
				A_SpawnProjectile("BFGTrailSparkles", 0, 0, frandom(0, 360), CMF_AIMDIRECTION, frandom(0, 360));
			}
		}
		BFS1 AAAAA 1 A_SetRoll(roll + random(-75, 75), SPF_INTERPOLATE);
		
		#### # 0 A_Jump(256, "Projectile");
		Wait;
	
	Projectile:
		BFS1 A random(1, 4)
		{ A_SetRoll(roll + random(-45, 45), SPF_INTERPOLATE);
			A_SpawnProjectile("BFGTrailSparkles", 0, 0, frandom(0, 360), CMF_AIMDIRECTION, frandom(0, 360));
		}
		Loop;
	
	Death:		
		BFE1 A 2
		{
			A_Explode(100, 256, 0);
			A_SetRoll(0);
			A_SetScale(2);
			A_SpawnItem("BFGShockwave", 0, 12);
						
			A_QuakeEx(9, 9, 9, 35, 0, 1024, "world/quake", QF_SCALEDOWN,0,0,0,1);
			
			for (int i = 0; i < 32; i++)
			{
				//A_SpawnItemEx("BFGSparkles", 0, 0, random(0,50), random(-50,50), random(-50,50), random(-50, 50), random(0,360));
				A_SpawnProjectile("BFGSparkles", 0, 0, frandom(0, 360), CMF_AIMDIRECTION, frandom(0, 360));
				A_SpawnProjectile("BFGTrailSparkles", 0, 0, frandom(0, 360), CMF_AIMDIRECTION, frandom(0, 360));
			}
		}
		BFE1 BCD 2;
		BFE1 E 8 A_BFGSpray("EquestriaBFGExtra", 40, 15, 360, 1024, 32, 0, BFGF_MISSILEORIGIN);
		BFE1 FGHIJ 3;
		BFE1 K 1 A_FadeOut(0.1);
		Wait;
	}
		
	override void Tick()
	{
		if (InStateSequence(CurState, ResolveState("Projectile")) && !isFrozen())
		{
			A_SpawnItemEx("BFGTrail",0,0,0,0,0,0,0, SXF_TRANSFERSPRITEFRAME | SXF_TRANSFERROLL);
		}
		Super.Tick();
	}
}

Class EquestriaBFGExtra : BFGExtra replaces BFGExtra
{	Default { +ForceXYBillBoard }
	States
	{
	Spawn:
		BFE2 ABCD 0 A_SpawnProjectile("BFGTrailSparkles", 0, 0, frandom(0, 360), CMF_AIMDIRECTION, frandom(0, 360));
		BFE2 A 8 Bright A_QuakeEx(4,4,4,10,0,128,0,QF_SCALEDOWN);
		BFE2 BCD 6 Bright;
		Stop;
	}
}

Class BFGTrail : Equestria_Trails
{
	Default	{ Scale 1; Alpha 0.5;	}
	States
	{
	Spawn:
		#### # 1 Bright A_FadeOut(0.025);
		Loop;
	}
}

Class BFGShockwave : Actor
{
	Default
	{
		+FORCEXYBILLBOARD
		+NOINTERACTION
		
		RenderStyle "Add"; Alpha 0.5;
	}
	States
	{
	Spawn:
		BFEF ABCDEFGHIJKLMN 1 Bright;
		BFEF O 1 Bright A_FadeOut (0.1);
		Wait;
	}
}

Class BFGSparkles : PuffSparkles
{
	Default
	{
		+NOINTERACTION
		RenderStyle "Add";
		Scale 1;
		Speed 50;
		Gravity 0.1;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetRoll(random(0, 360));
		
		PLS2 B random(35,45);
		
	Death:
		PLS2 BBBB 1 A_SetScale(Scale.X + 0.25, Scale.Y + 0.25);
		PLS2 BBBBBBBB 1 A_SetScale(Scale.X - 0.25, Scale.Y - 0.25);
		Stop;
	}
	Override void Tick()
	{
		Super.Tick();
		if (!isFrozen())
		{
			A_SpawnItemEx("BFGSparkles_Trail", 0,0,0,0,0,0,0, SXF_TRANSFERROLL);
			A_ScaleVelocity(0.75);
			A_Recoil(frandom(-5, 5));
		}	
	}
}

Class BFGSparkles_Trail : Equestria_Trails
{
	Default { RenderStyle "Add"; Alpha 0.25; Scale 1; +Bright }
	States
	{
	Spawn:
		PLS2 B random(17,35);
		
	Death:
		PLS2 B 1
		{
			A_SetScale(Scale.X - 0.05, Scale.Y - 0.05);
			If(Scale.X <= 0)
				return ResolveState("Null");
			return state(null);
		}
		Wait;
	}
}

Class BFGTrailSparkles : PlasmaSparkle
{
	Default { +MISSILE RenderStyle "Add"; Alpha 0.75; Scale 1; Speed 25; BounceType "Hexen"; BounceFactor 128.0; }
	States
	{
	Spawn:
		PLS2 B random(70,140) NoDelay A_SetRoll(random(0, 359));
		
	Death:
		PLS2 BB 1 A_SetScale(Scale.X + 0.25, Scale.Y + 0.25);
		PLS2 BBBBBBBB 1 A_SetScale(Scale.X - 0.25, Scale.Y - 0.25);
		Stop;
	}
}
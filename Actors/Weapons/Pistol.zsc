Class EquestriaPistol : EquestriaDoomWeapon replaces Pistol
{
	Default
		{
	//$Title Pistol
		Weapon.BobSpeed 2;
		Weapon.BobRangeX 0.25;
		Weapon.SelectionOrder 6;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "Clip";
		Weapon.Kickback 50;
		
		Obituary "$OB_MPPISTOL";
		
		+WEAPON.WIMPY_WEAPON;
		
		Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED";
		
		Tag "$TAG_PISTOL";
		AttackSound "weapons/pistol";
	}
	States
	{
	Spawn:
		PIST A -1 NoDelay { bSPRITEFLIP = RandomPick(0,1); }
		Stop;
	
	Ready:
		PISG A 1 A_WeaponReady();
		Loop;
		
	Select:
		PISG A 0 Offset(-67, 100)
		{
			A_SetCrosshair(8);
			
			if (GetCVAR("AuraToggle") == true)
				A_Overlay(10, "Aura.Select");
		}
		#### D 1 A_WeaponOffset(-54, 81, WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-32, 69, WOF_INTERPOLATE);
		#### C 1 A_WeaponOffset(-22, 58, WOF_INTERPOLATE);
		#### C 1 A_WeaponOffset(-10, 47, WOF_INTERPOLATE);
		#### A 1 A_WeaponOffset(-7, 39, WOF_INTERPOLATE);
		#### A 1 A_WeaponOffset(-2, 34, WOF_INTERPOLATE);
		#### # 0 A_Raise();
		Wait;
	
	Deselect:
		#### # 0 A_JumpIf(health <= 0, "DeathCheck");
		
	/*	#### A 1 A_WeaponOffset(7, 39, WOF_INTERPOLATE);
		#### C 1 A_WeaponOffset(10, 47, WOF_INTERPOLATE);
		#### C 1 A_WeaponOffset(22, 58, WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(32, 69, WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(54, 81, WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(67, 100, WOF_INTERPOLATE);*/
		#### # 1 A_Lower();
		Wait;
	
	Fire:
		PISG A 4
		{
			if (GetCVAR("AuraToggle") == true)
				A_Overlay(10, "Aura.Fire");
			else
				A_ClearOverlays(10, 11);
		}
		PISG A 1
		{
			A_FireBullets (1, 1, 1, 4, "Player_BulletPuff", 1, 0, "BulletTracer", -8);
			A_WeaponOffset(0, 40, WOF_KEEPX | WOF_INTERPOLATE);
			A_GunFlash();
		}
		PISG B 2 A_FireProjectile("BulletCasingSpawn",0,0,0,-10, FPF_NOAUTOAIM);
		PISG C 0 A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
		PISG CD 3;
		PISG C 2;
		PISG A 1
		{
			A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
			A_ReFire();
		}
		Goto Ready;
  
	Flash:
		PISF A 1 Bright { if(GetCVar("MuzzleStyle")) { A_OverlayFlags(1000, PSPF_RENDERSTYLE, 1); A_OverlayRenderStyle(1000, STYLE_Add); }}
		Goto LightDone;
	
	Aura.Select:
		AUR4 A 0 A_Overlay(11, "Aura.Setup");
		AUR4 DCA 2 Bright;
	Aura.Ready:
		AUR4 A 1 Bright A_Overlay(11, "Aura.Setup", true);
		Wait;
		
	Aura.Fire:
		AUR4 A 0 A_Overlay(11, "Aura.Setup");
		
		#### A 5 Bright	A_OverlayScale(10,1,1, WOF_INTERPOLATE);
		#### B 2 Bright	A_OverlayScale(10,1,1, WOF_INTERPOLATE);
		#### CD 3 Bright A_OverlayScale(10,1,1, WOF_INTERPOLATE);
		#### C 2 Bright A_OverlayScale(10,1,1, WOF_INTERPOLATE);
		#### A 1 Bright A_OverlayScale(10,1,1, WOF_INTERPOLATE);
		Goto Aura.Ready;
	}
}
Class EquestriaPistol : DoomEquestriaWeapon replaces Pistol
{
	Default
		{
	//$Title Pistol
		Weapon.SelectionOrder 6;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "Clip";
		
		Obituary "$OB_MPPISTOL";
		
		+WEAPON.WIMPY_WEAPON;
		
		Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED";
		
		Tag "$TAG_PISTOL";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay { bSPRITEFLIP = RandomPick(0,1); }
		PIST A -1;
		Stop;
	
	Ready:
		TNT1 A 0 A_JumpIfInventory("KickCheck", 1, "Kick.Normal");
		
		PISG A 1 A_WeaponReady();
		Loop;
		
	Select:
		PISG A 0 A_SetCrosshair(8);
		
		PISG A 1 A_Raise();
		Wait;
	
	Deselect:
		TNT1 A 0 A_TakeInventory("KickCheck", 999);
		TNT1 A 0 A_JumpIf(health <= 0, "DeathCheck");
		
		PISG A 1 A_Lower();
		Wait;
	
	Fire:
		PISG A 3;
		PISG B 1
		{
			A_FireBullets (0.5, 1, 1, 4, "Player_BulletPuff", 1, 0, "BulletTracer", -8);
			A_WeaponOffset(0, 30, WOF_KEEPX | WOF_INTERPOLATE);
			A_StartSound("weapons/pistol", CHAN_WEAPON);
			A_GunFlash();
			
			if (GetCVAR("WeaponLight") == true)
			{
				A_FireProjectile("PistolLight",0,0,0, -14);
			}
		}
		PISG C 2
		{
			A_WeaponOffset(0, 29, WOF_KEEPX | WOF_INTERPOLATE);
			A_FireProjectile("BulletCasingSpawn",0,0,0,-10, FPF_NOAUTOAIM);
		}
		PISG D 3;
		PISG E 4;
		PISG DD 1;
		PISG B 1
		{
			A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
			A_ReFire();
		}
	Goto Ready;
  
	Flash:
		TNT1 A 0 { A_OverlayFlags(1000, PSPF_RENDERSTYLE, 1); A_OverlayRenderStyle(1000, STYLE_Add); }
		
		PISF A 1 Bright;
		Goto LightDone;
	
	}
}

Class PistolLight : ShotgunLight
{}
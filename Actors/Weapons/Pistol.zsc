Class EquestriaPistol : DoomEquestriaWeapon replaces Pistol
{
	Default
		{
	//$Title Pistol
		Weapon.BobSpeed 2;
		Weapon.BobRangeX 0.25;
		Weapon.SelectionOrder 6;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "Clip";
		
		Obituary "$OB_MPPISTOL";
		
		+WEAPON.WIMPY_WEAPON;
		
		Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED";
		
		Tag "$TAG_PISTOL";
	}
	States
	{
	Spawn:
		PIST A -1 NoDelay { bSPRITEFLIP = RandomPick(0,1); }
		Stop;
	
	Ready:
		PISG A 1
		{
			A_WeaponReady();
			if (CountInv("KickCheck"))
				return ResolveState("Kick.Normal");
			return ResolveState(null);
		}
		Loop;
		
	Select:
		PISG A 0 A_SetCrosshair(8);
		PISG A 1 A_Raise();
		Wait;
	
	Deselect:
		TNT1 A 0 {
			TakeInventory("KickCheck", 999);
			if(health <= 0)
				return ResolveState("DeathCheck");
			return ResolveState(null);
		}
		
		PISG A 1 A_Lower();
		Wait;
	
	Fire:
		PISG A 4;
		PISG A 1
		{
			A_FireBullets (1, 1, 1, 4, "Player_BulletPuff", 1, 0, "BulletTracer", -8);
			A_WeaponOffset(0, 40, WOF_KEEPX | WOF_INTERPOLATE);
			A_StartSound("weapons/pistol", CHAN_WEAPON);
			A_GunFlash();
			
			if (GetCVAR("WeaponLight") == true)
			{
				A_FireProjectile("PistolLight",0,0,0, -14);
			}
		}
		PISG B 2 A_FireProjectile("BulletCasingSpawn",0,0,0,-10, FPF_NOAUTOAIM);
		PISG C 0 A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
		PISG CD 3;
		PISG C 2;
		PISG A 1
		{
			A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
			A_ReFire();
		}
		Goto Ready;
  
	Flash:
		TNT1 A 0 { A_OverlayFlags(1000, PSPF_RENDERSTYLE, 1); A_OverlayRenderStyle(1000, STYLE_Add); }
		
		PISF A 1 Bright;
		Goto LightDone;
	
	}
}

Class PistolLight : ShotgunLight
{}
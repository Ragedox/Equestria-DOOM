Class BulletCasing : PuffSparkles
{
	Default
	{
		Height 4; Radius 2;
		-BRIGHT
		+MISSILE
		RenderStyle "Normal";
		Scale 0.15;
		Gravity 0.3;
		
		BounceType "Doom"; BounceFactor 0.5;
		
		BounceSound "weapons/casinga";
	}
	States
	{
	Spawn:
		CSIG A 0 NoDelay A_SetRoll(random(0, 359));
		CSIG A 1 A_SetRoll(roll + 25, SPF_INTERPOLATE);
		Wait;
	Death:
		CSIG A 350 A_Recoil(frandom(-2, 1));
		CSIG A 1 A_FadeOut(0.01);
		Wait;
	}
}

Class ShellCasing : BulletCasing
{
	Default
	{
		Gravity 0.5;
		BounceSound "weapons/casingb";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(GetCVar("CV_CLASSIC")==1, "Classic");
		
		CSIG B 0 A_SetRoll(random(0, 359));
		#### # 1 A_SetRoll(roll + 25, SPF_INTERPOLATE);
		Wait;
	
	Classic:
		CSIG D 0 { A_SetScale(1,1); A_SetRoll(random(0, 359)); }
		#### # 1 A_SetRoll(roll + 25, SPF_INTERPOLATE);
		Wait;
		
	Death:
		TNT1 A 0 A_JumpIf(GetCVar("CV_CLASSIC")==1, "Classic.Death");
		
		CSIG B 350 A_Recoil(frandom(-2, 1));
		#### # 1 A_FadeOut(0.01);
		Wait;
		
	Classic.Death:
		CSIG D 350 { A_SetRoll(randompick(90, -90)); A_Recoil(frandom(-2, 1)); }
		#### # 1 A_FadeOut(0.01);
		Wait;
	}
}

Class BulletCasingSpawn : Actor
{
	Default
	{
		Speed 20;
		PROJECTILE;
		+NOCLIP
		+NOINTERACTION
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnProjectile("BulletCasing",0,0, frandom(-80,-100), CMF_AIMDIRECTION, frandom(-60,-40));
		Stop;
	}
}

Class ShotCasingSpawn : BulletCasingSpawn
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnProjectile("ShellCasing",0,0, frandom(-80,-100), CMF_AIMDIRECTION, frandom(-60,-40));
		Stop;
	}
}

Class SSGCasingSpawn : ShotCasingSpawn
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnProjectile("ShellCasing",0,0, frandom(80,100), CMF_AIMDIRECTION, frandom(-70,-40));
		Stop;
	}
}
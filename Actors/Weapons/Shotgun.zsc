Class EquestriaShotgun : EquestriaDoomWeapon replaces Shotgun
{
	Default
	{
		//$Title Shotgun
		Weapon.SelectionOrder 5;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "Shell";

		Inventory.PickupMessage "$GOTSHOTGUN";
		Obituary "$OB_MPSHOTGUN";
		Tag "$TAG_SHOTGUN";
		
		AttackSound "weapons/shotgf";
	}
	States
	{
	Spawn:
		SHOT A -1 NoDelay { bSPRITEFLIP = RandomPick(0,1); }
		Stop;
		
	Select:
		SHTG A 0 Offset(-67, 100)
		{
			A_SetCrossHair(9);
			
			if (GetCVAR("AuraToggle") == true)
				A_Overlay(10, "Aura.Select");
		}
		//SHTG A 0 A_PlaySoundEX("misc/w_pkup", 1);

		#### D 1 A_WeaponOffset(-54, 81, WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-32, 69, WOF_INTERPOLATE);
		#### C 1 A_WeaponOffset(-22, 58, WOF_INTERPOLATE);
		#### C 1 A_WeaponOffset(-10, 47, WOF_INTERPOLATE);
		#### B 1 A_WeaponOffset(-7, 39, WOF_INTERPOLATE);
		#### B 1 A_WeaponOffset(-2, 34, WOF_INTERPOLATE);
		#### # 0 A_Raise();
		Wait;
		
	Deselect:
		#### # 0 A_JumpIf(health <= 0, "DeathCheck");
		SHTG A 1 A_Lower();
		Wait;
	
	Ready:
		SHTG A 1 A_WeaponReady();
		Loop;
		
	Fire:
		SHTG A 7
		{
			invoker.previousangle = angle;
			invoker.previouspitch = pitch;
			
			A_FireBullets (6, 1, 8, 5, "Player_ShotgunPuff", 1, 0, "ShotgunTracer", -8);
			A_GunFlash();

			if (GetCVAR("CamRecoilToggle") == true)
				A_Overlay(2, "CamRecoil");
			//if (GetCVAR("WeaponLight") == true)
				//A_FireProjectile("ShotgunLight",0,0,0, -14);
		}
		Goto Pump;
		
	Pump:
		SHTG B 2
		{
			if (GetCVAR("AuraToggle") == true)
				A_Overlay(10, "Aura.Pump");
			else
				A_ClearOverlays(10, 11);
		}
		SHTG C 2;
		SHTG DEFGHIJ 1;
		SHTG H 0 A_FireProjectile("ShotCasingSpawn",random(0,5),0, -18, -25);
		SHTG IH 4;
		SHTG GFEDCB 1;
		SHTG A 1 A_WeaponOffset(1, 3, WOF_ADD);
		SHTG A 1 A_WeaponOffset(2, -2, WOF_ADD);
		SHTG A 1 A_WeaponOffset(-3, -1, WOF_ADD);
		SHTG A 7 A_ReFire();
		Goto Ready;
	
	Flash:
		TNT1 A 0
		{
			A_Overlay(-1000, "Muzzle");
			if(GetCVar("MuzzleStyle"))
			{
				A_OverlayFlags(1000, PSPF_RENDERSTYLE, 1);
				A_OverlayRenderStyle(1000, STYLE_Add);
				A_OverlayFlags(-1000, PSPF_RENDERSTYLE | PSPF_FORCESTYLE | PSPF_FORCEALPHA , 1);
				A_OverlayRenderStyle(-1000, STYLE_Add); 
				if(GetCVar("CV_MUZZLEOFFSET"))
				{
					A_OverlayFlags(-1000, PSPF_ADDWEAPON, 0);
					A_OverlayOffset(-1000, 0, 32);
					A_Overlay(-999, "MuzzleOffset");
				}
			}
			else
			{
				A_OverlayFlags(1000, PSPF_RENDERSTYLE, 0);
				A_OverlayFlags(-1000, PSPF_RENDERSTYLE | PSPF_FORCESTYLE | PSPF_FORCEALPHA , 0);
				A_OverlayFlags(-1000, PSPF_ADDWEAPON, 1);
			}
		}
		
		SHTF A 1 Bright
		{
			A_WeaponOffset(0, random(40, 45), WOF_KEEPX | WOF_INTERPOLATE);
			
			if (GetCVar("WeaponLight"))
				A_AttachLight("FiringLight", DynamicLight.PointLight, "ff 2a 00", random(64,80), 64,
					DYNAMICLIGHT.LF_SPOT, (-8,0,32),0,10,25,pitch);
			else
				A_Light(1);
		}
		SHTF B 3 Bright { if (!GetCVAR("WeaponLight")) A_Light(2); }
		SHTF A 2 Bright
		{
			A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
			
			if (GetCVar("WeaponLight"))
				A_RemoveLight("FiringLight");
			else
				A_Light(1);
		}
		Goto LightDone;
	
	Muzzle:
		SHTF C 1 Bright;
		#### D 3 Bright;
		#### C 2 Bright;
		Stop;
		
	MuzzleOffset:
		TNT1 AAAAAA 1 A_OverlayOffset(-1000,angle - invoker.previousangle, 32 + (-pitch) - (-invoker.previouspitch), WOF_INTERPOLATE);
		Stop;
		
	CamRecoil:		
		TNT1 A 1 A_SetPitch(pitch-2, SPF_INTERPOLATE);
		TNT1 AA 1 A_SetPitch(pitch+0.5, SPF_INTERPOLATE);
		TNT1 AA 1 A_SetPitch(pitch+0.25, SPF_INTERPOLATE);
		TNT1 AAAAA 1 A_SetPitch(pitch+0.1, SPF_INTERPOLATE);
		Stop;
	
	Aura.Select:
		AURA A 0 A_Overlay(11, "Aura.Setup");
		AURA DCB 2 Bright;
		
	Aura.Ready:
		AURA A 1 Bright A_Overlay(11, "Aura.Setup", true);
		Wait;
		
	Aura.Pump:
		AURA A 0 A_Overlay(11, "Aura.Setup");
		
		AURA BC 2 Bright		A_OverlayScale(10,1,1, WOF_INTERPOLATE);
		AURA DEFGHIJ 1 Bright	A_OverlayScale(10,1,1, WOF_INTERPOLATE);
		AURA IH 4 Bright		A_OverlayScale(10,1,1, WOF_INTERPOLATE);
		AURA GFEDCB 1 Bright	A_OverlayScale(10,1,1, WOF_INTERPOLATE);
		AURA A 3 Bright			A_OverlayScale(10,1,1, WOF_INTERPOLATE);
		Goto Aura.Ready;
	}
}
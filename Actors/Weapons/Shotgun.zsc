Class EquestriaShotgun : DoomEquestriaWeapon replaces Shotgun
{
	Default
	{
		//$Title Shotgun
		Weapon.SelectionOrder 5;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "Shell";

		Inventory.PickupSound "pickup/shotgun";
		Inventory.PickupMessage "$GOTSHOTGUN";
		Obituary "$OB_MPSHOTGUN";
		Tag "$TAG_SHOTGUN";
		
		AttackSound "weapons/shotgf";
	}
	States
	{
	Spawn:
		SHOT A -1 NoDelay { bSPRITEFLIP = RandomPick(0,1); }
		Stop;
		
	Select:
		SHTG A 0 //Offset(0, 32)
		{
			A_SetCrossHair(9);
			
			if (GetCVAR("AuraToggle") == true)
			{
				A_Overlay(10, "Aura.Ready");
			}
		}
		//SHTG A 0 A_PlaySoundEX("misc/w_pkup", 1);

		SHTG A 1 A_Raise();
		Wait;
		//#### # 0 A_Overlay(-10, "WeaponRaise");
		//Goto SelectAnimation;
		
	SelectAnimation:
		#### # 6;
		Goto Ready;
		
	Deselect:
		TNT1 A 0 {
			if(health <= 0)
				return ResolveState("DeathCheck");
			return ResolveState(null);
		}
		
		SHTG A 1 A_Lower();
		Wait;
	
	Ready:
		SHTG A 1 A_WeaponReady();
		Loop;
		
	Fire:
		SHTG A 7
		{
			A_FireBullets (6, 1, 8, 5, "Player_ShotgunPuff", 1, 0, "BulletTracer", -8);
			A_GunFlash();
			
			if (GetCVAR("CamRecoilToggle") == true)
			{
				A_Overlay(2, "CamRecoil");
			}
			if (GetCVAR("WeaponLight") == true)
			{
				A_FireProjectile("ShotgunLight",0,0,0, -14);
			}
		}
		Goto Pump;
		
	Pump:
		SHTG B 2
		{
			if (GetCVAR("AuraToggle") == true)
			{
				A_Overlay(10, "Aura.Pump");
			}
			else
			{
				A_ClearOverlays(10, 11);
			}
		}
		SHTG C 2;
		SHTG DEFGHIJ 1;
		SHTG H 0 A_FireProjectile("ShotCasingSpawn",random(0,5),0, -18, -25);
		SHTG IH 4;
		SHTG GFEDCB 1;
		SHTG A 1 A_WeaponOffset(1, 3, WOF_ADD);
		SHTG A 1 A_WeaponOffset(2, -2, WOF_ADD);
		SHTG A 1 A_WeaponOffset(-3, -1, WOF_ADD);
		SHTG A 7 A_ReFire;
		Goto Ready;
	
	Flash:
		TNT1 A 0 { if(GetCVar("MuzzleStyle")) { A_OverlayFlags(1000, PSPF_RENDERSTYLE, 1); A_OverlayRenderStyle(1000, STYLE_Add); }}
		
		SHTF A 1 Bright A_WeaponOffset(0, random(40, 45), WOF_KEEPX | WOF_INTERPOLATE);
		SHTF B 3 Bright;
		SHTF A 2 Bright A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
		Goto LightDone;
		
	CamRecoil:		
		TNT1 A 1 A_SetPitch(pitch-2, SPF_INTERPOLATE);
		TNT1 AA 1 A_SetPitch(pitch+0.5, SPF_INTERPOLATE);
		TNT1 AA 1 A_SetPitch(pitch+0.25, SPF_INTERPOLATE);
		TNT1 AAAAA 1 A_SetPitch(pitch+0.1, SPF_INTERPOLATE);
		Stop;
	
	Aura.Ready:
		AURA A 1 Bright A_Overlay(11, "Aura.Setup", true);
		Wait;
		
	Aura.Pump:
		AURA A 0 A_Overlay(11, "Aura.Setup");
		
		AURA BC 2 Bright		A_OverlayScale(10,1,1, WOF_INTERPOLATE);
		AURA DEFGHIJ 1 Bright	A_OverlayScale(10,1,1, WOF_INTERPOLATE);
		AURA IH 4 Bright		A_OverlayScale(10,1,1, WOF_INTERPOLATE);
		AURA GFEDCB 1 Bright	A_OverlayScale(10,1,1, WOF_INTERPOLATE);
		AURA A 3 Bright			A_OverlayScale(10,1,1, WOF_INTERPOLATE);
		Goto Aura.Ready;
	}
}

Class ShotgunLight : SuperShotgunLight
{}
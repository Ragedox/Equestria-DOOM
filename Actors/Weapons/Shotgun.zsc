Class EquestriaShotgun : DoomEquestriaWeapon replaces Shotgun
{
	Default
	{
		//$Title Shotgun
		Weapon.SelectionOrder 5;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "Shell";

		Inventory.PickupSound "pickup/shotgun";
		Inventory.PickupMessage "$GOTSHOTGUN";
		Obituary "$OB_MPSHOTGUN";
		Tag "$TAG_SHOTGUN";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay { bSPRITEFLIP = RandomPick(0,1); }
		SHOT A -1;
		Stop;
		
	Select:
		SHTG A 0 A_SetCrossHair(9);
		
		//SHTG A 0 A_PlaySoundEX("misc/w_pkup", 1);

		SHTG A 1 A_Raise();
		Wait;
	/*
	SelectAnimation:
		SHTG A 0 A_PlaySoundEX("misc/w_pkup", 1);
		SHTG GFEDCB 1;
		Goto Ready;*/
		
	Deselect:
		TNT1 A 0 A_TakeInventory("KickCheck", 999);
		
		SHTG A 1 A_Lower();
		Wait;
	
	Ready:
		TNT1 A 0 A_JumpIfInventory("KickCheck", 1, "Kick.Normal");
		
		SHTG A 1 A_WeaponReady();
		Loop;
		
	Fire:
		SHTG A 7
		{
			A_FireBullets (6, 1, 8, 5, "Player_ShotgunPuff", 1, 0, "BulletTracer", -8);
			A_GunFlash();
			A_StartSound ("weapons/shotgf", CHAN_WEAPON);
			
			A_FireProjectile("ShotgunLight", 0, 0, 0, -5);

			A_Overlay(2, "CamRecoil");
		}
		Goto Pump;
		
	Pump:
		SHTG B 2;
		SHTG C 2;
		SHTG DEFGHIJ 1;
		SHTG H 0 A_FireProjectile("ShellCasing",-90,0,0,-8, FPF_NOAUTOAIM);
		SHTG IH 4;
		SHTG GFEDCB 1;
		SHTG A 3;
		SHTG A 7 A_ReFire;
		Goto Ready;
	
	Flash:
		SHTF A 1 Bright A_WeaponOffset(0, random( 40, 45), WOF_KEEPX | WOF_INTERPOLATE);
		SHTF B 3 Bright;
		SHTF A 2 Bright A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
		Goto LightDone;
		
	CamRecoil:
		TNT1 A 0 A_JumpIf(GetCVar("CamRecoilToggle") == 0, "Off");
		
		TNT1 A 1 A_SetPitch(pitch-2, SPF_INTERPOLATE);
		TNT1 AA 1 A_SetPitch(pitch+0.5, SPF_INTERPOLATE);
		TNT1 AA 1 A_SetPitch(pitch+0.25, SPF_INTERPOLATE);
		TNT1 AAAAA 1 A_SetPitch(pitch+0.1, SPF_INTERPOLATE);
		Stop;
	}
}

Class ShotgunLight : SuperShotgunLight
{}
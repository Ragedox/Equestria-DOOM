Class EquestriaRifle : DoomEquestriaWeapon
{
	Default
	{
		Weapon.SelectionOrder 3;
		Weapon.BobStyle "InverseSmooth";
		
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 30;
		Weapon.AmmoType "Clip";
		
		Inventory.PickupMessage "$GOTRIFLE";
		Obituary "$OB_MPCHAINGUN";
		Tag "Rifle";
	}
	States
	{
	Spawn:
		RFLE E -1 NoDelay { bSPRITEFLIP = RandomPick(0,1); }
		Stop;
		
	Ready:
		TNT1 A 0 A_JumpIfInventory("KickCheck", 1, "Kick.Normal");
		
		RFLE A 1 A_WeaponReady();
		Loop;
		
	Deselect:
		TNT1 A 0 A_TakeInventory("KickCheck", 999);
		TNT1 A 0 A_JumpIf(health <= 0, "DeathCheck");
		
		RFLE A 1 A_Lower();
		Wait;
		
	Select:
		TNT1 A 0
		{
			A_SetCrossHair(2);
			
			if (GetCVAR("AuraToggle") == true)
			{
				A_Overlay(10, "Aura.Ready");
			}
		}
		RFLE A 1 A_Raise();
		Wait;
		
	Fire:
		RFLE A 2
		{
			A_FireBullets(1, 2, 1, 5, "Player_BulletPuff", 1, 0, "BulletTracer", -8);
			A_GunFlash();
			A_StartSound("weapons/chngun", CHAN_WEAPON);
		
			A_WeaponOffset(0, 34, WOF_INTERPOLATE | WOF_KEEPX);
			
			A_FireProjectile("BulletCasingSpawn",0,0,0,-25, FPF_NOAUTOAIM);
			
			if (GetCVAR("CamRecoilToggle") == true)
			{
				A_Overlay(2, "CamRecoil");
			}
			
			if (GetCVAR("WeaponLight") == true)
			{
				A_FireProjectile("PistolLight",0,0,0, -14);
			}
			
			if (GetCVAR("AuraToggle") == true)
			{
				A_Overlay(10, "Aura.Fire");
			}
			else
			{
				A_ClearOverlays(10, 11);
			}
		}
		
		RFLE B 2 A_WeaponOffset(0, random(36, 38), WOF_INTERPOLATE | WOF_KEEPX);
		RFLE A 0
		{
			A_WeaponOffset(0, 32, WOF_INTERPOLATE | WOF_KEEPX);
			A_ReFire();
		}
		Goto Ready;
		
	Reloading:
		PLSG B 1;
		PLSG CDE 1;
		PLSG F 12;
		PLSG EDCB 1;
		Goto Ready;
		
	Flash:
		TNT1 A 0 { A_OverlayFlags(1000, PSPF_RENDERSTYLE, 1); A_OverlayRenderStyle(1000, STYLE_Add); }
		
		RFLE C 2 Bright;
		RFLE D 2 Bright;
		Goto LightDone;
		
	CamRecoil:
		//TNT1 A 1 A_ZoomFactor(0.998, ZOOM_NOSCALETURNING);
		TNT1 AA 1 A_SetPitch(pitch-0.15);
		//TNT1 A 1 A_ZoomFactor(1, ZOOM_NOSCALETURNING);
		TNT1 AA 1 A_SetPitch(pitch+0.15);
		Stop;
		
	Aura.Ready:
		AUR2 A -1 Bright A_Overlay(11, "Aura.Setup", true);
		Stop;
		
	Aura.Fire:
		AUR2 A 0 A_Overlay(11, "Aura.Setup");

		AUR2 AB 2 Bright A_OverlayScale(10,1,1);
		Goto Aura.Ready;
	}
}
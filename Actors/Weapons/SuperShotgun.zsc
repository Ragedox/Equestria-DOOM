Class EquestriaSuperShotgun : DoomEquestriaWeapon replaces SuperShotgun
{
	Default
	{
		//$Title Super Shotgun
		Weapon.SelectionOrder 2;
		Weapon.AmmoUse 2;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "Shell";
		
		Inventory.PickupMessage "$GOTSHOTGUN2";
		Obituary "$OB_MPSSHOTGUN";
		Tag "$TAG_SUPERSHOTGUN";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay { bSPRITEFLIP = RandomPick(0,1); }
		SGN2 A -1;
		Stop;

	Select:
		SHT2 A 0 A_SetCrossHair(10);
		
		SHT2 A 1 A_Raise();
		Wait;
	
	Deselect:
		TNT1 A 0 A_TakeInventory("KickCheck", 999);
		TNT1 A 0 A_JumpIf(health <= 0, "DeathCheck");
		
		SHT2 A 1 A_Lower();
		Wait;
		
	Deselect2:
		TNT1 A 0 A_TakeInventory("KickCheck", 999);
		TNT1 A 0 A_JumpIf(health <= 0, "DeathCheck");
		
		SHT2 C 1 A_Lower();
		Wait;
  
	/*SelectAnimation:
		SHTG A 0 A_StartSoundEX("misc/w_pkup", 1);
		SHT2 DCB 2;*/
	
	Ready:
		TNT1 A 0 A_JumpIfInventory("KickCheck", 1, "Kick.Normal");
		
		SHT2 A 1 A_WeaponReady();
		Loop;

	Fire:
		SHT2 A 8
		{
			A_FireBullets (12, 8, 20, 5, "Player_SuperShotgunPuff", 1, 0, "BulletTracer", -8);
			A_StartSound ("weapons/sshotf", CHAN_WEAPON);
			A_GunFlash();
			A_Overlay(2, "CamRecoil");
			
			if (GetCVAR("WeaponLight") == true)
			{
				A_FireProjectile("SuperShotgunLight",0,0,0, -14);
			}
		}
		Goto Reloading;
	
	Reloading:
		SHT2 BCDD 1;
		SHT2 D 1;
		SHT2 E 3;
		SHT2 F 4;
		SHT2 G 5 A_CheckReload();
		SHT2 H 5 A_OpenShotgun2();
		TNT1 AA 0 A_FireProjectile("SSGCasingSpawn",random(0, 5),0,-8, frandom(-20, -15));
		SHT2 IJ 4;
		SHT2 K 6 A_LoadShotgun2();
		SHT2 LM 4;
		SHT2 NO 3;
		SHT2 P 2 A_CloseShotgun2();
		SHT2 A 0 A_ReFire();
		Goto Ready;
	
	Flash:
		TNT1 A 0 { A_OverlayFlags(1000, PSPF_RENDERSTYLE, 1); A_OverlayRenderStyle(1000, STYLE_Add); }
		
		SHT2 Q 1 Bright A_WeaponOffset(0, random(45, 50));
		SHT2 R 2 Bright;
		SHT2 R 1 Bright A_WeaponOffset(0, random(-5, -10), WOF_ADD);
		SHT2 Q 2 Bright;
		TNT1 A 0 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
		Goto LightDone;
	
	CamRecoil:
		TNT1 A 0 A_JumpIf(GetCVar("CamRecoilToggle") == 0, "Off");
		
		TNT1 B 1 A_ZoomFactor(0.95, ZOOM_NOSCALETURNING);
		TNT1 A 1 A_SetPitch(pitch-4, SPF_INTERPOLATE);
		TNT1 B 1 A_ZoomFactor(0.975, ZOOM_NOSCALETURNING);
		TNT1 B 1 A_ZoomFactor(1, ZOOM_NOSCALETURNING);
		TNT1 AAAAA 1 A_SetPitch(pitch+0.5, SPF_INTERPOLATE);
		TNT1 AAAAAA 1 A_SetPitch(pitch+0.25, SPF_INTERPOLATE);
		Stop;
		
	Overlay.Aside:
		TNT1 A 0 A_OverlayFlags(3, PSPF_ADDBOB, true);
		TNT1 A 0 A_OverlayPivotAlign(3, PSPA_RIGHT, PSPA_BOTTOM);
		
		SHT2 ACC 1
		{
			A_OverlayOffset(3, -35, 5, WOF_INTERPOLATE );
			A_OverlayRotate(3, 4, WOF_ADD);
		}
		SHT2 DD 1
		{
			A_OverlayOffset(3, -30, 10, WOF_INTERPOLATE );
			A_OverlayRotate(3, 8, WOF_ADD);
		}
		SHT2 D 10;
		SHT2 DCC 1
		{
			A_OverlayOffset(3, 0, 0, WOF_INTERPOLATE );
			A_OverlayRotate(3, -8, WOF_ADD);
		}
		SHT2 A 1;
		Stop;
	
	Overlay.AirAside:
		TNT1 A 0 A_OverlayFlags(3, PSPF_ADDBOB, true);
		TNT1 A 0 A_OverlayPivotAlign(3, PSPA_RIGHT, PSPA_BOTTOM);
		
		SHT2 ACC 1
		{
			A_OverlayOffset(3, -35, 5, WOF_INTERPOLATE );
			A_OverlayRotate(3, 5, WOF_ADD);
		}
		SHT2 DD 1
		{
			A_OverlayOffset(3, -30, 10, WOF_INTERPOLATE );
			A_OverlayRotate(3, 10, WOF_ADD);
		}
		SHT2 D 8;
		SHT2 DCC 1 
		{
			A_OverlayOffset(3, 0, 0, WOF_KEEPY | WOF_INTERPOLATE );
			A_OverlayRotate(3, -10, WOF_ADD);
		}
		SHT2 A 1;
		Stop;
	}
}

Class SuperShotgunLight : PlasmaGunLight
{}
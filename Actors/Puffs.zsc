Class PuffSparkles : DebrisCore
{
	Default
	{
		+Bright
		+ThruActors
		+DontSplash
		+Missile
		
		Scale 0.5;
		
		Height 2; Radius 1;
		
		Gravity 0.5;
		
		Speed 6;
		
		RenderStyle "Add";
		BounceType "Doom"; BounceFactor 0.5;
	}
	States
	{
	Spawn:
		PUFF A random(4, 35);
	Death:
		PUFF A 1
		{
			A_SetScale(Scale.X - 0.05, Scale.Y - 0.05);
			If(Scale.X <= 0)
				Destroy();
		}
		Loop;
	}
}

Class ChainsawSparkles : PuffSparkles
{
	Default
	{
		Gravity 0.5;
		Speed 6;
	}
	States
	{
	Spawn:
		//TNT1 A 0 ACS_NamedExecute("CheckLiquid");
		PUFF A random(35, 70) NoDelay A_JumpIf(waterlevel > 1, "Vanish");
	
	Death:
		PUFF A 1
		{
			A_SetScale(Scale.X - 0.05, Scale.Y - 0.05);
			If(Scale.X <= 0)
				Destroy();
		}
		Loop;
	}
}

Class EquestriaBulletPuff : Actor replaces BulletPuff
{
	Default
	{
		Height 4; Radius 2;
		RenderStyle "Translucent"; Alpha 0.5;
		Gravity 0.05;
		Mass 5;
		Scale 0.75;
		
		AttackSound "weapons/ricochet";
		
		+NOBLOCKMAP +NOTELEPORT
		+FORCEXYBILLBOARD
		+ROLLSPRITE +ROLLCENTER
	}

	static const string liquidfloor[] =
	{
		"FWATER1","FWATER2","FWATER3","FWATER4",
		"SLIME01","SLIME02","SLIME03","SLIME04",
		"SLIME05","SLIME06","SLIME07","SLIME08",
		"BLOOD1","BLOOD2","BLOOD3",
		"NUKAGE1","NUKAGE2","NUKAGE3","LAVA1",
		"LAVA2","LAVA3","LAVA4"
	};
	
	States
	{
	
	Crash:
		TNT1 A 0
		{
			if (pos.z <= FloorZ)
			{	
				string floorname = TexMan.GetName(floorpic);
				for (int i = 0; i < liquidfloor.size(); i++)
				{
					if (floorname == liquidfloor[i])
					{
						A_SpawnItemEx("DEWaterSplashChunk", 0,0,0, random(1, 2), random(1, 2), random(2, 4), frandom(0,360));
						Destroy();
					}
				}
			}
		}
		Goto Spawn;
		
	Spawn:
		//TNT1 A 0 NoDelay ACS_NamedExecute("CheckLiquid");
		TNT1 A 0 NoDelay
		{
			if (abs(pos.z - GetZAt()) <= 5)
			{
				if (GetCVar("CV_TESTING"))
					A_SpawnItemEx("EQ_Decal_Bullet",0,0,0,0,0,0,0, SXF_ABSOLUTEPOSITION);
					
				for (int i = 0; i < 3; i++)
				{ A_SpawnItemEx("PuffSparkles",0,0,1,frandom(-2,2),frandom(-2,2), frandom(3,8), frandom(0,360), SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_NOCHECKPOSITION); }
			}
			else
			{	for (int i = 0; i < 3; i++)
					A_SpawnProjectile("PuffSparkles",0,0, frandom(0, 360), CMF_AIMDIRECTION, frandom(0, 360));
			}

			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(0, 359));
		}
		PUFF A random(5, 8) Bright;
		PUFF B 4;
	
	Melee:
		PUFF C 4;
		PUFF D 1 A_FadeOut(0.1);
		Wait;
	}
	
	Override void Tick()
	{
		Super.Tick();
		if (!isFrozen())
		{
			A_ScaleVelocity(0.96);
		}
	}
}

Class Player_BulletPuff : EquestriaBulletPuff
{	Default	{	AttackSound "";	}	}

Class ChainsawPuff : Player_BulletPuff
{
	Default
	{
		Damagetype "Chainsaw";
	}
	States
	{
	Spawn:
		TNT1 AAA 0 NoDelay
		{
			A_SpawnProjectile("ChainsawSparkles",0,0, frandom(0, 360), CMF_AIMDIRECTION, frandom(0, 360));
			A_SpawnProjectile("PuffSparkles",0,0, frandom(0, 360), CMF_AIMDIRECTION, frandom(0, 360));
		}
		TNT1 A 0
		{
			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(0, 359));
		}
		
		PUFF A random(5, 8) Bright;
		PUFF B 4;
		Goto Melee;
	}
}

Class ShotgunPuff : EquestriaBulletPuff
{
	Default
	{
		Scale 1;	DamageType "Shotgun";
	}
	States
	{
	Crash:
		TNT1 A 0
		{
			if (abs(pos.z - GetZAt()) <= 5)
			{
				string floorname = TexMan.GetName(floorpic);
				for (int i = 0; i < liquidfloor.size(); i++)
				{
					if (floorname == liquidfloor[i])
					{
						A_SpawnItemEx("DEWaterSplashChunk", 0,0,0, random(1, 2), random(1, 2), random(2, 4), frandom(0,360));
						Destroy();
					}
				}
			}
		}
		Goto Spawn;
		
	Spawn:
		TNT1 A 0 NoDelay
		{
			if (abs(pos.z - GetZAt()) <= 5)
			{
				if (GetCVar("CV_TESTING"))
					A_SpawnItemEx("EQ_Decal_Shotgun",0,0,0,0,0,0,0, SXF_ABSOLUTEPOSITION);
				
				for (int i = 0; i < 3; i++)
				{ A_SpawnItemEx("PuffSparkles",0,0,1,frandom(-2,2),frandom(-2,2), frandom(3,8), frandom(0,360), SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_NOCHECKPOSITION); }
			}
			else
			{	for (int i = 0; i < 3; i++)
				A_SpawnProjectile("PuffSparkles",0,0, frandom(0, 360), CMF_AIMDIRECTION, frandom(0, 360)); }

			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(0, 359));
		}
		
		PUFF A random(2, 8) Bright;
		PUFF B 4;
		
	Melee:
		PUFF C 4;
		PUFF D 1 A_FadeOut(0.05);
		Wait;
	}
}
	
Class Player_ShotgunPuff : ShotgunPuff
{	Default	{	AttackSound "";	}	}

Class Player_SuperShotgunPuff : Player_ShotgunPuff
{	Default	{	DamageType "SuperShotgun";	}	}

Class KickPuff : ShotgunPuff
{
	Default
	{
		DamageType "Kick";
		AttackSound "*fist";
		Decal "HorseShoe";
		+BLOODLESSIMPACT
		+NODECAL
		+NOEXTREMEDEATH
	}
	States
	{
	
	Crash:
	Spawn:
	Melee:
		PUFF C 4;
		PUFF D 1 A_FadeOut(0.1);
		Wait;
	}
}

Class BerserkPuff : KickPuff
{
	Default
	{
		DamageType "Berserk";
		Alpha 0.5;
		Scale 0.5;
		VSpeed 0.65;
		Decal "HorseShoe";
		-BLOODLESSIMPACT
	}
	States
	{
	
	Crash:
	Spawn:
	Melee:
		RSMK A 1 NoDelay
		{
			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(0, 359));
		}
		RSMK AAAAAABBBBBBCCCCCDDDDDEEEEE 1
		{
			A_FadeOut(0.02);
			A_SetScale(Scale.X + 0.025, Scale.Y + 0.025);
		}
		Wait;
	}
}

Class SmokePuff : BerserkPuff
{	Default{	Alpha 0.6; +NoInteraction	}	}
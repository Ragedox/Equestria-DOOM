Class PuffSparkles : DebrisCore
{
	Default
	{
		+Bright
		+ThruActors
		+DontSplash
		-MISSILE
		
		Scale 0.5;
		
		Height 2; Radius 1;
		
		Gravity 0.25;
		
		Speed 4;
		
		RenderStyle "Add";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay ACS_NamedExecute("CheckLiquid");
		TNT1 A 0 A_JumpIf(waterlevel > 1, "Vanish");
		
		PUFF A random(5, 35);
	Bye:
		PUFF A 1 A_SetScale(Scale.X - 0.05, Scale.Y - 0.05);
		PUFF A 0 A_JumpIf(Scale.X <= 0, "Null");
		Loop;
		
	Vanish:
		PUFF A 1 A_FadeOut(0.1);
		Wait;
	}
}

Class ChainsawSparkles : PuffSparkles
{
	Default
	{
		Gravity 0.5;
		Speed 6;
	}
	States
	{
	Spawn:
		TNT1 A 0 ACS_NamedExecute("CheckLiquid");
		TNT1 A 0 A_JumpIf(waterlevel > 1, "Vanish");
		PUFF A random(35, 70);
	
	Bye:
		PUFF A 1 A_SetScale(Scale.X - 0.05, Scale.Y - 0.05);
		PUFF A 0 A_JumpIf(Scale.X <= 0, "Null");
		Loop;
	}
}

Class EquestriaBulletPuff : Actor replaces BulletPuff
{
	Default
	{
		Height 8; Radius 4;
		RenderStyle "Translucent"; Alpha 0.5;
		Gravity 0.05;
		Mass 5;
		Scale 0.75;
		
		AttackSound "weapons/ricochet";
		
		+NOBLOCKMAP +NOTELEPORT
		+FORCEXYBILLBOARD
		+ROLLSPRITE +ROLLCENTER
		+CORPSE
	}
	States
	{
	
	Spawn:
		TNT1 A 0 NoDelay ACS_NamedExecute("CheckLiquid");
		TNT1 A 0
		{
			if (abs(pos.z - GetZAt()) <= 5)
			{	for (int i = 0; i < 3; i++)
				{
				A_SpawnProjectile("PuffSparkles",0,0, frandom(0, 360), CMF_AIMDIRECTION | CMF_BADPITCH, frandom(50, 90)); }
				/*A_SpawnItemEx("BulletDecal",0,0,0,0,0,0,0);*/ }
			else
			{	for (int i = 0; i < 3; i++)
				A_SpawnProjectile("PuffSparkles",0,0, frandom(0, 360), CMF_AIMDIRECTION, frandom(0, 360)); }

			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(0, 359));
		}
		PUFF A random(5, 8) Bright;
		PUFF B 4;
	
	Melee:
		PUFF C 4;
		PUFF D 1 A_FadeOut(0.1);
		Wait;
	}
	
	Override void Tick()
	{
		Super.Tick();
		if (!isFrozen())
		{
			A_ScaleVelocity(0.96);
		}
	}
}

Class BulletDecal : Actor
{
	Default
	{
		Height 4; Radius 4;
		
		+NOINTERACTION
		+DONTSPLASH
		+NOTELEPORT
		+THRUACTORS
		+NOBLOCKMAP
		+MOVEWITHSECTOR
		+NODAMAGETHRUST
		+GHOST
		+RELATIVETOFLOOR
		
		+FLATSPRITE
		+ROLLSPRITE +ROLLCENTER
		
		RenderStyle "Shaded"; StencilColor "Black";
	}
	States
	{
	Spawn:
		DCAL A 350 NoDelay A_SetRoll(random(0, 359));
		#### # 1 A_FadeOut(0.1);
		Wait;
	}
}

Class Player_BulletPuff : EquestriaBulletPuff
{	Default	{	AttackSound "";	}	}

Class ChainsawPuff : Player_BulletPuff
{
	Default
	{
		Damagetype "Chainsaw";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay ACS_NamedExecute("CheckLiquid");
		TNT1 AAA 0
		{
			A_SpawnProjectile("ChainsawSparkles",0,0, frandom(0, 360), CMF_AIMDIRECTION, frandom(0, 360));
			A_SpawnProjectile("PuffSparkles",0,0, frandom(0, 360), CMF_AIMDIRECTION, frandom(0, 360));
			A_SpawnProjectile("PuffSmoke",0,0, frandom(0, 360), CMF_AIMDIRECTION, frandom(0, 360));
		}
		TNT1 A 0
		{
			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(0, 359));
		}
		
		PUFF A random(5, 8) Bright;
		PUFF B 4;
		Goto Melee;
	}
}

Class ShotgunPuff : EquestriaBulletPuff
{
	Default
	{
		Scale 1;	DamageType "Shotgun";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay ACS_NamedExecute("CheckLiquid");
		TNT1 A 0
		{
			if (abs(pos.z - GetZAt()) <= 5)
			{	for (int i = 0; i < 3; i++)
				{
				A_SpawnProjectile("PuffSparkles",0,0, frandom(0, 360), CMF_AIMDIRECTION | CMF_BADPITCH, frandom(50, 90)); }
				/*A_SpawnItemEx("BulletDecal",0,0,0,0,0,0,0);*/ }
			else
			{	for (int i = 0; i < 3; i++)
				A_SpawnProjectile("PuffSparkles",0,0, frandom(0, 360), CMF_AIMDIRECTION, frandom(0, 360)); }

			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(0, 359));
		}
		
		PUFF A random(2, 8) Bright;
		PUFF B 4;
		
	Melee:
		PUFF C 4;
		PUFF D 1 A_FadeOut(0.05);
		Wait;
	}
}

Class PuffSmoke : Smoke
{
	Default { Speed 5; Scale 1; Alpha 0.25; Gravity 0.25; -NoGravity }
	States
	{
	Spawn:
		PUFF B random(0, 8) NoDelay { bSPRITEFLIP = RandomPick(0, 1); A_SetRoll(random(0, 359)); A_SetScale(frandom(0.5, 1.0));}
		PUFF CD random(0,8);
		PUFF D 1 A_FadeOut(0.1);
		Wait;
	}
}
	
Class Player_ShotgunPuff : ShotgunPuff
{	Default	{	AttackSound "";	}	}

Class Player_SuperShotgunPuff : Player_ShotgunPuff
{	Default	{	DamageType "SuperShotgun";	}	}

Class KickPuff : ShotgunPuff
{
	Default
	{
		DamageType "Kick";
		AttackSound "*fist";
		
		+BLOODLESSIMPACT;
		+NODECAL;
	}
	States
	{
	
	Spawn:
	Melee:
		PUFF C 4;
		PUFF D 1 A_FadeOut(0.1);
		Wait;
	}
}

Class BerserkPuff : KickPuff
{
	Default
	{
		DamageType "Berserk";
		Alpha 0.5;
		Scale 0.5;
		VSpeed 0.65;
		
		-BLOODLESSIMPACT;
	}
	States
	{
	
	Spawn:
	Melee:
		RSMK A 0;
		RSMK A 1
		{
			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(0, 359));
		}
		RSMK AAAAAABBBBBBCCCCCDDDDDEEEEE 1
		{
			A_FadeOut(0.02);
			A_SetScale(Scale.X + 0.025, Scale.Y + 0.025);
		}
		Wait;
	}
}

Class SmokePuff : BerserkPuff
{	Default{	Alpha 0.6; +NoInteraction	}	}
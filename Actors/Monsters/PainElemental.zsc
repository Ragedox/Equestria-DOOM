Class EquestriaPainElemental : PainElemental replaces PainElemental
{
	Default
	{
		Monster;
		
		Tag "$FN_PAIN";
				
		Health 400;
		Height 50; Radius 25; DeathHeight 50;
		Mass 400;
		Speed 8;
		PainChance 100;
		FloatBobStrength 0.25;
		
		+CASTSPRITESHADOW
		+NOGRAVITY +FLOAT  +FLOATBOB 
		+FORCEXYBILLBOARD +AvoidMelee +DONTFALL
		
		SeeSound "pain/sight";
		PainSound "pain/pain";
		DeathSound "pain/death";
		ActiveSound "pain/active";
	}
	States
	{
	Spawn:
		PAIN A 10 A_Look();
		Loop;
	See:
		//PAIN # 0 A_CheckProximity("EquestriaRocket")
		PAIN AABBCC 3 A_Chase();
		Loop;
		
	Missile:
		PAIN D 5 A_FaceTarget();
		PAIN E 5 A_FaceTarget();
		PAIN F 5 BRIGHT A_FaceTarget();
		PAIN F 0 BRIGHT A_Jump(256, "Missile.Pony", "Missile.Unicorn", "Missile.Nirik");
		Goto See;
	
		Missile.Pony:
			PAIN F 0 A_PainAttack("EquestriaLostSoul");
			Goto See;
			
		Missile.Unicorn:
			PAIN F 0 A_PainAttack("UnicornSoul");
			Goto See;
			
		Missile.Nirik:
			PAIN F 0 A_SpawnProjectile("MissileNirikSoul");
			Goto See;
	
	Pain:
		PAIN G 6;
		PAIN G 6 A_Pain();
		Goto See;
		
	Death:
		PAIN H 8;
		PAIN I 8 Bright A_Scream();
		PAIN J 8 Bright;
		PAIN K 8 Bright
		{
			A_Fall();
			A_QuakeEx(3, 3, 3, 4, 0, 500, "", 0,0,0,0, 1);
			
			for (int i = 0; i < 10; i++)
			{
				A_SpawnProjectile("GenericGib", frandom(0, 50), frandom(-25, 25), frandom(0, 359), CMF_AIMDIRECTION, frandom(0, 359));
			}
			for (int i = 0; i < 4; i++)
			{
				A_SpawnItemEx("BloodExplosionSphere",0,0,25,0,0,0,0, SXF_TRANSFERPOINTERS);
				A_SpawnProjectile("BigGoreRandom", frandom(0, 50), frandom(-25, 25), frandom(0, 359), CMF_AIMDIRECTION, frandom(0, 359));
			}
		}
		PAIN L 8 Bright
		{
			A_PainAttack("EquestriaLostSoul", 90);
			A_PainAttack("NirikSoul", 0);
			A_PainAttack("UnicornSoul", -90);
		}
		PAIN M 8 Bright;
		Stop;
		
	Raise:
		PAIN MLKJIH 8;
		Goto See;
		
	Crush:
		PAIN H 4 BRIGHT;
		PAIN I 4 BRIGHT A_Scream();
		PAIN JK 4 BRIGHT;
		PAIN L 4 BRIGHT A_Fall();
		PAIN M 4 BRIGHT;
		Stop;
	}
}


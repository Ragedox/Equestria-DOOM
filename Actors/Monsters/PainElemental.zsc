Class EquestriaPainElemental : DoomEquestriaMonster replaces PainElemental
{
	Default
	{
	//$Title Pain Elemental
		Tag "$FN_PAIN";
				
		Health 400;
		Height 50; Radius 25; DeathHeight 50;
		Mass 400;
		Speed 8 / 3;
		PainChance 100;
		FloatBobStrength 0.25;
		
		+CASTSPRITESHADOW
		+NOGRAVITY +FLOAT  +FLOATBOB 
		+FORCEXYBILLBOARD +AvoidMelee +DONTFALL
		+ADDLIGHTLEVEL
		
		LightLevel 64;
		SeeSound "pain/sight";
		PainSound "pain/pain";
		DeathSound "pain/death";
		ActiveSound "pain/active";
	}
	
	int lostsoulspecie;
	
	States
	{
	Spawn:
		PAIN A 10 A_Look();
		Loop;
	See:
		//PAIN # 0 A_CheckProximity("EquestriaRocket")
		PAIN A 1 A_Chase();
		PAIN AA 1 A_Chase(null,null);
		#### # 1 A_Chase();
		#### ## 1 A_Chase(null,null);
		PAIN B 1 A_Chase();
		PAIN BB 1 A_Chase(null,null);
		#### # 1 A_Chase();
		#### ## 1 A_Chase(null,null);
		PAIN C 1 A_Chase();
		PAIN CC 1 A_Chase(null,null);
		#### # 1 A_Chase();
		#### ## 1 A_Chase(null,null);
		Loop;
		
	Missile:
		#### # 0
		{
			lostsoulspecie = random(0, 2);
			
			if		(lostsoulspecie == 0) A_SetTranslation("");
			else if	(lostsoulspecie == 1) A_SetTranslation(""); //Make a Unicorn version
			else if (lostsoulspecie == 2) A_SetTranslation("PainElemental_Nirik");
		}
		PAIN DE 5
		{
			A_FaceTarget();
			lightlevel = 128;
		}
		PAIN F 3 BRIGHT A_FaceTarget();
		#### E 2
		{
			if		(lostsoulspecie == 0) A_PainAttack("EquestriaLostSoul");
			else if (lostsoulspecie == 1) A_PainAttack("UnicornSoul");
			else if	(lostsoulspecie == 2) A_SpawnProjectile("MissileNirikSoul");
			
			A_SetTranslation("");
			A_Stop();
			lightlevel = 64;
		}
		Goto See;
	
	Pain:
		PAIN G 6;
		PAIN G 6 A_Pain();
		Goto See;
	
	XDeath:
	Death:
		PAIN H 8;
		PAIN I 8 Bright A_Scream();
		PAIN J 8 Bright;
		PAIN K 8 Bright
		{
			A_Fall();
			A_QuakeEx(3, 3, 3, 4, 0, 500, "", 0,0,0,0, 1);
			
			for (int i = 0; i < 10; i++)
			{
				A_SpawnProjectile("GenericGib", frandom(0, 50), frandom(-25, 25), frandom(0, 359), CMF_AIMDIRECTION, frandom(0, 359));
			}
			for (int i = 0; i < 4; i++)
			{
				A_SpawnItemEx("BloodExplosionSphere",0,0,25,0,0,0,0, SXF_TRANSFERPOINTERS);
				A_SpawnProjectile("BigGoreRandom", frandom(0, 50), frandom(-25, 25), frandom(0, 359), CMF_AIMDIRECTION, frandom(0, 359));
			}
		}
		PAIN L 8 Bright
		{
			A_PainAttack("EquestriaLostSoul", 90);
			A_PainAttack("NirikSoul", 0);
			A_PainAttack("UnicornSoul", -90);
		}
		PAIN M 8 Bright;
		Stop;
		
	Crush:
		PAIN H 4 BRIGHT;
		PAIN I 4 BRIGHT A_Scream();
		PAIN JK 4 BRIGHT;
		PAIN L 4 BRIGHT A_Fall();
		PAIN M 4 BRIGHT;
		Stop;
	}
}
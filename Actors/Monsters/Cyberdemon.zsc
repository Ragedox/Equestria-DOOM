Class EquestriaCyberdemon : Cyberdemon replaces Cyberdemon
{
	Default
	{
		//$Sprite CYBRA2
		+CASTSPRITESHADOW;
		//Health 5000;
		Mass 1500;
	}
	States
	{
	Spawn:
		IDLY AB 10 A_Look();
		Loop;
		
	 Missile:
		CYBR E 6 A_FaceTarget();
		CYBR F 12 A_SpawnProjectile("EquestriaRocket", 50, -26);
		CYBR E 12 A_FaceTarget();
		CYBR F 12 A_SpawnProjectile("EquestriaRocket", 50, -26);
		CYBR E 12 A_FaceTarget();
		CYBR F 12 A_SpawnProjectile("EquestriaRocket", 50, -26);
		Goto See;
   
	Death:
		CYBR H 10;
		CYBR I 5;
		CYBR IJKLMNOPPP 0 A_SpawnItemEx("CyberdemonExplosion", random(radius * -1, radius), random(radius * -1, radius), random(32, 110));
		CYBR I 5 A_Scream();
		CYBR J 8;
		CYBR KL 5
		{
			A_QuakeEx(4, 4, 4, 35, 0, 800, "world/quake", QF_SCALEDOWN, 0,0,0, 1);
			
			for (int i = 0; i < 20; i++)
			{
				A_SpawnProjectile("BigGoreRandom", frandom(32, 110), frandom(radius * -1, radius), frandom(0, 359), CMF_AIMDIRECTION, frandom(0, 359));
				A_SpawnProjectile("GenericGib", frandom(32, 110), frandom(radius * -1, radius), frandom(0, 359), CMF_AIMDIRECTION, frandom(0, 359));
			}
		}
		CYBR MNOPP 0
		{
			A_SpawnProjectile("EquestriaBlood", random(height * -1, height), random(radius * -1, radius), random (0, 359), CMF_AIMDIRECTION, random (-180, 0));
			A_SpawnProjectile("BloodTrail", random(height * -1, height), random(radius * -1, radius), random (0, 359), CMF_AIMDIRECTION, random (-180, 0));	
			A_SpawnProjectile("SmallBloodTrail", random(height * -1, height), random(radius * -1, radius), random (0, 359), CMF_AIMDIRECTION, random (-180, 0));	
		}
		CYBR M 5 A_NoBlocking();
		CYBR NO 5;
		CYBR P 30;
		CYBR P -1 A_BossDeath();
		Stop;
	}
}

Class CyberdemonExplosion : DEParticles
{
	Default
	{
		Speed 0;
		RenderStyle "Translucent"; Alpha 0.75;
		+NOINTERACTION
		+ZDOOMTRANS
		
		Height 0; Radius 0;
	}
	States
	{
	Spawn:
		TNT1 A random(0,16) NoDelay
		{
			A_SetScale(frandom(0.5, 1.0));
			A_SpawnItem("BloodExplosionSphere");
		}
		EXP2 A random(3,6);
		EXP2 B 4;
		EXP2 C 1 A_FadeOut(0.1);
		Wait;
	}
}
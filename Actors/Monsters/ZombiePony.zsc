Class EquestriaZombieMan : DoomEquestriaMonster Replaces ZombieMan
{
	Default
	{
	//$Title "Former Trooper"
		Tag "$FN_ZOMBIE"; Obituary "$OB_ZOMBIE";
		Health 20; WoundHealth 10;
		
		Radius 20; Height 56;
		
		Speed 8;
		
		PainChance 200; PainThreshold 4;
		DamageFactor "Chainsaw", 2;
		+FLOORCLIP
		+CASTSPRITESHADOW
		+MISSILEMORE
		
		SeeSound "grunt/sight";
		AttackSound "grunt/attack";
		PainSound "grunt/pain";
		DeathSound "grunt/death";
		ActiveSound "grunt/active";
	
		DropItem "EquestriaClip";
		DoomEquestriaMonster.TrainerBuff true;
	}
	
	/*override void Tick()
	{
		Super.Tick();
		if(health <= woundhealth && health > 0 && bCORPSE == false)
			sprite = getspriteindex("SPOS");
	}*/
	
	States
    {
	Spawn:
        POSS A 10 A_Look();
        Loop;

	/*See:
		POSS AA 5 A_Wander();
		#### # 0 A_LookEx(0,0,0,0,0,"Seek");
		POSS BB 5 A_Wander();
		#### # 0 A_LookEx(0,0,0,0,0,"Seek");
		POSS CC 5 A_Wander();
		#### # 0 A_LookEx(0,0,0,0,0,"Seek");
		POSS DD 5 A_Wander();
		#### # 0 A_LookEx(0,0,0,0,0,"Seek");
		#### # 0 A_Jump(100, 1);
		Loop;
		#### # 35;
		Goto See;*/
		
	See:
        POSS AABBCCDD 4
		{
			EQ_Chase("Melee", "Missile");
			A_CheckProximity("See.Trainer", "PossessedTrainer", 1024, 1);
		}
        Loop;
		
	Missile:
        POSS E 10 A_FaceTarget(180, 270, 0, FAF_MIDDLE);
        POSS F 8
		{
			A_CustomBulletAttack (20, 10, 1, random(1,5)*2, "EquestriaBulletPuff", 0, CBAF_NORANDOM, AAPTR_TARGET, "BulletTracer");
			A_SpawnProjectile("BulletCasingSpawn");
		}
		POSS E 8;
        Goto See;
    
	See.Trainer:
		#### AABBCCDD 4 EQ_Chase("Melee", "Missile");
		Goto See;
		
	Pain.Kick:
		#### # 0 A_Recoil(5);
	Pain:
        POSS G 3;
        POSS G 3 A_Pain();
        Goto See;
   
	Death.Plasma:
		#### # 0 A_SetTranslation("Toasted");
	//Death.BFGSplash:
		#### ##### 0 A_SpawnProjectile("SplashSmoke", random(-20, 20), random (-20, 20), random (0, 360), CMF_AIMDIRECTION, random(0, -90));
	Death:
		/*#### # 0
		{
			if (health < -10)
				return ResolveState("XDeath");
			return state(null);
		}*/
		
        POSS H 5;
        POSS I 5 A_Scream();
        POSS J 4 A_NoBlocking();
        #### J 1 A_CheckFloor(1);
		Wait;
		#### K 5;
		#### L -1;
		Stop;
    
	Death.Shotgun:
		#### # 0 A_JumpIfCloser(radius + 64, 3);
	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(radius + 32, "XDeath");
		#### # 0 A_JumpIfCloser(radius + 128, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,20,0,1);
			A_Recoil(5);
		}
		Goto Death;
	
	XDeath.Plasma: //TO DO CUSTOM PLASMA DEATH
		#### # 0 A_SpawnItem("PlasmaExplosion",0, height / 2);
		Goto XDeath;
		
	Classic.XDeath:
		POSS MNOPQRST 4;
        POSS U -1;
        Stop;
		
	Crush:
		POSS U -1 { bSPRITEFLIP = randompick(0, 1); }
		Stop;
		
	Real.XDeath:
		POSS M 5
		{	
			for (int i = 0; i < 10; i++)
			{
				A_SpawnItemEx("GenericGib", FRandom(radius * -1, radius), FRandom(radius * -1, radius), FRandom(8, 56), Random(2, 5),Random(2, 5),Random(2, 5), FRandom (0, 359));
				A_SpawnItemEx("TinyGenericGib", FRandom(radius * -1, radius), FRandom(radius * -1, radius), FRandom(8, 56), Random(2, 8),Random(2, 8),Random(2, 8), FRandom (0, 359));
			}
			for (int i = 0; i < 6; i++)
			{
				A_SpawnProjectile("Equestria_SmallBlood", 0, 0, FRandom (0, 360), CMF_AIMDIRECTION, FRandom (0, 360));
				A_SpawnProjectile("Equestria_TinyBlood", 0, 0, FRandom (0, 360), CMF_AIMDIRECTION, FRandom (0, 360));
				A_SpawnProjectile("Equestria_Blood", 0, 0, FRandom (0, 360), CMF_AIMDIRECTION, FRandom (0, 360));
			}
			
			A_Fall(); A_XScream();
		}
		POSS NOPQRST 1;
        POSS U -1;
        Stop;

	Death.Berserk:
	XDeath.Berserk:
		#### # 0 ThrustThingZ(0,30,0,1);
		
		#### # 0 A_Jump(256, "MidAir");
		Wait;

	MidAir:
		POSS J 1 A_SetRoll((roll + 20));
		Loop;

	Crash.Berserk:
		#### # 0 A_SetRoll(0);

		#### # 0 A_Jump(256, "XDeath");
		Wait;

	Raise:
        POSS LKJIH 5;
        Goto See;
    }
}

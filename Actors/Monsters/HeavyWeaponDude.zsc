Class EquestriaChaingunGuy : DoomEquestriaMonster replaces ChaingunGuy
{
	Default
	{
		//$Title Former Commando
		Tag "$FN_HEAVY"; Obituary "$OB_CHAINGUY";
		
		Health 70;
		Radius 20; Height 56;
		Mass 100;
		Speed 8;
		PainChance 170; PainThreshold 5;
		
		+FLOORCLIP
		
		SeeSound "chainguy/sight";
		DeathSound "chainguy/death";
		PainSound "chainguy/pain";
		AttackSound "weapons/chaingun";
		ActiveSound "chaingun/active";

		Species "Couple";
		
		Dropitem "EquestriaChaingun";
	}
	States
	{
	Spawn:
		CPOS AB 10 A_Look();
		Loop;
	
	See:
		#### # 0 A_CheckProximity("See.Trainer", "PossessedTrainer", 1024, 1);
		CPOS AABBCCDD 3 A_Chase();
		Loop;
	See.Trainer:
		#### AABBCCDD 2
		{
			A_Chase("Melee", "Missile");
			A_SpawnProjectile("DEWaterSplashChunk", random(32, 56), 0, random (0, 359), CMF_AIMDIRECTION, random (0, -180));
		}
		Goto See;
		
	Missile:
		CPOS E 10 A_FaceTarget(180, 270, 0, FAF_MIDDLE);
		TNT1 A 0 A_JumpIfInTargetLOS("Missile.Stand", 0, JLOSF_DEADNOJUMP, 512);
		TNT1 A 0 A_Jump(256, "Missile.Stand", "Missile.Walking");
		Wait;
		
	Missile.Stand:
		CPOS F 3 bright
		{
			A_CustomBulletAttack(20, 10, 1, random(1,5)*2, "BulletPuff", 0, CBAF_NORANDOM, AAPTR_TARGET, "BulletTracer");
			A_SpawnProjectile("BulletCasingSpawn", 32, 15);
		}
		CPOS E 1 A_CPosRefire();
		CPOS E 0 A_JumpIfInTargetLOS("Missile.Stand", 0, JLOSF_DEADNOJUMP, 512);
		Goto See;
		
	Missile.Walking:
		CPOS F 3 bright
		{
			A_Chase(null,null); A_FaceTarget(180, 270, 0, FAF_MIDDLE);
			A_CustomBulletAttack(10, 5, 1, random(1,5)*2, "BulletPuff", 0, CBAF_NORANDOM, AAPTR_TARGET, "BulletTracer");
			A_SpawnProjectile("BulletCasingSpawn", 32, 15);
		}
		CPOS E 2 { A_Chase(null,null); }
		CPOS E 2 { A_Chase(null,null); }
		CPOS E 0 A_CPosRefire();
		Loop;
	
	Pain:
		CPOS G 2;
		CPOS G 2 A_Pain();
		Goto See;
	
	Death:
		#### # 0 { bSPRITEFLIP = randompick(0, 1); }
		
		CPOS H 5;
		CPOS I 5 A_Scream(); 
		CPOS AAA 0 A_SpawnProjectile("TinyGenericGib", 50, random(-15, -10), random (0, 359), CMF_AIMDIRECTION, random (-90, -10));	
		CPOS J 5 A_NoBlocking();
        CPOS KLM 5;
		CPOS M 1 A_CheckFloor(1);
		Wait;
		CPOS N -1;
		Stop;
		
	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(128, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,15,0,1);
			A_Recoil(2.5);
		}
		Goto Death;
	
	Classic.XDeath:
	Crush:
		CPOS OPQRS 5;
		CPOS T -1;
		Stop;
	
	Raise:
		CPOS NMLKJIH 5;
		
		"####" "#" 0 { bSPRITEFLIP = 0; }
		Goto See;
  }
}
Class PossessedHacker : DoomEquestriaMonster
{
	bool isDead;
	
	Default
	{
	//$Category Monsters
	//$Title Former Hacker
		Tag "$FN_HACKER"; Obituary "$OB_HACKER";
		
		Health 15;
		Radius 20; Height 56;
		Mass 80;
		Speed 10; FastSpeed 15;
		Species "Couple";
		PainChance 170;
		
		+FloorClip
		+MissileMore
		-AvoidMelee
		
		SeeSound "female/sight";
		AttackSound "";
		PainSound "female/pain";
		DeathSound "female/death";
		ActiveSound "female/active";
		
		DropItem "ArmorBonus";
		DropItem "Cell", 64; //25% Chance of drop Cells
		
		MissileType "EquestriaPlasmaBall";
	}
	States
	{
	
	Spawn:
		TECH AB 10 A_Look();
		Loop;
	
	See:
		//#### # 0 A_CheckProximity("Raise", "PossessedTrainer", 1024, 1);
		TECH AABBCCDD 3 A_Chase();
		Loop;
	
	Missile:
		TECH E 1;
		TECH F 2 Bright 
		{
			A_FaceTarget();
			A_SpawnProjectile("EquestriaPlasmaBall", 32, 0, frandom(-2, 2));
		}
		TECH E 1;
		TECH F 2 Bright 
		{
			A_FaceTarget();
			A_SpawnProjectile("EquestriaPlasmaBall", 32, 0, frandom(-4, 4));
		}
		TECH E 1;
		TECH F 2 Bright 
		{
			A_FaceTarget();
			A_SpawnProjectile("EquestriaPlasmaBall", 32, 0, frandom(-6, 6));
		}
		TECH E 20;
		Goto See;
	
	Pain.Kick:
		#### # 0 A_Recoil(5);
	Pain:
		TECH G 3;
		TECH G 3 A_Pain();
		Goto See;
	
	Death:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
		TECH H 5;
		TECH I 5 
		{
			A_AlertMonsters();
			A_Scream();
		}
		#### J 4 A_NoBlocking();
		#### J 1 A_CheckFloor(1);
		Wait;
		#### K 5;
		#### L -1;
		Stop;
	
	Death.Shotgun:
		#### # 0 A_JumpIfCloser(radius + 64, 3);
	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(radius + 32, "XDeath");
		#### # 0 A_JumpIfCloser(radius + 128, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,20,0,1);
			A_Recoil(5);
		}
		Goto Death;
		
	Classic.XDeath:
		#### NOPQRSTU 5;
        #### V -1;
        Stop;
	
	Crush:
		#### V -1 { bSPRITEFLIP = randompick(0, 1); }
		Stop;
		
	Death.Berserk:
	XDeath.Berserk:
		"####" "#" 0 ThrustThingZ(0,30,0,1);
		
		"####" "#" 0 A_Jump(256, "MidAir");
		Wait;
	
	MidAir:
		POSS J 1 A_SetRoll((roll + 20));
		Loop;
		
	Crash.Berserk:
		"####" "#" 0 A_SetRoll(0);
		
		"####" "#" 0 A_Jump(256, "XDeath");
		Wait;
		
	Raise: //Make her transform into a nirik
		TNT1 A 8 A_SpawnItem("ArchvileFire",0);
		TNT1 A 0 { A_SpawnItemEx("DemonNirik",0,0,0,0,0,0,0,32); Destroy(); }
		Stop;
	}
}

Class DemonNirik : PossessedHacker
{
	Default
	{
	//$Category Monsters
		Tag "$FN_DEMONIRIK"; Obituary "$FN_DEMONIRIK";
		
		Health 50;
		
		Radius 20; Height 56;
		
		Mass 100;
		
		Speed 10; FastSpeed 15;
		
		PainChance 100;
		
		+FloorClip
		+ExtremeDeath
		+JumpDown
		+Bright
		-AvoidMelee
		 
		SeeSound "demon/sight";
		AttackSound "demon/melee";
		PainSound "demon/pain";
		DeathSound "female/death";
		ActiveSound "demon/active";
	}
	
	override void Tick()
	{
		if (health > 0 && !InStateSequence(CurState, ResolveState("Spawn")))
		{
			A_SpawnItemEx("EQ_FireBallSmoke",0,0,height/2);
			A_SpawnItemEx("EQ_FireBallTrail",0,0,height/2);
		}
		
		Super.Tick();
	}
	
	States
	{
	
	Spawn:
		TECH AB 10 A_Look();
		Loop;
	See:
		TECH AAAABBBBCCCCDDDD 1 A_Chase();
		TNT1 A 0 A_JumpIf(Random(1, 10) == 5, "TryJump");
		TNT1 A 0 A_JumpIfCloser(128, "TryJump");
		Loop;
	
	Missile:
	TryJump:
		TNT1 A 0 A_CheckFloor("Jump"); //don't jump if mid-air
		goto See;
	
	Jump:
		TECH H 0 A_FaceTarget();
		TECH J 8 A_StartSound("demon/melee");
		TECH H 0 A_Recoil(-15);
		TECH H 0 A_ChangeVelocity(0, 0, 10, 0);
		Goto See;
	
	Melee:
		TECH E 5 A_FaceTarget();
		TECH F 5 Bright A_CustomMeleeAttack(random(1, 10) * 6);
		TECH F 0 A_Recoil(2);
		
		TECH E 5 A_FaceTarget();
		TECH F 5 Bright A_CustomMeleeAttack(random(1, 10) * 6);
		TECH F 0 A_Recoil(2);
		Goto See;
	
	Pain:
		TECH G 3;
		TECH G 3 A_Pain();
		Goto See;
	
	Death:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
		TECH H 5;
		TECH I 5 
		{
			A_AlertMonsters();
			A_Scream();
			A_SpawnItem("ArchvileFire");
		}
		#### J 4 A_NoBlocking();
		#### J 1 A_CheckFloor(1);
		Wait;
		#### K 5;
		#### L -1;
		Stop;
	
	Death.Shotgun:
		#### # 0 A_JumpIfCloser(radius + 64, 3);
	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(radius + 32, "XDeath");
		#### # 0 A_JumpIfCloser(radius + 128, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,20,0,1);
			A_Recoil(5);
		}
		Goto Death;
		
	Classic.XDeath:
		#### NOPQRSTU 5;
        #### V -1;
        Stop;
	
	Crush:
		#### V -1 { bSPRITEFLIP = randompick(0, 1); }
		Stop;
		
	Raise:
		TNT1 A 8
		{
			A_SpawnItem("ArchvileFire",0);
			bSPRITEFLIP = 0;
		}
		Goto See;
	}
}
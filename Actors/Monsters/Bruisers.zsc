Class EquestriaBaronofHell : DoomEquestriaMonster replaces BaronOfHell
{
	Default
	{
	//$Title Baron of Hell
		Tag "$FN_BARON"; Obituary "$OB_BARON"; HitObituary "$OB_BARONHIT";
		
		Health 750;
		Radius 15; Height 64;
		Mass 1000;
		Speed 8;
		PainChance 50; PainThreshold 10;
		BloodColor "darkgreen";
		
		+FLOORCLIP
		+BOSSDEATH
		+E1M8BOSS
		
		SeeSound "baron/sight";
		PainSound "baron/pain";
		DeathSound "baron/death";
		ActiveSound "baron/active";
		
	}
	States
	{
	Spawn:
		IDLB CD 10 A_Look();
		Loop;
	
	See:
		BOSS AABBCCDD 3 A_Chase();
		Loop;
		
	Melee:
		BOSS E 8 A_FaceTarget();
		BOSS F 6 A_FaceTarget();
		BOSS G 6 A_CustomMeleeAttack(10 * random(1, 8), "baron/melee", "monster/miss", "BruisersMelee", true);
		Goto See;
		
	Missile:
		BOSS EF 8 A_FaceTarget();
		BOSS G 8 A_SpawnProjectile("EquestriaBaronBall");
		Goto See;
	
	Pain:
		BOSS H 2;
		BOSS H 2 A_Pain();
		Goto See;
		
	Death:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
		BOSD A 8;
		BOSD B 8 A_Scream();
		BOSD C 8;
		BOSD D 8 A_NoBlocking();
		BOSD E 8;
		BOSD F -1 A_BossDeath;
		Stop;
		
	XDeath:
	Crush:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
		BOSD G 8;
		BOSD H 8 A_Scream();
		BOSD I 8;
		BOSD J 8 A_NoBlocking();
		BOSD KL 8;
		BOSD M -1 A_BossDeath();
		Stop;
		
	Raise:
		BOSS O 8;
		BOSS NMLKJI  8;
		"####" "#" 0 { bSPRITEFLIP = 0; }
		Goto See;
	}
}
 
Class EquestriaHellKnight : EquestriaBaronofHell replaces HellKnight
{
	Default
	{
	//$Title Hell Knight
		Tag "$FN_HELL"; Obituary "$OB_KNIGHT"; HitObituary "$OB_KNIGHTHIT";
		
		Health 500;
		
		Speed 10;
		PainChance 75;
		BloodColor "green";
		-BOSSDEATH
		SeeSound "knight/sight";
		ActiveSound "knight/active";
		PainSound "knight/pain";
		DeathSound "knight/death";
	}
	States
	{
	
	Spawn:
		IDLB AB 10 A_Look();
		Loop;
	
	See:
		BOS2 AABBCCDD 3 A_Chase();
		Loop;
		
	Melee:
		BOS2 EF 6 A_FaceTarget(180, 270,0,0, FAF_MIDDLE);
		BOS2 G 6 A_CustomMeleeAttack(10 * random(1, 8), "baron/melee", "monster/miss", "BruisersMelee", true);
		Goto See;
		
	Missile:
		BOS2 EF 8 A_FaceTarget();
		BOS2 G 8 A_SpawnProjectile("KnightBall");
		Goto See;
	
	Pain:
		BOS2 H 2;
		BOS2 H 2 A_Pain();
		Goto See;
		
	Death:
		#### # 0 { bSPRITEFLIP = randompick(0, 1); }
		
		BOS2 I 8;
		BOS2 J 8 A_Scream();
		BOS2 K 8;
		BOS2 L 8 A_NoBlocking();
		BOS2 MN 8;
		BOS2 O -1;
		Stop;
		
	XDeath:
	Crush:
		#### # 0 { bSPRITEFLIP = randompick(0, 1); }
		
		BOS2 P 5 A_Scream();
		BOS2 Q 5 A_NoBlocking();
		BOS2 R 5 A_XScream;
		BOS2 STUVW 5;
		BOS2 X -1;
		Stop;
		
	Raise:
		BOS2 O 8;
		BOS2 NMLKJI  8;
		
		#### # 0 { bSPRITEFLIP = 0; }
		Goto See;
	}
}

Class BossBaronofHell : EquestriaBaronofHell
{	Default	{	+MISSILEMORE	}
	States
	{
	See:
		BOSS A 1;
		BOSS C 0 ACS_NamedExecuteAlways("BossHealthBar");
	Chase:
		BOSS AABBCCDD 3 A_Chase();
		Loop;
		
	Melee:
	Missile:
		BOSS A 0 A_Jump(128, "AltMissile");
		BOSS EF 6 A_FaceTarget();
		BOSS G 8 A_BruisAttack();
		Goto Chase;
		
	AltMissile:
		BOSS I 12 A_FaceTarget();
		BOSS J 8 A_FaceTarget();
		BOSS K 8
		{
			A_SpawnProjectile("KnightBall", 32, 0, -25, CMF_AIMOFFSET);
			A_SpawnProjectile("KnightBall", 32, 0, 25, CMF_AIMOFFSET);
		}
		Goto Chase;
	}
}

 
Class EquestriaBaronBall : BaronBall replaces BaronBall
{
	Default
	{
		Radius 8; Height 16;
		
		Decal "BaronScorch";
		
		+ForceXYBillBoard
		+Bright
	}
	States
	{
	Spawn:
		BAL7 AB 4 Fast A_SpawnItem("BaronBallTrail");
		Loop;
 
	Death:
		BAL7 CD 6 Bright;
		BAL7 E 1 Bright A_FadeOut(0.1);
		Wait;
	}
}


Class KnightBall : EquestriaBaronBall
{
	Default
	{
		Speed 20; FastSpeed 25;
		Damage 6;
	}
	States
	{
	Spawn:
		BBAL AB 4 Fast A_SpawnItem("BaronBallTrail");
		Loop;
	}
}

Class BaronBallTrail : Trails
{	Default	{	RenderStyle	"Add";	}
	States
	{
	Spawn:
		BAL7 CD 3 Bright;
		BAL7 E 1 Bright A_FadeOut(0.1);
		Wait;
	}
}

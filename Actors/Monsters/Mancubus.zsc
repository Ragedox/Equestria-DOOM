Class EquestriaMancubus : DoomEquestriaMonster replaces Fatso
{
	Default
	{
	//$Title Mancubus
		Tag "$FN_MANCU"; Obituary "$OB_FATSO";
		
		Health 600;
		
		Radius 30; Height 60;
		
		Mass 1028;
		Speed 8;
		PainChance 80;
		
		+FLOORCLIP
		+BOSSDEATH
		+MAP07BOSS1
		-AVOIDMELEE
		
		SeeSound "fatso/sight";
		PainSound "fatso/pain";
		DeathSound "fatso/death";
		ActiveSound "fatso/active";
	}
	States
	{
	Spawn:
		FATT AB 15 A_Look();
		Loop;
	See:
		FATT AABBCCDDEEFF 4 A_Chase();
		Loop;
		
	Missile:
		FATT G 20 Fast A_FatRaise();
		FATT H 10 Bright
		{
			A_SpawnProjectile("EquestriaFatShot", 16, 30, -10);
			A_SpawnProjectile("EquestriaFatShot", 16, -30, 0);
		}
		FATT IG 5 Fast A_FaceTarget(180, 270, 0, FAF_MIDDLE);
		FATT H 10 Bright
		{
			A_SpawnProjectile("EquestriaFatShot", 16, 30, 0);
			A_SpawnProjectile("EquestriaFatShot", 16, -30, 10);
		}
		FATT IG 5 Fast A_FaceTarget(180, 270, 0, FAF_MIDDLE);
		FATT H 10 Bright
		{
			A_SpawnProjectile("EquestriaFatShot", 16, 30, -4);
			A_SpawnProjectile("EquestriaFatShot", 16, -30, 4);
		}
		FATT IG 5 A_FaceTarget(180, 270, 0, FAF_MIDDLE);
		Goto See;
		
	Pain:
		FATT J 3;
		FATT J 3 A_Pain();
		Goto See;
	
	XDeath:
    Death:
		FATT K 6;
		FATT L 6 A_Scream();
		FATT M 6 A_NoBlocking();
		FATT NOPQRS 6;
		FATT T -1 A_BossDeath();
	    Stop;
		
	 Raise:
		FATT R 5;
		FATT QPONMLK 5;
		Goto See;
	}
}

Class EquestriaFatShot : FatShot replaces FatShot
{
	Default
	{
		Height 20; Radius 10;
		
		+ForceXYBillBoard;
		-Randomize;
	}
	States
	{
	Spawn:
		MANF AB 4 Bright
		{
			A_SpawnItem("EquestriaRocketSmoke");
			A_SpawnItemEx("FireBallTrail", 0,0,0,0,0,0,0, SXF_TRANSFERSCALE);
		}
		Loop;
		
	Death:
		TNT1 AAAAAA 0 A_SpawnProjectile("Smoke", random(-32, 32), 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		TNT1 AAAA 0 A_SpawnProjectile("SplashSmoke", random(-20, 20), random (-40, 40), random (0, 359), CMF_AIMDIRECTION, random(0, -90));

		MISL B random(5, 8) Bright A_QuakeEx(4, 4, 4, 8, 0, 175, "", QF_SCALEDOWN, 0,0,0, 1);
		MISL C 5 Bright;
		MISL D 1 Bright A_FadeOut(0.1);
		Wait;
	}
}
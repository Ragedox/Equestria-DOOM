Class EquestriaRevenant : DoomEquestriaMonster replaces Revenant
{
	Default
	{
	//$Title Revenant
		Tag "$FN_REVEN"; Obituary "$OB_UNDEAD";
		
		Health 300;
		Radius 20; Height 60;
		
		Speed 10;
		PainChance 100; PainThreshold 5;
		MeleeThreshold 196;
		Mass 500;
		
		+MISSILEMORE 
		+FLOORCLIP
		
		SeeSound "skeleton/sight";
		PainSound "skeleton/pain";
		DeathSound "skeleton/death";
		ActiveSound "skeleton/active";
		MeleeSound "skeleton/melee";
		HitObituary "$OB_UNDEADHIT";
	}
	States
	{
	Spawn:
		SKEL AB 10 A_Look();
		Loop;
		
	See:
		SKEL AABBCCDDEEFF 2 A_Chase();
		Loop;
		
	Melee:
		SKEL G 0 A_FaceTarget();
		SKEL G 6 A_SkelWhoosh();
		SKEL H 6 A_FaceTarget();
		SKEL I 6 { A_Recoil(-4); A_SkelFist(); }
		Goto See;
		
	Missile:
		SKEL J 10 bright A_FaceTarget();
		SKEL K 10 A_SpawnProjectile("EquestriaRevenantTracer", 55, randompick(-8, 8));
		SKEL K 10 A_FaceTarget();
		Goto See;
	
	Pain:
		SKEL L 5;
		SKEL L 5 A_Pain();
		Goto See;
		
	Death:
	XDeath:
		#### # 0 { bSPRITEFLIP = randompick(0,1); }
		SKEL LM 7;
		SKEL N 7 A_Scream();
		SKEL O 7 A_NoBlocking();
		SKEL P 7;
		SKEL Q -1;
		Stop;
		
	Raise:
		SKEL Q 5;
		SKEL PONML 5;
		#### # 0 { bSPRITEFLIP = 0; }
		Goto See;
	}
}

Class EquestriaRevenantTracer : RevenantTracer replaces RevenantTracer
{
	Default
	{
		Projectile;
		
		Decal "PlasmaScorchLower";
		
		Height 12; Radius 6;
		
		RenderStyle "Normal"; Alpha 1;
		
		Speed 15;
		
		Damage 10;
		
		+ForceXYBillboard
		+DontSeekInvisible
		+SeekerMissile
		+Randomize
		+CASTSPRITESHADOW
		-ZDOOMTRANS
		
		SeeSound "skeleton/attack";
		DeathSound "skeleton/tracex";
	}
	States
	{
	Spawn:
		FATB AB 2 A_Tracer();
		FATB A 0 A_SpawnItemEx("RevenantTracerTrail");
		Loop;
		
	Death:
		TNT1 AAAAAAAAAA 0
		{
			A_SpawnProjectile("ChainsawSparkles",0,0, frandom (0, 359), CMF_AIMDIRECTION, frandom (0, 359));
			A_SpawnProjectile("PuffSparkles",0,0, frandom (0, 359), CMF_AIMDIRECTION, frandom (0, 359));
		}
		FBXP A 8 Bright;
		FBXP B 6;
		FBXP C 1 A_FadeOut(0.1);
		Wait;
	}
}

Class RevenantTracerTrail : Equestria_Trails
{
	Default
	{
		RenderStyle "Translucent";
	}
	States
	{
	Spawn:
		FBXP AB 3 Bright A_FadeOut(0.125);
		FBXP C 1 A_FadeOut(0.1);
		Stop;
	}
}
//For DOOM Equestria : Hell on Earth

Class Harbinger_of_Doom : Actor
{}

//It still needs to do gore and others
Class SS_Soldier : DoomEquestriaMonster Replaces WolfensteinSS
{
	Default
	{
		Health 50;
		Height 56; Radius 20;
		Speed 10;
		PainChance 128; PainThreshold 4;
		
		+FLOORCLIP
		+NOINFIGHTSPECIES
		
		SeeSound "wolfss/sight";
		PainSound "wolfss/pain";
		DeathSound "wolfss/death";
		ActiveSound "wolfss/active";
		AttackSound "wolfss/attack";
		Obituary "$OB_WOLFSS";
		Tag "$FN_WOLFSS";
		Dropitem "Clip";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256, 1,2,3,4,5,6);
		Wait;
		
		White:			#### # 0 { A_SetTranslation("Nazi_White");		}	Goto Ready;
		LightishRed:	#### # 0 { A_SetTranslation("Nazi_LightishRed");}	Goto Ready;
		LimeGreen:		#### # 0 { A_SetTranslation("Nazi_LimeGreen");	}	Goto Ready;
		FakeGold:		#### # 0 { A_SetTranslation("Nazi_FakeGold");	}	Goto Ready;
		Orange:			#### # 0 { A_SetTranslation("Nazi_Orange");		}	Goto Ready;

	Ready:
	Idle:
		SSWV AB 10 A_Look();
		Loop;
	
	See:
		SSWV AABBCCDD 4 Fast A_Chase();
		Loop;
	
	Missile:
		SSWV EF 10 A_FaceTarget(180);
		SSWV G 4 BRIGHT A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "BulletPuff", 0, CBAF_NORANDOM);
		SSWV F 6 A_FaceTarget(180);
		SSWV G 4 BRIGHT A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "BulletPuff", 0, CBAF_NORANDOM);
		SSWV F 1 A_CPosRefire();
		Goto Missile+1;
	
	Pain:
		//#### # 0 A_Jump(256, 1, "PainAvoid");
		//Wait;
		SSWV H 3;
		SSWV H 3 A_Pain();
		Goto See;
	PainAvoid:
		#### # 0 ThrustThing(angle * 256 / 360 + randompick(90, 180), 10, 0, 0);
		Goto Missile;
	
	Death.Shotgun:
		#### # 0 A_JumpIfCloser(128, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,20,0,1);
			A_Recoil(2);
		}
		Goto Death;

	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(75, "XDeath");
		#### # 0 A_JumpIfCloser(150, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,25,0,1);
			A_Recoil(5);
		}
		Goto Death;
		
	Death:
		SSWV I 5 { bSPRITEFLIP = randompick(0, 1); }
		SSWV J 5 A_Scream();
		SSWV K 4 A_NoBlocking();
		#### K 1 A_CheckFloor(1);
		Wait;
		#### L 5;
		#### M -1;
		Stop;
	
	Classic.XDeath:
		SSWV N 4 { A_Fall(); A_XScream(); A_SpawnProjectile("SS_Cap", 56, 0, frandom(0,356), CMF_AIMDIRECTION, frandom(-180,0)); } 
		SSWV OPQRSTU 4;
		SSWV V -1;
		Stop;
	
	Raise:
		SSWV MLKJI 5;
		
		#### # 0 { bSPRITEFLIP = 0; }
		Goto See;
	}
}

Class SS_Cap : DebrisCore
{
	Default
	{
		Gravity 0.1;
		Speed 2;
	}
	States
	{
	Spawn:
		SSWV A 15;
		SSWV A 0 A_Stop();
	Fall:
		SSWV A 15 A_Recoil(4);
		SSWV A 15 A_Recoil(-8);
		Loop;
		
	Death:
		SSWV M -1;
		Stop;
	}
}
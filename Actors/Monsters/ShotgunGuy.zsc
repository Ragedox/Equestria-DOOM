Class EquestriaShotgunGuy : DoomEquestriaMonster replaces ShotgunGuy
{
	Default
	{
	//$Title "Former Sergeant"
		Tag "$FN_SHOTGUN"; Obituary "$OB_SHOTGUY";
		
		Health 30;
		Radius 20; Height 56;
		Mass 100;
		Speed 8;
		
		PainChance 170; PainThreshold 4;
		
		+FLOORCLIP
		
		SeeSound "shotguy/sight";
		AttackSound "shotguy/attack";
		PainSound "shotguy/pain";
		DeathSound "shotguy/death";
		ActiveSound "shotguy/active";
		
		DropItem "EquestriaShotgun";
		DoomEquestriaMonster.TrainerBuff true;
	}
	States
	{
	
	Spawn:
		ISPS A 10 A_Look();
		Loop;
	
	See:
		//#### # 0 A_CheckProximity("See.Trainer", "PossessedTrainer", 1024, 1);
		SPOS AABBCCDD 3 A_Chase(); 
		Loop;
	See.Trainer:
		#### AABBCCDD 2
		{
			A_Chase("Melee", "Missile");
			A_SpawnProjectile("DEWaterSplashChunk", random(32, 56), 0, random (0, 359), CMF_AIMDIRECTION, random (0, -180));
		}
		#### # 0 A_CheckProximity("See.Trainer", "PossessedTrainer", 1024, 1);
		Goto See;
		
	Missile:
		SPOS E 10 A_FaceTarget(180, 270, 0, FAF_MIDDLE);
		SPOS F 10 Bright
		{
			A_CustomBulletAttack(25, 20, 6, random(1,3)*3, "ShotgunPuff", 0, CBAF_NORANDOM, AAPTR_TARGET, "ShotgunTracer");
			A_SpawnProjectile("ShotCasingSpawn");
		}
		SPOS E 5 A_Jump(128, 1);
		Goto See;
		SPOS E 5 A_JumpIfInTargetLOS("Missile", 0, JLOSF_DEADNOJUMP, 256);
		Goto See;
	
	Pain.Kick:
		#### # 0 A_Recoil(5);
	Pain:
        SPOS G 3;
        SPOS G 3 A_Pain();
        Goto See;
	
	Death.Plasma:
		"####" "#" 0 A_SetTranslation("Toasted");
		"####" "#####" 0 A_SpawnProjectile("SplashSmoke", random(-20, 20), random (-20, 20), random (0, 360), CMF_AIMDIRECTION, random(0, -90));
	Death:
        SPOS H 5;
        SPOS I 5 A_Scream();
        #### J 4 A_NoBlocking();
        #### J 1 A_CheckFloor(1);
		Wait;
		#### K 5;
		#### L -1;
		Stop;
    
	Death.Shotgun:
		#### # 0 A_JumpIfCloser(radius + 64, 3);
	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(radius + 32, "XDeath");
		#### # 0 A_JumpIfCloser(radius + 128, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,15,0,1);
			A_Recoil(2.5);
		}
		Goto Death;
    
	Classic.XDeath:
		SPOS MNOPQRST 5;
		SPOS U -1;
		Stop;
		
	Crush:
		SPOS U -1;
		Stop;
		
	Death.Berserk:
	XDeath.Berserk:
		"####" "#" 0 ThrustThingZ(0,30,0,1);
		
		"####" "#" 0 A_Jump(256, "MidAir");
		Wait;
	
	MidAir:
		SPOS J 1 A_SetRoll((roll + 20));
		Loop;
		
	Crash.Berserk:
		"####" "#" 0 A_SetRoll(0);
		
		"####" "#" 0 A_Jump(256, "XDeath");
		Wait;
		
	Raise:
        SPOS LKJIH 5;
        Goto See;
    }
}

Class EquestriaArchvile : DoomEquestriaMonster replaces Archvile
{
	Default
	{
	//$Title Arch-Vile
		Tag "$FN_ARCH"; Obituary "$OB_VILE";
		
		Health 700;
		
		Radius 20; Height 56;
		
		Mass 500;
		Speed 15;
		PainChance 10;
		MaxTargetRange 896;
		
		+QUICKTORETALIATE 
		+FLOORCLIP 
		+NOTARGET
		
		SeeSound "vile/sight";
		PainSound "vile/pain";
		DeathSound "vile/death";
		ActiveSound "vile/active";
		MeleeSound "vile/stop";
	}
	States
	{
	Spawn:
		IDLV AB 5 A_Look();
		Loop;
	
	See:
		VILE AABBCCDDEEFF 2 A_VileChase();
		Loop;
	
	Missile:
		VILE G 10 Bright
		{
			A_VileStart();
			A_FaceTarget();
		}
		VILE H 8 Bright A_VileTarget();
		VILE IJKLMN 8 Bright A_FaceTarget();
		VILE O 8 Bright A_VileAttack();
		VILE P 16 Bright;
		Goto See;
		
	Melee:
		VILE LMN 4 Bright Fast A_FaceTarget();
		VILE O 4 Bright A_SpawnProjectile("ArchVileFireBall");
		VILE P 8;
		Goto See;
	
	Heal:
		VILE [\] 10 Bright;
		Goto See;
	
	Pain:
		VILE Q 5;
		VILE Q 5 A_Pain();
		Goto See;
	
	XDeath:
	Death:
		VILE Q 7 { bSPRITEFLIP = randompick(0, 1); }
		VILE R 7 A_Scream();
		VILE S 7 A_NoBlocking();
		VILE TUVWXY 7;
		VILE Z -1;
		Stop;
	}
}

class EquestriaArchvileFire : Actor replaces ArchvileFire
{
	Default
	{
		+NOBLOCKMAP +NOGRAVITY +ZDOOMTRANS +FORCEXYBILLBOARD
		RenderStyle "Add";
	}
	States
	{
	Spawn:
		FIRE A 2 BRIGHT  A_StartFire;
		FIRE BAB 2 BRIGHT  A_Fire;
		FIRE C 2 BRIGHT  A_FireCrackle;
		FIRE BCBCDCDCDEDED 2 BRIGHT  A_Fire;
		FIRE E 2 BRIGHT  A_FireCrackle;
		FIRE FEFEFGHGHGH 2 BRIGHT  A_Fire;
		Stop;
	}
}

Class ArchVileFireBall : EquestriaFatShot
{
	Default { ProjectileKickBack 512; }
	States
	{
	Death:
		TNT1 AAAAAA 0 A_SpawnProjectile("Smoke", random(-32, 32), 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		TNT1 AAAA 0 A_SpawnProjectile("SplashSmoke", random(-20, 20), random (-40, 40), random (0, 359), CMF_AIMDIRECTION, random(0, -90));

		MISL B random(5, 8) Bright A_QuakeEx(4, 4, 4, 8, 0, 175, "", QF_SCALEDOWN, 0,0,0, 1);
		MISL C 5 Bright;
		MISL D 1 Bright A_FadeOut(0.1);
		Wait;
	}
}
Class PossessedTrainer : DoomEquestriaMonster
{
	Default
	{
	//$Category Monsters
	//$Title Former Trainer
		Tag "$FN_TRAINER"; Obituary "$OB_BURSTBAT";
		
		Health 75;
		
		Radius 20; Height 56;
		
		Mass 80;
		Speed 6; FastSpeed 8;
		
		PainChance 128; PainThreshold 8;
		
		ReactionTime 0;
		
		+FLOATBOB FloatSpeed 8;
		+FLOAT
		+NOGRAVITY
		+QUICKTORETALIATE
		+MISSILEMORE
		+AVOIDMELEE
		+BOSS
		+LOOKALLAROUND
		
		SeeSound "female/sight";
		AttackSound "weapons/chngun";
		PainSound "female/pain";
		DeathSound "female/death";
		ActiveSound "female/active";

		DropItem "LargeClip";
	}
	override void Tick()
	{
		Super.Tick();
		
		if (health <= 0 || !target)
            return;
		
		if(CheckProximity("EquestriaDoomPlayer", 1024, 1, CPXF_CHECKSIGHT | CPXF_ANCESTOR | CPXF_SETTARGET | CPXF_CLOSEST))
			A_PrintBold("!She can see you!", 0, "BIGFONT");
		else
			A_PrintBold("She can hear you", 0, "BIGFONT");
	}
		
	States
	{
	Spawn:
		CPOS A 1 A_LookEx(LOF_FULLVOLSEESOUND, 0, 512, 0, 360);
		Loop;

	See:
		CPOS A 0 A_AlertMonsters(512);
		CPOS AABBCCDD 3 A_Chase("Melee", "Missile", CHF_FASTCHASE);
		Loop;
	
	Kick:
		SKUL C 10 Bright A_FaceTarget();
		SKUL D 4 Bright A_SkullAttack();
		SKUL CD 4 Bright;
		Goto Missile+2;
	
	Melee:
	Dodge:
		CPOS E 8 ThrustThingZ(0, 50, 0, 0);
		CPOS E 0 A_Stop();
		Goto Missile+2;

	Missile:
		#### # 0
		{
			A_FaceTarget();
			
			if(!CheckProximity("EquestriaDoomPlayer", 1024, 1, CPXF_CHECKSIGHT | CPXF_ANCESTOR))
				SetStateLabel("See");
				
			if(CheckProximity("EquestriaDoomPlayer", 128, 1, CPXF_CHECKSIGHT | CPXF_ANCESTOR))
				SetStateLabel("Dodge");
			if(CheckSight(target)) // Attack if enemy hides behind wall
				if(!CheckLof() && CheckProximity("PossessedTrainer", 1024, 1, CPXF_CHECKSIGHT)) // Is something in the way
					SetStateLabel("See");
		}
		CPOS A 0 A_JumpIfCloser(128, "Dodge");
		//CPOS A 0 A_Jump(128, "Kick");
		
		CPOS E 3;
		CPOS F 4 Bright 
		{
			A_CustomBulletAttack(10, 0, 1, random(1,5)*3, "EquestriaBulletPuff", 0, CBAF_NORANDOM, AAPTR_TARGET, "BulletTracer");
			A_SpawnProjectile("BulletCasingSpawn");
		}
		CPOS E 3 A_FaceTarget();
		CPOS F 4 Bright
		{
			A_CustomBulletAttack(15, 10, 1, random(1,5)*3, "EquestriaBulletPuff", 0, CBAF_NORANDOM, AAPTR_TARGET, "BulletTracer");
			A_SpawnProjectile("BulletCasingSpawn");
		}
		CPOS E 5 A_FaceTarget();
		CPOS F 4 Bright
		{
			A_CustomBulletAttack(15, 10, 1, random(1,5)*3, "EquestriaBulletPuff", 0, CBAF_NORANDOM, AAPTR_TARGET, "BulletTracer");
			A_SpawnProjectile("BulletCasingSpawn");
		}
		Goto See;
	
	Pain:
		CPOS G 1;
		CPOS G 2 A_Pain();
		Goto See;
	
	Death.Plasma:
		"####" "#" 0 A_SetTranslation("Toasted");
		"####" "#####" 0 A_SpawnProjectile("SplashSmoke", random(-20, 20), random (-20, 20), random (0, 360), CMF_AIMDIRECTION, random(0, -90));
	Death:
		HEAD G 8;
		HEAD H 8 A_Scream();
		HEAD I 8;
		HEAD J 8;
		HEAD K 8 A_NoBlocking();
		HEAD L -1 A_SetFloorClip();
		Stop;
		
	Death.Shotgun:
		#### # 0 A_JumpIfCloser(radius + 64, 3);
	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(radius + 32, "XDeath");
		#### # 0 A_JumpIfCloser(radius + 128, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,15,0,1);
			A_Recoil(2.5);
		}
		#### H 5;
        #### I 5 A_Scream();
        #### J 5 A_NoBlocking();
        #### K 5;
		#### K 1 A_CheckFloor(1);
		Wait;
		#### L -1;
		Stop;
									
	Classic.XDeath:
	Crush:
		CPOS OPQRS 5;
		CPOS T -1 A_SetFloorClip();
		Stop;
	
	Death.Berserk:
	XDeath.Berserk:
		"####" "#" 0 ThrustThingZ(0,30,0,1);
		
		"####" "#" 0 A_Jump(256, "MidAir");
		Wait;
	
	MidAir:
		POSS J 1 A_SetRoll((roll + 20));
		Loop;
		
	Crash.Berserk:
		"####" "#" 0 A_SetRoll(0);
		
		"####" "#" 0 A_Jump(256, "XDeath");
		Wait;
		
	Raise:
		CPOS N 5 A_UnSetFloorClip();
		CPOS MLKJIH 5;
		Goto See;
	}
}
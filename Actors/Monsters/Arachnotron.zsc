Class EquestriaArachnotron : Arachnotron replaces Arachnotron
{
	Default
	{
		Radius 40; Height 50;
		+CastSpriteShadow
		Translation "160:167=112:127";
	}
	States
	{
	
	Missile:
		BSPI A 20 A_FaceTarget();
		BSPI G 4 bright A_SpawnProjectile("EquestriaAracPlasmaBall", 16, 4);
		BSPI H 4 bright;
		BSPI H 1 bright A_SpidRefire();
		Goto Missile+1;
   
	}
}

Class EquestriaAracPlasmaBall : EquestriaPlasmaBall replaces ArachnotronPlasma
{
	Default
	{
		Decal "ArachnotronScorch";
		SeeSound "baby/attack";
		DeathSound "baby/shotx";
	}
	States
	{
	Spawn:
		APLS A 0 NoDelay
		{
			A_SetRoll(random(0, 359)); bSPRITEFLIP = RandomPick(0,1);
			A_SpawnProjectile("AracPlasmaSparkle", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		}
		
	Projectile:
		APLS AB 1 Bright
		{
			A_SpawnItemEx("AracPlasmaTrail",0,0,0,0,0,0,0, SXF_TRANSFERSPRITEFRAME | SXF_TRANSFERROLL);
			A_SetRoll((roll + 15), SPF_INTERPOLATE);
		}
		Loop;
 
	XDeath:
		APBX AB 0 A_SpawnItemEx("NoBlood_TinyGenericGib",0,0,0,random(1, 2),random(1, 2),random(1, 2),frandom(0, 359),0, 128);
	Crash:
	Death:
		APBX A 0 A_SpawnItem("SmokePuff");
		APBX AAAA 0 A_SpawnProjectile("AracPlasmaSparkle", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		APBX ABC 4 Bright;
		APBX DE 4 A_FadeOut(0.1);
		Stop;
	}
}

Class AracPlasmaTrail : PlasmaTrail
{	Default { Alpha 0.7; }
	States
	{
	Spawn:
		TNT1 A 2 NoDelay { A_SetRoll(random(0, 359)); bSPRITEFLIP = RandomPick(0,1); }

		APLS A 1 Bright A_FadeOut(0.1);
		Wait;
	}
}
	
Class AracPlasmaSparkle : PlasmaSparkle
{
	States
	{
	Spawn:
		PLS2 B 0 NoDelay A_SetRoll(random(0, 359));
		
		PLS2 B random(15,25);
		
	Death:
		PLS2 BBBB 1 A_SetScale(Scale.X + 0.25, Scale.Y + 0.25);
		PLS2 BBBB 1 A_SetScale(Scale.X - 0.25, Scale.Y - 0.25);
		Stop;
	}
}
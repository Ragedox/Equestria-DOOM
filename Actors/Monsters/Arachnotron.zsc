Class EquestriaArachnotron : DoomEquestriaMonster replaces Arachnotron
{
	Default
	{
	//$Title Arachnotron
		Tag "$FN_ARACH"; Obituary "$OB_BABY";
		
		Health 500;
		
		Radius 50; Height 50;
		Mass 500;
		Speed 12;
		PainChance 128;
		
		+FLOORCLIP
		+BOSSDEATH
		+MAP07BOSS2
		+FLOORCLIP
		+NORADIUSDMG
		+DONTMORPH
		+VULNERABLE
		-AVOIDMELEE
		-SHOOTABLE
		+NONSHOOTABLE
		
		SeeSound "baby/sight";
		PainSound "baby/pain";
		DeathSound "baby/death";
		ActiveSound "baby/active";
	}
	
	override void PostBeginPlay()
	{
		A_SpawnItemEx("Equestria_ArachnotronHitbox",0,0,0,0,0,0,0,SXF_SETMASTER | SXF_NOCHECKPOSITION);
	}
	
	States
	{
	Spawn:
		BSPI AB 10 A_Look();
		Loop;
		
	See:
		BSPI A 20;
		BSPI A 3 A_BabyMetal();
		BSPI ABBCC 3 A_Chase();
		BSPI D 3 A_BabyMetal();
		BSPI DEEFF 3 A_Chase();
		Goto See+1;
		
	Missile:
		BSPI A 20 A_FaceTarget(90, 270, 0, FAF_MIDDLE);
		BSPI G 4 bright A_SpawnProjectile("EquestriaAracPlasmaBall", 16, 4);
		BSPI H 4 bright;
		BSPI H 0  bright A_SpidRefire();
		Goto Missile+1;
   
   Pain:
		BSPI I 3;
		BSPI I 3 A_Pain();
		Goto See+1;
		
	XDeath: //to do
	Death:
		BSPI J 20 A_Scream();
		BSPI K 7 A_NoBlocking();
		BSPI LMNO 7;
		BSPI P -1 A_BossDeath();
		Stop;
		
    Raise:
		BSPI P 5;
		BSPI ONMLKJ 5;
		Goto See+1;
	}
}

Class EquestriaAracPlasmaBall : EquestriaPlasmaBall replaces ArachnotronPlasma
{
	Default
	{
		Decal "GreenPlasmaScorch";
		SeeSound "baby/attack";
		DeathSound "baby/shotx";
	}
	States
	{
	Spawn:
		BAL4 A 0 NoDelay
		{
			A_SetRoll(random(0, 359)); bSPRITEFLIP = RandomPick(0,1);
			A_SpawnProjectile("AracPlasmaSparkle", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		}
	Projectile:
		BAL4 AB 1 Bright
		{
			A_SpawnItemEx("AracPlasmaTrail",0,0,0,0,0,0,0, SXF_TRANSFERSPRITEFRAME | SXF_TRANSFERROLL);
			A_SetRoll((roll + 15), SPF_INTERPOLATE);
		}
		Loop;
 
	Crash:
	Death:
		APBX A 0 A_SpawnItem("SmokePuff");
		APBX AAAA 0 A_SpawnProjectile("AracPlasmaSparkle", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		APBX ABC 4 Bright;
		APBX DDDDEEEE 1 A_FadeOut(0.025);
		Stop;
	}
}

Class AracPlasmaTrail : PlasmaTrail
{	Default { Alpha 0.7; }
	States
	{
	Spawn:
		TNT1 A 2 NoDelay { A_SetRoll(random(0, 359)); bSPRITEFLIP = RandomPick(0,1); }

		BAL4 A 1 Bright A_FadeOut(0.1);
		Wait;
	}
}
	
Class AracPlasmaSparkle : PlasmaSparkle
{
	States
	{
	Spawn:
		PLS2 B 0 NoDelay A_SetRoll(random(0, 359));
		
		PLS2 B random(15,25);
		
	Death:
		PLS2 BBBB 1 A_SetScale(Scale.X + 0.25, Scale.Y + 0.25);
		PLS2 BBBB 1 A_SetScale(Scale.X - 0.25, Scale.Y - 0.25);
		Stop;
	}
}

Class Equestria_ArachnotronHitbox : Equestria_MastermindHitbox
{
	Default
	{
		Radius 25; Height 30;
		
		PainThreshold 5;
		PainChance 128;
		PainChance "EquestriaBFGBall", 128; PainChance "EquestriaBFGSplash", 128;
		DamageFactor "BFGSplash", 1.0;
	}

	override void Tick()
	{
		if(!master)
			Destroy();
			
		Super.Tick();
								
		A_Warp(AAPTR_MASTER,0,0,22,master.angle,WARPF_INTERPOLATE|WARPF_COPYINTERPOLATION|WARPF_NOCHECKPOSITION);
			
		if(master.health < 1)
			Destroy();
		
		if(self.health < self.lastHealth)
			A_DamageMaster(int(self.lastHealth - self.Health),"None", DMSS_INFLICTORDMGTYPE ); self.lasthealth = self.health;
	}
}
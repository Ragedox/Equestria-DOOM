Class EquestriaCacodemon : Cacodemon replaces Cacodemon
{
	Default
	{
		Monster;
		
		Tag "$FN_CACO";
		
		Health 400;
		
		Height 50; Radius 25;
		
		Mass 750;
		Speed 10;
		PainChance 128;
		BloodColor "blue";
		
		+NOGRAVITY
		+FLOAT
		+CASTSPRITESHADOW
		+FLOATBOB
		+FORCEXYBILLBOARD
		FloatBobStrength 0.5;
		
		SeeSound "caco/sight";
		PainSound "caco/pain";
		DeathSound "caco/death";
		ActiveSound "caco/active";
		
		Obituary "$OB_CACO";
		HitObituary "$OB_CACOHIT";
	}
	States
	{

	Spawn:
		HEAD A 10 A_Look;
		Loop;
	
	See:
		HEAD A 3 A_Chase();
		Loop;
	
	Melee:
		HEAD BCDD 2
		{
			A_Chase(null,null);
			A_FaceTarget();
		}
		HEAD C 3
		{
			A_Chase(null,null);
			A_FaceTarget();
		}
	Melee2:
		HEAD C 3 A_Recoil(-15);
		HEAD B 4;
		
		HEAD A 8 A_CustomMeleeAttack(10 * random(1, 6), "demon/melee", "skeleton/swing");
		HEAD A 0 A_Stop();
		Goto See;
	
	Missile:
		HEAD BC 5
		{
			A_FaceTarget();
			A_JumpIfCloser(radius + 128, "Melee2");
		}
		HEAD D 5 Bright { A_SpawnProjectile("EquestriaCacodemonBall", 16); A_Recoil(-2); }
		HEAD A 0 A_Stop();
		Goto See;
	
	Death:
		TNT1 A 0 
		{
			A_Scream();
			bFLOATBOB = 0;
			bFORCEXYBILLBOARD = 0;
		}
		HEAD G 4 A_SetSolid();
		HEAD G 4 A_SetShootable();
		HEAD H 8;
		HEAD H -1;
		Stop;
	
	Crash:
		HEAD IJ 4;
		HEAD K 2 A_NoBlocking();
		HEAD K 1 A_UnsetSolid();
		HEAD K 1 A_UnSetShootable();
		HEAD L -1 A_SetFloorClip();
		Stop;
	
	Raise:
		HEAD L 8 A_UnSetFloorClip();
		HEAD KJIHG 8;
		
		HEAD A 0
		{
			bFLOATBOB = 1;
			bFORCEXYBILLBOARD = 1;
		}
		Goto See;

	}
}

Class EquestriaCacodemonBall : Actor replaces CacodemonBall
{
	Default
	{
		Projectile;
		
		Radius 7; Height 14;
		Speed 15; FastSpeed 25;
		Damage 5;
		
		+ForceXYBillBoard
		+Randomize
		+ZDoomTrans
		+Bright
		+RollSprite +RollCenter
		
		RenderStyle "Add"; Alpha 1;
		
		SeeSound "caco/attack";
		DeathSound "caco/shotx";
	}
	States
	{
	Spawn:
		BAL2 AAAABBBB 1 A_SetRoll(roll + Speed, SPF_INTERPOLATE);
		Loop;
		
	Death:
		#### # 0
		{
			A_SetRoll(0);
			A_SetRenderStyle(0.75, STYLE_Translucent);
		}
		BAL2 CDE 6;
		BAL2 E 1 A_FadeOut(0.1);
		Wait;
	}
}
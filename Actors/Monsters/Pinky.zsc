Class EquestriaPinky : DoomEquestriaMonster replaces Demon
{
	Default
	{
	//$Title Pinky
		Tag "$FN_DEMON"; Obituary "$OB_DEMONHIT";
		
		Health 150;
		PainChance 180; PainThreshold 10;
		Speed 10;
		Height 56; Radius 20;
		Mass 400;
		
		+FLOORCLIP
		+EXTREMEDEATH
		
		SeeSound "demon/sight";
		AttackSound "demon/melee";
		PainSound "demon/pain";
		DeathSound "demon/death";
		ActiveSound "demon/active";
	}
	States
	{
	Spawn:
		IDLP AB 10 A_Look();
		Loop;
		
	See:
		SARG # 0 A_JumpIfInTargetInventory("EquestriaBlurSphere", 1, "Idle");
		SARG AABBCCDD 2 Fast A_Chase();
		SARG # 0 A_JumpIfInTargetInventory("EquestriaBlurSphere", 1, "Idle");
		Loop;

	Idle:
		SARG # 0 A_ClearTarget();
		SARG AABBCCDD 4 A_Wander();
		SARG # 0 A_JumpIfInTargetInventory("EquestriaBlurSphere", 1, "Idle");
		Goto See;
	
	Melee:
		SARG EF 8 Fast A_FaceTarget();
		SARG G 8 Fast A_SargAttack();
		Goto See;
		
	Pain:
		SARG H 2 Fast;
		SARG H 2 Fast A_Pain();
		Goto See;
	
	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(radius + 32, "XDeath");
	Death:
		SARG I 8;
		SARG J 8 A_Scream();
		SARG K 6 A_Recoil(4);
		SARG L 4 A_NoBlocking();
		SARG M 2;
		SARG N -1;
		Stop;
		
	Classic.XDeath:
	Crush:
		SARG O 4 { A_Fall(); A_XScream(); }
		SARG PQRSTUVW 4;
        SARG X -1;
        Stop;
		
	Raise:
		SARG NMLKJI 5;
		Goto See;
	}
}

Class EquestriaSpectre : EquestriaPinky replaces Spectre
{
	Default
	{
		+NOSPRITESHADOW
		+SEEINVISIBLE

		RenderStyle "Shaded";
	
		SeeSound "spectre/sight";
		AttackSound "spectre/melee";
		PainSound "spectre/pain";
		DeathSound "spectre/death";
		ActiveSound "spectre/active";
	
		HitObituary "$OB_SPECTREHIT";
	}
	States
	{
	XDeath:
	Death:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
		SARG I 8 
		{
			A_FadeIn(0.5, FTF_CLAMP);
			A_SetRenderStyle(0.25, STYLE_Translucent);
		}
		SARG J 8 
		{
			A_SetRenderStyle(0.50, STYLE_Translucent);
			A_Scream();
		}
		SARG K 4 
		{
			A_FadeIn(1, FTF_CLAMP);
			A_SetRenderStyle(0.75, STYLE_Translucent);
		}
		SARG L 4 
		{
			A_SetRenderStyle(1, STYLE_Normal);
			A_NoBlocking();
		}
		SARG M 4;
		SARG N -1;
		Stop;
		
	Raise:
		SARG NMLKJI 5 A_FadeOut(0.1);
		"####" "#" 0 { bSPRITEFLIP = 0; A_SetRenderStyle(1, STYLE_Shaded); }
		Goto See;
	}
}
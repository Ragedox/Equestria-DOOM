Class EquestriaImp : DoomEquestriaMonster Replaces DoomImp
{
	Default
	{
		//$Title Imp
		Tag "$FN_IMP"; Obituary "$OB_IMP";
		
		Health 60;
		
		Height 56; Radius 12;
		Mass 100;
		Speed 8;
		PainChance 200;
		
		+FLOORCLIP
		+ROLLSPRITE +ROLLCENTER
		
		SeeSound "imp/sight";
		PainSound "imp/pain";
		DeathSound "imp/death";
		ActiveSound "imp/active";
		HitObituary "$OB_IMPHIT";
	}
	States
	{
	Spawn:
		IDLI AB 10 A_Look();
		Loop;
	
	See:
		TROO AABBCCDD 3 A_Chase();
		Loop;
		
	/*Idle:
		TROO AABBCCDD 3
		{
			A_Wander();
			A_Look();
		}
		TROO A 0 A_Jump(32, "Tired");
		Loop;
	
	Tired:
		IDLI AB 10 A_Look();
		TROO A 0 A_Jump(128, "Idle");
		Loop;*/
	
	Melee:
		TROO E 8 A_FaceTarget();
		TROO F 6 A_FaceTarget();
		TROO G 6
		{
			A_Recoil(-4);
			A_CustomMeleeAttack(3 * random(1, 8), "imp/melee", "monster/miss2", "", true);
		}
		TROO G 0 A_Stop();
		Goto See;
		
	Missile:
		TROO EF 8 A_FaceTarget(0, 270, 0, FAF_MIDDLE);
		TROO G 6 A_SpawnProjectile("DoomImpBall");
		Goto See;
	
	Pain.Kick:
		#### # 0 A_Recoil(5);
	Pain:
		TROO H 2;
		TROO H 2 A_Pain();
		Goto See;
	
	Death.Plasma:
		#### # 0 A_SetTranslation("Toasted");
		#### ##### 0 A_SpawnProjectile("SplashSmoke", random(-20, 20), random (-20, 20), random (0, 360), CMF_AIMDIRECTION, random(0, -90));
	Death:
		#### # 0 { bSPRITEFLIP = randompick(0, 1); }
		TROO I 5;
		TROO J 5 A_Scream();
		#### K 4 A_NoBlocking();
        #### K 1 A_CheckFloor(1);
		Wait;
		#### L 5;
		#### M -1;
		Stop;
		
	Death.Shotgun:
		#### # 0 A_JumpIfCloser(radius + 64, 3);
	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(radius + 32, "XDeath");
		#### # 0 A_JumpIfCloser(radius + 128, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,15,0,1);
			A_Recoil(2.5);
		}
		Goto Death;
		
	Classic.XDeath:
	Crush:
		TROO NOPQRST 4;
		TROO U -1;
		Stop;
		
	Real.XDeath:
		TROO U 0
		{
			A_FaceTarget();
			
			A_SpawnItemEx("Gib_ImpHead",0,0, 35, random(4, 8), random(4, 8), random(4, 8), frandom(0,360)); 
			A_SpawnItemEx("Gib_ImpTorso",0,0, 25, -4, 0, 4, 0);
			
			A_SpawnItemEx("Gib_ImpArm1",10,0, 25, random(1,2), random(1,2), random(1,2), -90);
			A_SpawnItemEx("Gib_ImpArm2",13,0, 25, random(1,2), random(1,2), random(1,2), 90);
			
			A_SpawnItemEx("Gib_ImpLeg1",10,0, 0, random(1,2), random(1,2), random(1,2), -90);
			A_SpawnItemEx("Gib_ImpLeg2",13,0, 0, random(1,2), random(1,2), random(1,2), 90);
			
			for (int i = 0; i < 10; i++)
			{
				A_SpawnProjectile("TinyGenericGib", FRandom(8, 56), FRandom(radius * -1, radius), FRandom (0, 360), CMF_AIMDIRECTION, FRandom (0, -180));
			}
			for (int i = 0; i < 5; i++)
			{
				A_SpawnProjectile("BloodTrail", 0, 0, FRandom (0, 360), CMF_AIMDIRECTION, FRandom (0, 360));
				A_SpawnProjectile("SmallBloodTrail", 0, 0, FRandom (0, 360), CMF_AIMDIRECTION, FRandom (0, 360));
				A_SpawnProjectile("EquestriaBlood", 0, 0, FRandom (0, 360), CMF_AIMDIRECTION, FRandom (0, 360));
			}
		}
		TROO NOPQRST 1;
		TROO U -1;
		Stop;
		
	Death.Berserk:
	XDeath.Berserk:
		"####" "#" 0 ThrustThingZ(0,30,0,1);
		
		"####" "#" 0 A_Jump(256, "MidAir");
		Wait;
	
	MidAir:
		TROO J 1 A_SetRoll((roll + 20));
		Loop;
		
	Crash.Berserk:
		"####" "#" 0 A_SetRoll(0);
		
		"####" "#" 0 A_Jump(256, "XDeath");
		Wait;
		
	Raise:
		TROO ML 8;
		TROO KJI 6;
		
		"####" "#" 0 { bSPRITEFLIP = 0; }
		Goto See;
		
	}
}

Class EquestriaImpBall : DoomImpBall replaces DoomImpBall
{
	Default
	{
		Radius 8; Height 16;
	
		DamageType "Fire";
		
		Decal "FireBallScorch";
		
		+ForceXYBillBoard
		+Bright
	}
	States
	{
	Spawn:
		BAL1 B 2 A_SpawnItem("SmokePuff",0, height / 2);
		BAL1 A 2 A_SpawnItem("FireBallTrail");
		Loop;
		
	Death:
		#### # 0 A_SpawnItem("SplashSmoke");
		BAL1 CD 6;
		BAL1 E 1 A_FadeOut(0.1);
		Wait;
	}
}

Class FireBallTrail : Trails
{
	Default { +ZDOOMTRANS }
	States
	{
	Spawn:
		BAL1 CD 3 Bright A_FadeOut(0.125);
		BAL1 E 1 Bright A_FadeOut(0.1);
		Wait;
	}
}
Class EquestriaCyberdemon : DoomEquestriaMonster replaces Cyberdemon
{
	Default
	{
	//$Title Tyrant
		Tag "$FN_CYBER"; Obituary "$OB_CYBER";
		
		Health 4000;
		
		Mass 1500;
		Scale 1.5;
		Height 165; Radius 45; //Height 110 * 1.5; Radius 30 * 1.5;

		Speed 18;
		PainChance 20;
		MinMissileChance 160;
		
		+BOSS +BOSSDEATH +E2M8BOSS +E4M6BOSS
		+MISSILEMORE
		+FLOORCLIP
		+NORADIUSDMG
		+DONTMORPH
		
		SeeSound "cyber/sight";
		PainSound "cyber/pain";
		DeathSound "cyber/death";
		ActiveSound "cyber/active";
	}
	States
	{
	Spawn:
		IDLY AB 10 A_Look();
		Loop;
	
	See:
		CYBR A 3 A_Hoof();
		CYBR ABBCC 3 A_Chase();
		CYBR D 3 A_Metal();
		CYBR D 3 A_Chase();
		Loop;
	
	Pain:
		CYBR G 10 A_Pain();
		Goto See;
		
	Missile:
		CYBR E 6 A_FaceTarget(100, 0, 0, 0, FAF_BOTTOM);
		CYBR F 12 A_SpawnProjectile("EquestriaRocket", 52 * 1.5, -26 * 1.5, 0, CMF_OFFSETPITCH, 2);
		CYBR E 12 A_FaceTarget(100, 0, 0, FAF_BOTTOM);
		CYBR F 12 A_SpawnProjectile("EquestriaRocket", 52 * 1.5, -26 * 1.5, random(-2, 2), CMF_OFFSETPITCH, 2);
		CYBR E 12 A_FaceTarget(100, 0, 0, 0, FAF_BOTTOM);
		CYBR F 12 A_SpawnProjectile("EquestriaRocket", 52 * 1.5, -26 * 1.5, random(-2, 2), CMF_OFFSETPITCH, 2);
		Goto See;
	
	XDeath:
	Death:
		CYBR GGGGGGG random(4,8)
		{
			A_SpawnItemEx("Equestria_BloodExplosion", Frandom(-45, 45), frandom(-45, 45), frandom(32, 150));
			A_SpawnItemEx("Equestria_Explosion", Frandom(-45, 45), frandom(-45, 45), frandom(32, 150));
		}
		CYBR HHII 2
		{
			A_SpawnItemEx("Equestria_BloodExplosion", Frandom(-45, 45), frandom(-45, 45), frandom(32, 150));
			A_SpawnItemEx("Equestria_Explosion", Frandom(-45, 45), frandom(-45, 45), frandom(32, 150));
		}
		CYBR J 8 A_Scream();
		CYBR KL 5
		{
			A_QuakeEx(4, 4, 4, 35, 0, 800, "world/quake", QF_SCALEDOWN, 0,0,0, 1);
			A_SpawnItemEx("Equestria_BloodExplosion", Frandom(-45, 45), frandom(-45, 45), frandom(32, 150));
			for (int i = 0; i < 16; i++)
			{
				A_SpawnItemEx("BigGoreRandom", frandom(-50, 50), frandom(-50, 50), frandom(32, 110),
					frandom(2,8),frandom(2,8),frandom(4,16),
					frandom(0, 360), SXF_USEBLOODCOLOR);
				A_SpawnItemEx("GenericGib", frandom(-50, 50), frandom(-50, 50), frandom(32, 110),
					frandom(8,16),frandom(8,16),frandom(8,24), 
					frandom(0, 360), SXF_USEBLOODCOLOR);
			}
		}
		CYBR M 5 A_NoBlocking();
		CYBR NO 5;
		CYBR P 30;
		CYBR P -1 A_BossDeath();
		Stop;
	}
}

Class Equestria_Explosion : Equestria_Particles
{
	States
	{
	Spawn:
		TNT1 A 0
		{
			A_QuakeEx(4, 4, 4, 16, 0, 512, "", QF_SCALEDOWN | QF_3D, 0,0,0, 1);
			A_StartSound("weapons/rocklx");
			
			for (int i = 0; i < 4; i++)
				A_SpawnProjectile("Smoke", 0, 0, frandom(0, 360), CMF_AIMDIRECTION,frandom(0, 360));
			
			bSPRITEFLIP = randompick(0,1);
			
			int gorequantity;
			gorequantity = random(8, 16);
				
			for (int i = 0; i < gorequantity; i++)
				A_SpawnItemEx("GenericGib",
					0,0,0,
					frandom(-4,4), frandom(-4,4), frandom(-8,8),
					frandom(0, 360), SXF_USEBLOODCOLOR | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_NOCHECKPOSITION);
				
			for (int i = 0; i < gorequantity; i++)
				A_SpawnItemEx("TinyGenericGib",
					0,0,0,
					frandom(-4,4), frandom(-4,4), frandom(-8,8),
					frandom(0, 360), SXF_USEBLOODCOLOR | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_NOCHECKPOSITION);

			for (int i = 0; i < gorequantity; i++)
			{
				A_SpawnItemEx("Equestria_TinyBlood",
					0,0,0,
					frandom(-4,4), frandom(-4,4), frandom(-8,8),frandom(0,360),
					SXF_USEBLOODCOLOR | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_NOCHECKPOSITION);
					
				A_SpawnItemEx("Equestria_SmallBlood",
					0,0,0,
					frandom(-4,4), frandom(-4,4), frandom(-8,8),frandom(0,360),
					SXF_USEBLOODCOLOR | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_NOCHECKPOSITION);
					
				A_SpawnItemEx("Equestria_Blood",
					0,0,0,
					frandom(-4,4), frandom(-4,4), frandom(-8,8),frandom(0,360),
					SXF_USEBLOODCOLOR | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_NOCHECKPOSITION, 128);
			}
		}
		TNT1 A 0 A_Jump(256, "Spawn1", "Spawn2");
		Wait;
		
	Spawn1:
		MISL B 6 Bright { bFORCEXYBILLBOARD = 1; }
		MISL C 4 Bright;
		MISL D 1 A_FadeOut(0.1);
		Wait;
		
	Spawn2:
		MISL E 4 Bright { bFORCEXYBILLBOARD = 0; }
		MISL FGHH 2 Bright;
		MISL I 1 A_FadeOut(0.1);
		Wait;
	}
}
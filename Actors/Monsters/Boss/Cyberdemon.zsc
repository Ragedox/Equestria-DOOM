Class EquestriaCyberdemon : DoomEquestriaMonster replaces Cyberdemon
{
	Default
	{
	//$Title Tyrant
		Tag "$FN_CYBER"; Obituary "$OB_CYBER";
		
		Health 4000;
		
		Mass 1500;
		Scale 1.5;
		Height 165; Radius 45; //Height 110 * 1.5; Radius 30 * 1.5;

		Speed 18;
		PainChance 20;
		MinMissileChance 160;
		
		+BOSS +BOSSDEATH +E2M8BOSS +E4M6BOSS
		+MISSILEMORE
		+FLOORCLIP
		+NORADIUSDMG
		+DONTMORPH
		
		SeeSound "cyber/sight";
		PainSound "cyber/pain";
		DeathSound "cyber/death";
		ActiveSound "cyber/active";
	}
	States
	{
	Spawn:
		IDLY AB 10 A_Look();
		Loop;
	
	See:
		CYBR A 3 A_Hoof();
		CYBR ABBCC 3 A_Chase();
		CYBR D 3 A_Metal();
		CYBR D 3 A_Chase();
		Loop;
	
	Pain:
		CYBR G 10 A_Pain();
		Goto See;
		
	Missile:
		CYBR E 6 A_FaceTarget(100, 0, 0, 0, FAF_BOTTOM);
		CYBR F 12 A_SpawnProjectile("EquestriaRocket", 52 * 1.5, -26 * 1.5, 0, CMF_OFFSETPITCH, 2);
		CYBR E 12 A_FaceTarget(100, 0, 0, FAF_BOTTOM);
		CYBR F 12 A_SpawnProjectile("EquestriaRocket", 52 * 1.5, -26 * 1.5, random(-2, 2), CMF_OFFSETPITCH, 2);
		CYBR E 12 A_FaceTarget(100, 0, 0, 0, FAF_BOTTOM);
		CYBR F 12 A_SpawnProjectile("EquestriaRocket", 52 * 1.5, -26 * 1.5, random(-2, 2), CMF_OFFSETPITCH, 2);
		Goto See;
	
	XDeath:
	Death:
		CYBR GGGGGGG random(4,8) A_SpawnItemEx("CyberdemonExplosion", Frandom(-50, 50), frandom(-90, 90), frandom(32, 150));
		CYBR HHII 2 A_SpawnItemEx("CyberdemonExplosion", Frandom(-50, 50), frandom(-50, 50), frandom(32, 150));
		CYBR J 8 A_Scream();
		CYBR KL 5
		{
			A_QuakeEx(4, 4, 4, 35, 0, 800, "world/quake", QF_SCALEDOWN, 0,0,0, 1);
			A_SpawnItemEx("CyberdemonExplosion", Frandom(radius * -1, radius), frandom(radius * -1, radius), frandom(32, 150));
			for (int i = 0; i < 20; i++)
			{
				A_SpawnProjectile("BigGoreRandom", frandom(32, 110 * 1.5), frandom(radius * -1, radius), frandom(0, 359), CMF_AIMDIRECTION, frandom(0, 359));
				A_SpawnProjectile("GenericGib", frandom(32, 110 * 1.5), frandom(radius * -1, radius), frandom(0, 359), CMF_AIMDIRECTION, frandom(0, 359));
			}
		}
		CYBR M 5
		{
			A_NoBlocking();
			A_SpawnProjectile("EquestriaBlood", frandom(32, 110 * 1.5), random(radius * -1, radius), random (0, 359), CMF_AIMDIRECTION, random (-180, 0));
			A_SpawnProjectile("BloodTrail", frandom(32, 110 * 1.5), random(radius * -1, radius), random (0, 359), CMF_AIMDIRECTION, random (-180, 0));	
			A_SpawnProjectile("SmallBloodTrail", frandom(32, 110 * 1.5), random(radius * -1, radius), random (0, 359), CMF_AIMDIRECTION, random (-180, 0));	
		}
		CYBR NO 5;
		CYBR P 30;
		CYBR P -1 A_BossDeath();
		Stop;
	}
}

Class CyberdemonExplosion : DEParticles
{
	Default
	{
		Speed 0;
		RenderStyle "Translucent"; Alpha 0.75;
		+NOINTERACTION
		+ZDOOMTRANS
		
		Height 0; Radius 0;
	}
	States
	{
	Spawn:
		TNT1 A random(0,8) NoDelay
		{
			A_SetScale(frandom(0.5, 1.0));
			A_SpawnItem("BloodExplosionSphere");
		}
		EXP2 A random(3,6) A_StartSound("weapons/rocklx");
		EXP2 B 4;
		EXP2 C 1 A_FadeOut(0.1);
		Wait;
	}
}
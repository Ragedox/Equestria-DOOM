Class DebrisCore : Actor abstract
{
	int rollspd;

	property RollSpeed: rollspd;
	
	//string rollstt;
	
	//property RollState: rollstt;
	
	Default
	{
		+Missile
		+Corpse
		+NotAutoAimed
		+DropOff
		+NoBlockMap +NoTeleport +NoTrigger +NoBlockMonst
		+ForceXYBillboard
		+MoveWithSector
		+CanPass
		+ThruActors
		+ClientSideOnly
		+NOTONAUTOMAP
		
		-SOLID
		
		+ROLLSPRITE +ROLLCENTER
		
		DistanceCheck "CV_SFXDISTANCE";
		
		DebrisCore.RollSpeed 16;
		Damage 0;
		//DebrisCore.RollState "Roll";
	}
	override void Tick()
	{
		if (InStateSequence(CurState, ResolveState("Roll")) && !isFrozen())
			A_SetRoll((roll + rollspd), SPF_INTERPOLATE);
		
		Super.Tick();
	}
}

Class TerrainDebris : DebrisCore
{
	Default
	{
		DebrisCore.RollSpeed 8;
		Speed 4;
	}
	States
	{
	Spawn:
		DBRA ABCDEF 1;
		Loop;
	}
}

class SkyViewPointStatic : SkyViewPoint replaces SkyViewPoint
{
    PlayerPawn base;
    Vector3 basepos;
    int scaling;

    Default
    {
        //$Arg1 "Skybox Scene Scale"
        //$Arg1Tooltip "The scale of the skybox's scene (default is 100).  The larger this number is, the farther away the skybox contents will appear to be."
    }

    override void PostBeginPlay()
    {
        // Set the scaling value according to whatever arg 1 value is passed; use 100 if nothing provided
        scaling = args[1] == 0 ? 100 : args[1];

        // Save the spawn location vector for later
        SpawnPoint = pos;

        // Iterate through all of the possible players in the game
        for (int i = 0; i < MAXPLAYERS; i++)
        {
            // If a player is in the game and has spawned...
            if (playeringame[i] && players[i].mo)
            {
                if (!base) { base = players[i].mo; } // Set the skybox to follow the first player who is in the game
                else { base = null; break; } // If there are multiple players, don't move the skybox
            }
        }
    }

    override void Tick()
    {
        if (base)
        {
            Vector3 newvel;

            // Reset the viewpoint's height location
            double heightdelta = (base.pos.z - base.floorz) / scaling;
            SetOrigin((pos.x, pos.y, SpawnPoint.z + heightdelta), true);

            // Only move the viewpoint on the x/y axis if the player actually is moving along that axis; otherwise stay still
            if (basepos.x != base.pos.x) { newvel.x = base.vel.x; }
            if (basepos.y != base.pos.y) { newvel.y = base.vel.y; }
            newvel.z = -base.vel.z;

            // Adjust the move amount by scaling value
            newvel /= scaling;
            A_ChangeVelocity(newvel.x, newvel.y, newvel.z, CVF_RELATIVE | CVF_REPLACE);

            // Save the player's old position for the move checks
            basepos = base.pos;
        }

        Super.Tick();
    }
}

Class InvToken : Inventory
{	Default {	Inventory.MaxAmount 1;	+INVENTORY.QUIET; -INVENTORY.INVBAR;	}}

Class HUDDisabled : InvToken {}

Class Fall : InvToken {}
Class LowHealthCheck : InvToken	{}
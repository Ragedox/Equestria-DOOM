actor FDEBFG9000 : DoomEquestriaWeapon replaces BFG9000
{
//$Title Big Fucking Gun 9000
	Height 20
	
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 0.75
	
	Weapon.SelectionOrder 2800
	Weapon.AmmoUse 40
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
	
	+WEAPON.NOAUTOFIRE
	
	Inventory.PickupMessage "$GOTBFG9000"
	
	States
	{
	
	Spawn:
		BFUG A -1
		Stop
	
	Ready:
		BFGG A 1 A_WeaponReady
		Loop
	
	Select:
		TNT1 A 0 A_TakeInventory("KickCheck", 999)
	
		BFGG A 0 A_SetCrossHair(14)
		BFGG A 1 A_Raise
		Wait

	Deselect:
		TNT1 A 0 A_TakeInventory("KickCheck", 999)
		
		BFGG A 1 A_Lower
		Wait

	Fire:
		TNT1 A 0 A_JumpIf(callACS("CamRecoilToggle")==1,"Fire.CamRecoil")
		
		BFGG A 10 A_BFGSound
		BFGG A 10
		BFGG B 10
		{
			A_WeaponOffset(0, 35, WOF_KEEPX | WOF_INTERPOLATE);
			A_Overlay(-10, "Flash");
		}
		BFGG C 4
		{
			A_WeaponOffset(0, 40, WOF_KEEPX | WOF_INTERPOLATE);
			A_FireBFG;
			//A_SetBlend("green", 0.5, 16);
		}
		BFGG C 6
		BFGG B 20
		{
			A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
			A_ReFire;
		}
		Goto Ready
		
	Fire.CamRecoil:	
		BFGG A 10 A_BFGSound
		BFGG A 10 A_QuakeEx(5, 4, 1, 20, 0, 1, "", QF_SCALEUP | QF_RELATIVE)
		BFGF D 10
		{
			A_WeaponOffset(0, 35, WOF_KEEPX | WOF_INTERPOLATE);
			A_Overlay(-10, "Flash");
		}
		BFGF D 4
		{
			A_QuakeEx(5, 0, 4, 4, 0, 1, "", QF_RELATIVE | QF_SCALEDOWN);
			A_WeaponOffset(0, 40, WOF_KEEPX | WOF_INTERPOLATE);
			A_FireBFG;
			A_SetPitch(pitch-3);
			A_ZoomFactor(0.85);
			//A_SetBlend("green", 0.5, 16);
		}
		BFGG DDDDD 1
		{
			A_SetPitch(pitch+0.60, SPF_INTERPOLATE);
			A_ZoomFactor(1);
		}
		BFGG B 21
		{
			A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
			A_ReFire;
		}
		Goto Ready
	
	Flash:
		BFGG A 0 A_OverlayOffset(-10, 0, 12, WOF_KEEPX)
		
		BFGG B 6 Bright A_Light(4)
		BFGF A 5 Bright A_Light(8)
		BFGF B 8 Bright A_Light(10) 
		BFGG D 4 Bright A_Light(6) 
		BFGG D 5 Bright 
		Goto LightDone
	
	OldFire:
		BFGG A 10 A_BFGSound
		BFGG BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBB 1 A_FireOldBFG
		BFGG B 0 A_Light0
		BFGG B 20 A_ReFire
		Goto Ready
	}
}

actor DEBFGBall : BFGBall replaces BFGBall
{
	Height 36
	Radius 18
	
	RenderStyle Add
	Alpha 1
	
	+FORCEXYBILLBOARD
  
	+ROLLSPRITE +ROLLCENTER
	
	States
	{
	Spawn:
		BFS1 A 0
		BFS1 A 0 A_SetRoll(random(-359, 359))
	Projectile:
		BFS1 ABAB random(2, 4) Bright
		{
			A_SpawnItemEx("BFGTrail");
		}
		Loop
	
	Death:
		BFE1 A 2 
		{
			A_SetRoll(0);
			A_SetScale(1.50);
			A_SpawnItem("BFGShockwave");
			
			A_SpawnItemEx("BFGFloorCheck", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
			
			A_QuakeEx(8, 8, 8, 35, 0, 1000, "world/quake", QF_SCALEDOWN, 0,0,0, 1);
		}
		BFE1 BCD 2 Bright
		BFE1 E 8 Bright A_BFGSpray
		BFE1 FGHIJ 3 Bright
		BFE1 K 1 Bright A_FadeOut(0.1)
		Wait
	}
}

Actor BFGTrail : Trails
{
	States
	{
	Spawn:
		BFS1 A 0
		BFS1 A 0 A_SetRoll(random(-359, 359))
		
	Trail:
		BFS1 A random(2,4) Bright A_FadeOut(0.1)
		BFS1 B random(2,4) Bright A_FadeOut(0.1)
		BFS1 C random(2,4) Bright A_FadeOut(0.1)
		BFS1 D random(2,4) Bright A_FadeOut(0.1)
		Loop
   }
}

actor BFGShockwave
{
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+NOGRAVITY
	+NOCLIP
	RenderStyle Add
	Alpha 0.50
	scale 0.70
	States
	{
	Spawn:
		BFEF EGIKMOQSUWY 1 Bright
		BFEF Z 1 Bright A_FadeOut (0.1)
		Wait
	}
}

actor FDEDualChaingun : DoomEquestriaWeapon 11002
{
//$Title Chaingun
	Weapon.BobStyle InverseSmooth
	Weapon.SelectionOrder 700
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	
	Inventory.PickupMessage "You got the Dual Miniguns!"
	Inventory.PickupSound "pickup/chaingun"
	
	Obituary "%o was obliterated down by %k's Dual Miniguns."
	
	Tag "Dual Miniguns"
	
	States
	{
	
	Spawn:
		MGUN A -1
		Stop
	
	Ready:
		TNT1 A 0 A_JumpIfInventory("KickCheck", 1, "Kick.Normal")
		
		MNGG A 1 A_WeaponReady
		Loop
	
	Select:
		MNGG A 0 A_SetCrossHair(11)
		
		MNGG A 1 A_Raise
		Wait
		
	Deselect:
		TNT1 A 0 A_TakeInventory("KickCheck", 999)
		
		MNGG A 1 A_Lower
		Wait
	
	Fire:
		TNT1 A 0 A_JumpIf(callACS("CamRecoilToggle")==1,"Fire.CamRecoil")
		
		MNGG A 2
		{
			A_FireBullets(5.6, 0, -1, 5, "EquestriaBulletPuff");
			A_FireBullets(5.6, 0, -1, 5, "EquestriaBulletPuff");
			A_GunFlash("Flash2");
			A_PlaySound("weapons/chngun", CHAN_WEAPON);
		}
		
		MNGG B 2
		
		MNGG A 2 
		{
			A_FireBullets(5.6, 0, -1, 5, "EquestriaBulletPuff");
			A_FireBullets(5.6, 0, -1, 5, "EquestriaBulletPuff");
			A_GunFlash("Flash");
			A_PlaySound("weapons/chngun", CHAN_WEAPON);
		}
		
		MNGG B 2
		MNGG A 0 A_ReFire
		Goto Ready
		
	Fire.CamRecoil:
		MNGG A 2
		{
			A_FireBullets(5.6, 0, -1, 5, "EquestriaBulletPuff");
			A_FireBullets(5.6, 0, -1, 5, "EquestriaBulletPuff");
			A_GunFlash("Flash2");
			A_PlaySound("weapons/chngun", CHAN_WEAPON);
			A_QuakeEx(1, 0, 1, 4, 0, 1, "", QF_RELATIVE | QF_SCALEDOWN | QF_SCALEUP);
			A_SetPitch(pitch-1, SPF_INTERPOLATE);
		}
		MNGG A 0 A_SetPitch(pitch+1, SPF_INTERPOLATE)
		MNGG B 2
		MNGG A 2 
		{
			A_FireBullets(5.6, 0, -1, 5, "EquestriaBulletPuff");
			A_FireBullets(5.6, 0, -1, 5, "EquestriaBulletPuff");
			A_GunFlash;
			A_PlaySound("weapons/chngun", CHAN_WEAPON);
			A_QuakeEx(1, 0, 1, 4, 0, 1, "", QF_RELATIVE | QF_SCALEDOWN | QF_SCALEUP);
		}
		MNGG B 2
		MNGG B 0 A_ReFire
		Goto Ready
	
	Flash:
		MNGF A 1 Bright A_WeaponOffset(0, 38, WOF_KEEPX | WOF_INTERPOLATE)
		Goto LightDone
	
	Flash2:
		TNT1 A 1 Bright 
		MNGF A 1 Bright A_WeaponOffset(0, 38, WOF_KEEPX | WOF_INTERPOLATE)
		Goto LightDone
	
	}
}
